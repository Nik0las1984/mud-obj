#6300
вошли к монаху~
0 r 100
~
wait 4
крест %actor.name%
say В недобрый час и в недобром месте пришлось встретиться нам!
wait 1
взд
say Эта разрушенная сторожевая башня стала местом сосредоточения темных сил.
say Где-то наверху башни вершит свой темный ритуал осквернитель неупокоенных трупов.
wait 3
взд
say Сможешь ли ты наказать Зло? 
вопр %actor.name%
end
~
#6301
сказал смогу~
0 d 100
смогу~
wait 2
say Я знал, что ты согласишься помочь.
if (%actor.sex% == 1)
  say Но будь осторожен, обитатели Царства мертвых тоже разгневаны
else
  say Но будь осторожна, обитатели Царства мертвых тоже разгневаны
end
say Они послали навью наказать осквернителя трупов, но для нее любой живой - враг!
say Смотри не попадись ей под руку!
wait 4
say Прежде чем разделаешься с осквернителем, тебе придется сразиться с нежитью
say Осквернитель успел поднять несколько умертвий. Теперь они выполняют его волю!
wait 5
say Этот злодей не имел бы такой силы, если бы не похитил из Храма плащаницу.
say Теперь этот святой дар он использует для усиления своих заклятий.
say Вернувшему плащаницу воздастся за труды его!
attach 6302 %self.id%
detach 6300 %self.id%
detach 6301 %self.id%
end
~
#6302
отдали плащаницу~
0 j 100
~
wait 1
if (%object.vnum% == 6310)    
  wait 1 
  %purge% %object%
  say Я верил в тебя!
  рад %actor.name%
  wait 1
  say Вот, прими за труды свои... тут немного, но это от чистого сердца.
  if (%random.1000% <= 75)
    mload obj 1716
    дать пергамент .%actor.name%
  else
    msend %actor% Седой монах дал небольшую кучку кун.
    %actor.gold(+150)%
  end
end
detach 6302 %self.id%
~
#6303
убили осквернителя~
0 f 100
~
if %world.curobjs(6310)% < 1
  mload obj 6310
end
if %random.100% <= 75
  if %world.curobjs(592)% < 1
    mload obj 592
  end
end
~
#6304
убили навью~
0 f 100
~
if (%world.curobjs(6309)% < 12) && (%random.100% <= 15)
  mload obj 6309
  mecho Навья, испустив истошный крик, растворилась в пустоте.
  mecho А на том месте, где она только что стояла, появились добротные сапоги.
end
~
#6305
убили силуэт~
0 f 100
~
if (%world.curobjs(6306)% < 10) && (%random.100% <= 15)
  mload obj 6306
  mecho Темный силуэт исчез, словно его никогда и не было
  mecho А на его месте что-то сверкнуло золотым отблеском.
end
~
#6306
убили умертвие~
0 f 100
*~
return 0
mecho Умертвие исчезло в ослепительной вспышке.
if (%world.curobjs(6307)% < 12) && (%random.100% <= 20)
  mload obj 6307
  set load 1
end
if ( %world.curobjs(6308)% < 4 ) && ( %random.100% <= 8 )
  mload obj 6308
  set load 1
end
if (%load% == 1)
  mecho А на его месте остался лежать какой-то предмет.
end
~
#6307
убили шершня~
0 f 100
~
if (%world.curobjs(6303)% < 12) && (%random.10% <= 2)
  mload obj 6303
end
~
$~
