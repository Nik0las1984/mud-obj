#189200
даем яд Евпатору~
0 j 100
~
context 1892
if (%actor.id%!=%quest1892.id%)
  halt
end
if (%object.vnum%!=189105)
  wait 3s
  г Хм.. Не похоже на то, что я просил
  брос %object.name%
  halt
else 
  wait 2s
  г Похоже на то, что надо.
  г Спасибо, и прощай...
  %echo% Евпатор бросил крупинку яда в бокал с вином, и залпом осушил его...
  %purge% %object%
  %echo% Бокал выпал из рук царя и покатился по каменным плитам пола...
  %echo% Великий государь и полководец закрыл глаза и обмяк на троне.
  wait 10s
  %echo% Евпатор открыл глаза, и разочарованно посмотрел по сторонам...
  г Какой странный яд, не действует совсем...
  г %quest1892.name%! Взялся за дело - доведи до конца!
  г Держи мой меч, я не буду сопротивляться.
  %load% obj 189209
  дать меч .%quest1892.name%
  г Рази! 
  attach 189206 %self.id%
  detach  189205 %self.id%
  detach  189200 %self.id%
end
~
#189201
триг на ресет~
2 f 100
~
wdoor 189229 e purge
calcuid r189229 189229 room
detach 189202 %r189229.id%
calcuid m189201 189201 mob
detach 189206 %m189201.id%
detach 189205 %m189201.id%
attach 189204 %m189201.id%
attach 189203 %m189201.id%
context 1892
set quest1892 0
remote quest1892 %m189201.id%
~
#189202
триг на гобелен~
2 c 0
сорвать дернуть потянуть~
if !%arg.contains(гобелен)%
  wsend %actor% Что Вы хотите сорвать? Крышу?
  halt
end
if %exist.mob(189200)%
  eval guard %world.mob(189200)%
  %teleport% %guard% 189229
  if %guard.realroom% == 189229
    wecho %guard.name% сказал:'Ах ты гад! Удумал испортить дворцовый интерьер???'
    wforce %guard% mkill %actor%
  end
end
wdoor 189229 e room 189235
detach 189202 %self.id%
~
#189203
Евпатор приветствует~
0 q 100
~
wait 2s
г Приветствую тебя, %actor.name%
wait 2s
г Не самое лучшее время для гостей...
г Слышал%actor.g% небось, проиграли мы войну-то римлянам
г Войско разбито, одна лишь охрана во дворце осталась.
г Хоть в петлю лезь...
дрож
г Нет, кажется есть способ менее страшный.
г Принеси мне яду, не откажи, в городе Пантикапее есть знахарь один, иди к нему,
г скажешь, что от меня, он приготовит зелье.
wait 1s
г Ну что, согласишься помочь?
г Хотя, как вариант, можешь попытаться меня убить, но учти, биться буду всерьез
wait 1s
г Думай, не тороплю.
~
#189204
выдача квеста~
0 d 1
да согласен попробую запросто помогу~
context 1892
if (%quest1892% == 0)
  wait 1s
  set quest1892 %actor%
  calcuid mage1891 189124 mob
  remote quest1892 %mage1891.id%
  remote quest1892 %self.id% 
  тоск
  г Спасибо, что не отказал%quest1892.g%, хотя умирать и не хочется.
  г Ну что же, иди, да не задерживайся особо, а то передумаю...
  wait 1s
end
attach 189205 %self.id%
attach 189200 %self.id%
detach 189203 %self.id%
detach 189204 %self.id%
~
#189205
в ожидании квестера~
0 r 100
~
wait 1s
context 1892
if (%actor.name%==%quest1892.name%)
  г Заждался я тебя, %actor.name% !
  г Ну, давай что принес...
  halt
else 
  г Слушай, %actor.name%, ты там в городе %quest1892.vname% не встречал%actor.g%?
  г Ушел - и поминай как звали!
end
~
#189206
пытаемся убить Евпатора~
0 c 1
ударить убить пнуть рубануть заколоть~
context 1892
if !%arg.contains(Евпатора)%
  %send% %actor% На кого Вы хотите совершить нападение?
  halt
end
calcuid o189209 189209 obj 
if (%quest1892.eq(16)%==%o189209%)
  г Прекрасный удар! Смерть моя близка, в благодарность за услугу хочу шепнуть тебе кое-что...
  шепт .%quest1892.name%, Там, за гобеленом, есть тайный проход
  шепт .%quest1892.name%, Сходи, если духов не боишься...
  г Спасибо тебе, %quest1892.name%, и прощай!
  calcuid r189229 189229 room
  attach 189202 %r189229.id%
  %purge% %self%
else
  крич Нет, от твоего поганого оружия я смерть не приму!!!
  %kill% .%actor.name%
end
~
#189207
смерть деметры~
0 f 100
*~
if (%world.curobjs(189204)% < 4) && (%random.10% <= 10)
  mload obj 189204
end
if (%world.curobjs(189205)% < 4) && (%random.10% <= 10)
  mload obj 189205
end
if (%world.curobjs(189206)% < 10) && (%random.10% <= 10)
  mload obj 189206
end
if (%world.curobjs(189207)% < 10) && (%random.10% <= 10)
  mload obj 189207
end
if (%world.curobjs(189208)% < 1) && (%random.10% <= 10)
  mload obj 189208
end
~
$~
