#9400
приветствие лесничего~
0 r 100
~
if (%questor94% == %nil%)
  wait 1s
  say Приветствую тебя, %actor.name%!
  ул
  say Не можешь ли ты мне помочь?
else
  wait 1s
  say %actor.name%! Ты уже выполнил мою просьбу?
  вопр %actor.name%
end
~
#9401
соглашаемся помочь лесничему~
0 d 0
могу смогу помогу~
if (%questor94% == %nil%) 
  wait 1s
  say Чудесно! Вот, послушай.
  say Было хорошо, значиться, в лесу-то моем. Птички, цветочки. Было, да быльем поросло!
  say Завелись у нас в лесу браконьеры. И теперь лес не тот, что был раньше.
  wait 1s
  взд
  say Зверья мало стало, деревья погибают... кончно! Им же жить надо!
  say Им наплевать что их дрова - некогда живые существа!
  wait 1s
  say В общем отмсти им за все те жизни, которые они погубили. Хорошо отмсти!
  say А я уж в долгу не останусь...
  makeuid questor94 %actor.id%
  global  questor94
  set questor01 %actor%
  global questor01
  wait 1s
  эм осторожно коснулся Вашего подбородка
  dg_cast 'защита' %actor.name%
  wait 1s
  say Вот, это единственные чары, которые я могу на тебя наложить...
  say А теперь ступай!
else
  wait 1s
  дум
end
~
#9402
ворон на дереве~
2 e 100
~
wsend %actor% Большой черный ворон внимательно смотрит на ваши попытки залезть на дерево.
wsend %actor% Когда ваша рука дотянулась до ветки, на которой он сидел,
if (%random.100% < 34)
  wsend %actor% вместо того что бы улететь, ворон хрипло каркнул и внезапно клюнул вас по пальцу.
  wsend %actor% Скорее от неожиданности, чем от боли, вы отпустили ветку и со сдавленным криком полетели вниз.
  eval buf %actor.hitp(-50)%
  wteleport %actor% 9444
  return 0
  wat 9444 wechoaround %actor.name% %actor.name% со сдавленным воплем упал%actor.g% сверху.
else
  wsend %actor% ворон недовольно каркнул, испражнился на Вас и перелетел на соседнее дерево.
end
~
#9404
получаем награду~
0 d 1
да~
if (%questor01.id% != %actor.id%)
  halt
end
if %exist.mob(9409)% || %exist.mob(9410)% || %exist.mob(9411)% || %exist.mob(9412)%
  wait 1s
  say Что-то подсказывает мне, что ты мне врешь! 
  эм отвернулся от Вас
  *по той же причине - лучше проверять, что что-то НЕ сделано и обрубать выполнение, есдли это надо
  halt
end
wait 1s
сия
say Да, ты, %actor.name%, потрудил%actor.u% на славу! Спасибо тебе, век не забуду!
wait 1s
if ((%random.3% == 1) && (%world.curobjs(9407)% < 5 ))
  mload obj 9407
  дат посох %actor.name%
else
  %self.gold(+300)%
  дат 300 кун %actor.name%
end
detach 9404 %self.id%
~
#9405
копаем клад~
2 c 100
раскопать~
if %arg% == землю
  if %actor.move% > 20
    wsend %actor% Немного поколебавшись, Вы все-таки начали копать землю. 
    wsend %actor% Спустя буквально пару минут Вы нашли кем-то спрятанный мешочек золота!
    wechoaround %actor% %actor.name% начал%actor.g% копать землю и внезапно откопал%actor.g% небольшой мешочек золота!
    eval buf %actor.move(-20)%
    %load% obj 9409
    attach 9406 %self%
    detach 9405 %self%
  else
    wsend %actor% Вы слишком устали для сего действа...
  endif
else
  wsend %actor% ЧТО бы такое раскопать?
end
~
#9406
копаем клад 2~
2 c 100
раскопать~
if %arg% == землю
  if %actor.move% > 20
    wsend %actor% Вы усиленно начали рыться в земле, но, как и следовало ожидать, ничего не нашли.
    wechoaround %actor% %actor.name% зачем-то начал%actor.g% раскапывать землю. Странно...
    eval buf %actor.move(-20)%
  else
    wsend %actor% Вы слишком устали для сего действа...
  end
else
  wsend %actor% ЧТО бы такое раскопать?
end
~
#9407
репоп зоны~
2 f 100
~
detach 9406 %self.id% 
attach 9405 %self.id% 
~
$~
