#24200
КуликПрилетел~
2 e 100
~
wait 1s
wecho _Прилетел кулик из-за моря!
wait 1s
wecho _Принес золотые ключи, замкнул холодную зиму, отомкнул землю-матушку!
wecho _Выпустил из неволья воду, траву, да теплое время.
wecho _И перекатилось солнце из зари в зарю, 
wait 1s
wecho _Повеяли нежные ветры, 
wait 1s
wecho _Пробудили чистое поле!
*лоад кулика
if !%exist.mob(24202)% 
  wload mob 24202
end
detach 24200 %self%
~
#24201
ЛоадЗолотыхКлючей~
0 n 100
~
mload obj 24200
~
#24202
БьемКулика~
0 l 100
~
*wait 1s
mecho _Взмыл кулик, птичка ранняя, в небо ясное - только Вы его и видели.
бро все
wait 1
%purge% %self%
~
#24203
ВстречаетКострома1~
0 q 100
~
wait 1s
mecho _Чуть только лес оделся листочками и теплое небо завилось белесыми хохолками,
wait 1s
mecho _Сбросила Кострома свою колючку - ежовую шубку.
*бросаем шубу
if ((%random.1000% < 401) && (%world.curobjs(24201)% < 6))
  mload obj 24201
  бро все
end
wait 1s
mecho _Протерла глазoньки, да на все четыре стороны кинулась, 
wait 1s
mecho _Ищи теперь ее - ветра в поле!
wait 1s
mecho _А без нее Лета-Красного не отомкнешь!
%teleport% %self% 24207
attach 24204 %self%
detach 24203 %self%
~
#24204
ВстречаетКострома2~
0 q 100
~
wait 1s
mecho _Валяется на полюшке Кострома - брюшко себе лапкой почесывает, 
mecho _Брюшко у Костромы мяконькое, переливается.
wait 1s
mecho _Нагрянули на лужайку звери лесные, 
wait 1s
mecho _Взяли друг дружку за лапы, да кругом вкруг Костромушки и пошли плясать!
wait 1s
*лоад зверей
if !%exist.mob(24206)% 
  mload mob 24206
  calcuid zveri 24206 mob
end
mforce %zveri% эм пляшут и пляшут, поют песенку
wait 1s
эм лежит, лежона-нежона, нежится, валяется.
wait 1s
mforce %zveri% г Дома Кострома?
г Дома
wait 1s
mforce %zveri% г Что она делает?
г Спит
wait 1s
mecho _Засмеялись звери весело, схватили Кострому и куда-то потащили.
*телепорт костромы
%teleport% %self% 24211
%teleport% %zveri% 24211
attach 24206 %self%
detach 24204 %self% 
~
#24205
РезетЗоны~
2 f 100
~
*кулик
calcuid nTroom 24202 room
attach 24200 %nTroom% 
*кострома
calcuid kostroma 24204 mob
attach 24203 %kostroma%
wait 1
*входы
wdoor 24223 east purge 
wdoor 24249 east purge
wdoor 24236 down purge
wdoor 24269 down purge
wdoor 24296 east purge
wdoor 24252 east purge
*алена с окружением
calcuid nTroom1 24243 room
attach 24225 %nTroom1%  
*змея с окружением
calcuid nTroom2 24244 room
attach 24227 %nTroom2%  
wait 1
*дева краса
calcuid deva 24224 mob
attach 24224 %deva%
*водяники
calcuid nTroom3 24205 room
attach 24214 %nTroom3%   
calcuid nTroom4 24225 room
attach 24214 %nTroom4%
wait 1
*белка
calcuid belka 24249 mob
attach 24239 %belka%
*заяц
calcuid zayaz 24268 mob
attach 24264 %zayaz%
*лебедь
wait 1 
calcuid lebed 24212 mob
attach 24286 %lebed%
*заглядываем за печь
calcuid nTroom4 24236 room
attach 24248 %nTroom4%   
*
wait 1
*встаем за дерево
calcuid nTroom5 24264 room
attach 24242 %nTroom5%
*убираем выход из 64
wdoor 24264 south purge
*Маруна
calcuid marun 24275 mob
attach 24271 %marun%
~
#24206
ВстречаетКострома3~
0 q 100
~
wait 1s
mecho _Кружатся, вертятся, ножками топают-притопывают звери лесные вокруг Костромы-Костромушки, 
wait 1s
mecho _А голосочки, как бубенчики, и звенят и заливаются.
mecho _Не угнаться и птице за такими плясунами.
wait 1s
mforce звери г Дома Кострома?
г Дома
wait 1s
mforce звери г Что она делает?
г Встает.
wait 1s
mecho _Встает Кострома, подымается на лапочки, обводит глазoньками, 
mecho _Поводит желтеньким усиком, прилаживается: кого бы ей наперед поймать.
wait 1s
mecho _Разбежались звери в страхе, а Кострома за ними ускакала!
*телепорт костромы
%teleport% %self% 24216
if %exist.mob(24206)% 
  calcuid zveri 24206 mob
  %teleport% %zveri% 24216
end
attach 24207 %self%
detach 24206 %self%
~
#24207
ВстречаетКострома4~
0 q 100
~
if %exist.mob(24206)% 
  calcuid zveri 24206 mob
end
wait 1s
mecho _Круг за кругом ходят по солнцу вкруг Костромушки, 
mecho _Допытывают звери: что Кострома поделывает?
wait 1s
mforce %zveri% г Дома Кострома?
г Дома
wait 1s
mforce %zveri% г Что она делает?
г Чешется.
wait 1s
mecho _А Кострома-Костромушка почесалась, и попила, и поела, и в баню пошла, 
mecho _А из бани вернулась, села чай пить, 
mecho _Чаю попила, прикoрнула на немножечко, встала и гулять ушла!
*телепорт костромы
%teleport% %self% 24218
%teleport% %zveri% 24218
attach 24208 %self%
detach 24207 %self%
~
#24208
ВстречаетКострома5~
0 q 100
~
if %exist.mob(24206)% 
  calcuid zveri 24206 mob
end
wait 1s
mecho _Кружатся звери лесные вокруг Костромы-Костромушки.
mecho _За бочок щиплют, за ушки таскают!
wait 1s
mforce %zveri% г Дома Кострома?
г Дома
wait 1s
mforce %zveri% г Что она делает?
г Померла
wait 1s
mecho &YЗакричали звери громким голосом: Померла Кострома, померла!
wait 1s
mecho _Поднялся вокруг такой крик и визг, что сами звери-зверюшки испугались!
wait 1s
mecho _Бросились все взахлес на мертвую Костромушку, 
mecho _Подняли ее к себе на загривку и понесли хоронить к ключику.
wait 1s
mecho _Несли, несли и уронили!
wait 1s
mforce %zveri% эм покачали головами, помахали лапами, да и плюнули на это дело - разбежались
if %zveri%
  %purge% %zveri%
end
*лоад трупа костромы
calcuid kroom 24218 room
exec 24210 %kroom%
wait 1s
%purge% %self%
~
#24209
ОживаетКострома~
2 e 100
~
if !%actor.haveobj(24202)%
  halt
end
*
wait 1s
*убираем труп
calcuid trup 24202 obj
%purge% %trup%
wecho _Вот и поле прошли, полянку пробежали, лесок за спиной оставили.
wecho _Вот и овражек, вот ключик - и бежит и недвижен - серая звездочка.
wait 1s
wecho _И вдруг раскрыла Кострома свои мертвые глазыньки, пошевелила мохнатыми ушками.
wecho &YЗакричал%actor.g% %actor.name% громким голосом: Ожила Кострома, ожила!&n
wait 1s 
if %exist.mob(24201)%
  calcuid egor 24201 mob
  wforce %egor% эм улыбнулся в усы
  wait 1s
  wforce %egor% эм ударил кнутом по весенней земле - пора на лето поворачивать!  
  *егорий уходит на восток
  wforce %egor% восток
  attach 24212 %egor%
  attach 24213 %egor%
end 
*лоад костромы
if !%exist.mob(24213)%  
  wload mob 24213
  calcuid kostroma 24213 mob
end
wait 1s
wecho _Мигом вскочила Костромушка, да припала к ключику.
wait 3s
wecho _Напилась Кострома вдоволь.
wait 1s
wforce %kostroma% эм утерла мордочку лапами
wait 1s
wforce %kostroma% щек .%actor.name%
wait 1s
wforce %kostroma% хих
wforce %kostroma% г Вот подарочек от меня возьми!
wforce %kostroma% дать все .%actor.name%
*награда
* 
~
#24210
ЛоадТрупаКостромы~
2 z 100
~
wload obj 24202
~
#24211
НаградаКостромы~
0 n 100
~
if ((%random.1000% < 501) && (%world.curobjs(24203)% < 6))
  mload obj 24203
  halt
end 
if ((%random.1000% < 501) && (%world.curobjs(24204)% < 6))
  mload obj 24204
  halt
end 
if ((%random.1000% < 501) && (%world.curobjs(24205)% < 6))
  mload obj 24205
  halt
end 
mload obj 24206
~
#24212
ВошлиКЕгорию~
0 q 100
~
wait 1s
эм развел руки в стороны
wait 1s
г Чтобы лето отомкнуть одного желания мало!
г Ключи надобно золотые, что кулик из-за моря принес, да небо ими отомкнул.
г Добудешь их, так смогу в лето дорогу указать!
~
#24213
ДаемКлючиЕгорию~
0 j 100
~
if (%object.vnum% != 24200)
  say Это мне без надобности
  return 0
  halt
end
wait 1
mpurge %object%
wait 1s
detach 24212 %self%
mforce %actor% г Егорий, вставай рано - отмыкай землю,
mforce %actor% г Выпускай росу на теплое лето, 
mforce %actor% г На буйное жито -
mforce %actor% г На ядренистое, на колосистое
wait 1s
кив
wait 1s
mecho _Загремел золотыми ключами Егорий...
mecho _Повалили валом густые облака, прорвался летний дождь.
mecho _Ветром повеяло, грянул гром!
*открываем вход в лето
mdoor 24223 east room 24224 
detach 24213 %self%
~
#24214
ВылазятВодяники~
2 e 30
~
wait 1s
if (%world.curmobs(24217)% < 7)
  wecho _Вылезли на берег водяники, сняли с себя тину, сели на колоды и поплыли.
  wload mob 24217
  wload mob 24217
  wload mob 24217
end
detach 24214 %self%
~
#24215
УмерлаЧуяЗмея~
0 f 100
~
*трансформ черепа
calcuid kroom 24204 room
exec 24216 %kroom% 
~
#24216
ТрансформЧерепа~
2 z 100
~
if (%world.curobjs(24207)% < 1)
  halt
end
calcuid kon1 24207 obj
wait 1s
if ((%random.1000% < 501) && (%world.curobjs(24208)% < 6))
  wload obj 24208
else
  wecho _Выбеленный череп коня рассыпался в прах...
end
wait 1
%purge% %kon1%
~
#24217
ВстречаютМышки~
0 q 100
~
wait 1s
всхл
wait 1s
г Привет, путник
mecho - Вижу не злой ты человек..
mecho - Подсоби нам, а мы может тоже тебе на что-нибудь сгодимся.
mforce плутиха эм согласно закивала
wait 1s
вопрос
attach 24218 %self%
~
#24218
ПомогуМышкам~
0 d 0
помогу~
wait 1s
detach 24217 %self%
эм важно кивнула и начала рассказ..
wait 1s
mecho - Долгая была зима...
mecho - Немало за зиму попало к нам в норку костяных зубов от ребят да зверят.
wait 1s
mecho - Но вот солнышко весну показало...
mecho - И пришла пора за зуб костяной получить нам зуб железный.
mecho - Лежал наш путь полем, через синий лес, к Громовой стрелке.
mecho - И полпути мы не прошли, как прыгнул на нас сверху котище-седые усищи!
боя
wait 1s
mforce плутиха г А все равно я никого я не боюсь!
mforce плутиха г Самому коту на лапу наступлю!
wait 1s
рыч плутиха
wait 1s
эм в сердцах укусила мышку-плутиху за хвост
wait 1s
mecho _Мышка-свистуха продолжила свой рассказ:
г Схватила я мышку-плутиху за шкирку, да еле ноги унесли.
груст
wait 1s
г Надобно проводить нас к Громовой стрелке?
г А мы тебя наградим
вопрос
attach 24219 %self%
detach 24218 %self%
~
#24219
ПровожуМышкам~
0 d 0
провожу~
wait 1s
рад
wait 1s
follow .%actor.name%
calcuid mouse 24209 mob
mforce %mouse% follow .%actor.name%
detach 24219 %self%
~
#24220
КотонайВстречаетМышек~
0 g 100
~
if (%actor.vnum% != 24205)
  halt
end
calcuid m2 24209 mob
calcuid m1 24205 mob
*wait 1s
насторож
*wait 1s
mforce %m2% г Ты кто такой?
mforce %m2% вопрос кот
*wait 1s
г Да я кот Котонай!
эм удивленно рассматривал наглую мышь
*wait 1s
mforce %m2% г А я тебя не боюсь!
г Чего меня бояться?
эм хитро сощурил зеленые глаза
*wait 1s
эм наершился, прицелил глаз на мышек, хотел на мышку бросится..
mecho _Туда-сюда, глянь, а мышек и нет, канули мышки.
mecho _Только хвосты их и видели...
*телепорт мышей
%teleport% %m1% 24209
%teleport% %m2% 24209
~
#24221
МышкиПришлиКГромовойСтрелке~
2 g 100
~
if (%actor.vnum% != 24205)
  halt
end
calcuid m2 24209 mob
calcuid m1 24205 mob
wait 1s
wecho _Оскалили мышки зубки, взялись за лапы.
wait 1s
wecho _Кулек со спинки стащили, россыпью на землю высыпали.
wecho _Придет ночь, соберет их Громовая стрелка, да мышкам взамен добро справит.
wecho _Довольны мышки остались!
set herro %actor.leader%
wait 1s
wforce %m1% след я
wforce %m2% след я
wait 1s
wecho _Теперь пришло время им долги отдавать.
wforce %m1% дум
wait 1s
*награда мышек
*wecho - - %herro.name%
exec 24223 %m1%
wforce %m1% give all .%herro.name%
~
#24222
КотофеевнаВстречаетМышек~
0 g 100
~
if %actor.vnum% != 24205
  halt
end
* 
mecho _Зашипела Котофеевна на мышек. 
mecho _Подняла только лапу, а мышек-то и нет!
if %exist.mob(24205)% 
  calcuid m1 24205 mob
  %teleport% %m1% 24209
end
if %exist.mob(24209)% 
  calcuid m2 24209 mob
  %teleport% %m2% 24209
end
~
#24223
НаградаМышек~
0 z 100
~
switch %random.3%
  case 1
    if ((%random.1000% < 201) && (%world.curobjs(24229)% < 4))
      mload obj 24229
      halt
    end 
  break
  case 2
    if ((%random.1000% < 201) && (%world.curobjs(24230)% < 4))
      mload obj 24230
      halt
    end 
  break
  case 3
    if ((%random.1000% < 201) && (%world.curobjs(24231)% < 4))
      mload obj 24231
      halt
    end 
  break
done
mload obj 24228
~
#24224
ВстречаетДеваКраса~
0 q 100
~
wait 1s
взд
wait 1s
груст
wait 1s
г Здравия тебе, путник...
mecho - Близится Русалья неделя, весь род людской от нечисти попрятался по соседним весям
mecho - Одной мне идти некуда...
плак
wait 1s
mecho - Все время выручал меня оберег моей матушки - венок из целебных трав.
wait 1s
mecho - Только не так давно утащил у меня его Окаяшка проклятый.
mecho - Обычный был человек, да возгордился не в меру!
mecho - Так с тех пор и мается он нежитью - Окаяшкой.
mecho - Не поможешь найти мне оберег мой?
attach 24229 %self%
~
#24225
ЛоадАленыСОкружением~
2 e 100
~
wait 1s
%send% %actor% Проходя по лесу Вы заметили некоторое шевеление...
%send% %actor% Обернувшись, Вы застыли недвижимо от представшей картины:
wait 1s
wecho _Несется по лесу нагая ведьма Алена верхом на рябиновой палке с мутовкой на плече!
*лоад алены
wload mob 24227 
wait 1s
wecho _За Аленой шесть девок!
wecho _У девок власы распущены, все в белых рубахах, с серпами и кочергами в руках.
*лоад белых девок 6шт
wload mob 24228
wload mob 24228
wload mob 24228
wload mob 24228
wload mob 24228
wload mob 24228
wait 1s
wecho _И другие шесть!
wecho _С распущенными волосами, в черных юбках ловят черного петуха.
*лоад черных девок и петуха
wload mob 24229 
wload mob 24229
wload mob 24229
wload mob 24229
wload mob 24229
wload mob 24229
wload mob 24230
wait 1s
wecho _Рвется черный петух, наливаются кровью глаза, колотится черное сердце.
detach 24225 %self%
~
#24226
ГрузятсяДевки~
0 n 100
~
след alena38200
~
#24227
ЛоадСкропеиСОкружением~
2 e 100
~
wait 1s
wecho _Выползла из-под дуба-сорокавца, из-под ярого руна сама змея Скоропея!
wecho _Переваливаясь, поползла на своих гусиных лапах
wait 1s
wecho _Лютые все семь ее голов катились за ней кубарем.
wload mob 24233
wload mob 24234
wload mob 24234
wload mob 24234
wload mob 24234
wload mob 24234
wload mob 24234
wload mob 24234
wait 1s
wecho _Скликнула-вызвала Скоропея своих змей-змеенышей.
wload mob 24235
wload mob 24235
wload mob 24235
detach 24227 %self%
~
#24228
ГрузятсяЗмеи~
0 n 100
~
след skoropeja38200
~
#24229
ПомогуДевеКрасе~
0 d 0
помогу~
wait 1s
кив
wait 1s
mecho - Я слышала ночью собаки лают за околицей.
mecho - Возможно там он живет...
*лоад окаяшки
calcuid okaya 24237 room
exec 24232 %okaya%
*
attach 24230 %self%
detach 24224 %self%
detach 24229 %self%
~
#24230
ПринеслиВенокДеве~
0 j 100
~
if %object.vnum% != 24211
  удивл .%actor.name%
  return 0
  halt
end
wait 1
wear венок
эм завертелась перед зеркалом
wait 1s
г Вот радость-то!
эм всплеснула руками
wait 1s
mecho - Теперь я не пропаду! Спасибо тебе путник!
поклон
wait 1s
mecho - Уж скорее кончилось бы нечистое время, да пришла бы золотая осень.
взд
wait 1s
mecho - Недавно прилетал кулик из-за моря, ключи мне золотые в руки бросил.
эм повертела в руках золотые ключи
wait 1s
mecho - Сказал, что придет осень лишь тогда когда ильинский олень прибежит,
mecho - Да опустит рога в Булат-реку, что вмиг хладной станет.
mecho - И придет тогда Илья на громовой колеснице и наступит следующее время года.
mecho - Вот возьми эти ключи, может пригодятся...
mload obj 24273
дать ключ .%actor.name%
wait 1s
mecho - Будь осторожнее только...
wait 1s
mecho - Булат-река в нынешнее время-то место страшное...
mecho - Сбирается там всякой нечисти на свой праздник видимо-невидимо!
mecho - Даже говорят есть на ее берегу заповедный лес, где цветет купальский цветок,
mecho - И охраняет его сияющая Жар-птица!
mecho - Так сказывают у нас в деревне...
mecho - А теперь иди!
~
#24231
ОтпираемОсень~
2 e 100
~
if !%actor.haveobj(24273)%
  halt
end
*
wait 1s
wecho _С горки на горку, от ветлы до ветлы примчался ильинский олень.
wecho _Окунул рога в речке - стала вода холоднее! 
wload mob 24231
wait 1s
wecho _Катит Илья, громыхает на своей колеснице по небу.
wecho _Стелет шумный колос по земле осень!
*открываем вход в осень
wdoor 24249 east room 24251 
*убираем ключи
calcuid klu 24273 obj
if %klu%
  %purge% %klu%
end
~
#24232
ЛоадОкаяшки~
2 z 100
~
wload mob 24232
~
#24233
МигаетПесьяГолова~
0 q 30
~
wait 1s
миг .%actor.name%
~
#24234
УмерлаЖарПтица~
0 f 100
~
*запуск трига на руме лоад свети цвета
calcuid zaples 24250 room
exec 24235 %zaples%
~
#24235
ЛоадСветиЦвета~
2 z 100
~
wait 1s
wecho _И разомкнулось тридевять золотых замков!
wait 1s
wecho _Раскуталось тридевять дубовых дверей!
wait 1s
wecho _Туча за тучу зашла - затрещало, загикало, засвистело, загаркало!
wait 1s
wecho _И расцвел вдруг свети-цвет - волшебный купальский цветок.
wecho _Засиял светом нестерпимым!
wait 1s
wecho _И внимая свету ярому растворялась земля, 
wecho _И выходили из-под земли на свет посмотреть зарытые клады.
*лоад сундуков и цветка
wload obj 24212
*
switch %random.3%
  case 1
    calcuid mz1 24242 room
  break
  case 2
    calcuid mz1 24243 room
  break
  case 3
    calcuid mz1 24244 room
  break
done  
exec 24260 %mz1%
~
#24236
БьемОмутницу~
0 l 100
~
г Стой!
г Пощади меня!
всхл
wait 1s
г Хочешь выкуп дам за жизнь свою?
вопрос
attach 24249 %self%
detach 24236 %self%
~
#24237
ЗапираемНебоНаЗиму~
0 j 100
~
if %object.vnum% != 24274
  нет
  return 0
  halt
end
wait 1
%purge% %object%
wait 1s
mecho _Заскрипели невидимые ворота...
wait 1s
mecho _Загремели невидимые засовы...
wait 1s
mecho _Запер Егорий небесные ворота!
wait 1s
mecho _Зима пришла!
mdoor 24252 east room 24276
~
#24238
ВошлиКЕгориюОсеннему~
0 q 100
~
wait 1s
эм развел руки в стороны
wait 1s
г Чтобы осень запереть, да зиме лютой ход дать одного желания мало!
г Ключи надобно золотые, что кулик из-за моря принес, да весну ими отомкнул.
г Добудешь их, так смогу в зиму дорогу указать!
~
#24239
ВстречаетБелкаМохнатка~
0 q 100
~
wait 1s
эм испуганно отпрыгнула в сторону
wait 1s
mecho _Но удостоверившись, что нечего Вас бояться белка-мохнатка вновь вернулась к дубу.
wait 1s
взд
wait 1s
хму .%actor.name%
wait 1s
г Чего приш%actor.y%?
г И без тебя все плохо!
гру
wait 1s
г Подготовились все звери лесные к зиме
г Леший уж всех зверей посчитал, лесная нежить вся повылазила - ко сну зимнему готовится.
г Уж стал Егорий небо на зиму-то запирать, а ключей-то золотых след простыл!
г Вот и маемся уж который день... 
wait 1s
г Пролетала недавно сорока-белобока, да на хвосте принесла, что видела она те ключи.
г Да идти туда далеко и опасно - никто не соглашается.
всхл
wait 1s
г Чем стоять-то как пень, лучше бы помог%actor.q%!
attach 24240 %self%
detach 24239 %self%
~
#24240
ПомогуБелке~
0 d 0
помогу~
wait 1s
эм недоверчиво покосилась
wait 1s
г Ладно слушай...
г Отправляйся в темный лес, иди на юг, все прямо-прямо, и будет тебе избушка Бабы-Яги.
г Заглянуть к Яге в окошко можно, а входить не входи в избушку. 
г Яга тебя без шапки-невидимки заметит и съесть захочет.
wait 1s
г Ты иди лучше мимо избушки наискосок по тропинке, 
г Пролезай через шиповник, не бойся, пальчиков не оцарапаешь.
г Встретит тебя птица Гагана, поздоровайся с птицей: Гагана тебе птичьего молочка даст.
г Покушаешь молочка и снова в путь трогайся.
wait 1s
г Вскоре придешь к странному холму посреди леса - под холмом этим подземелье, 
г Внимательнее присматривайся и дверь увидишь.
г Только не туркайся в дверь, а встань тихо за дерево что там стоит и жди, что будет.
г Пройдет мимо дерева слепышка Листин, прошуршит листьями, не бойся: Листин не страшный. 
г Листин только пугать любит.
эм замахала лапами, изображая Листина
wait 1s
г Пролетит мимо дерева Сорока-белобока, ты ее не трогай.
г Прискачет Коза рогатая, ты не бойся: больно Коза не забодает.
г Выйдут из подземелья двенадцать черных разбойников, 
г Ты слушай, что станут говорить разбойники, заруби их слова себе на носике
г А когда пропадут разбойники, спускайся в подземелье и скажи то, что они говорили.
г Ну, а дальше один Бог ведает...
г Это все что я знаю...
взд
wait 1s
г Коли найдешь золотые ключи, Егорию их отдай - он давно их ищет!
хих
wait 1s
эм взмахнула пушистым хвостом и исчезла в ветвях.
wait 1
%purge% %self%
~
#24241
ЗаглядываемВокошко~
2 c 0
заглянуть~
if !%arg.contains(окошко)%
  wsend  %actor% Куда хотим заглянуть?
  halt
end
if %exist.mob(24263)%
  wsend  %actor% Вы встали на цыпочки и осторожно заглянули в темное окошко.
  wechoaround %actor% %actor.name% встал%actor.g% на цыпочки и осторожно заглянул%actor.g% в темное окошко.
  wait 1s
  wsend  %actor% Спит Бабка-Ежка беззаботным сном, 
  wsend  %actor% Распустила длинные уши: одно ухо вместо подушки, 
  wsend  %actor% А другим, будто одеялом, с головкою покрыта.
  *усыпляем ежку
  calcuid zaples 24256 room
  exec 24247 %zaples%
elseif %exist.mob(24280)%
  wsend  %actor% Вы встали на цыпочки и осторожно заглянули в темное окошко.
  wechoaround %actor% %actor.name% встал%actor.g% на цыпочки и осторожно заглянул%actor.g% в темное окошко.
  wait 1s
  wsend  %actor% Спит Бабка-Ежка беззаботным сном, 
  wsend  %actor% Распустила длинные уши: одно ухо вместо подушки, 
  wsend  %actor% А другим, будто одеялом, с головкою покрыта.
else
  wsend %actor% Ничего интересного там нет.
end
~
#24242
ВстаемЗаДерево~
2 c 0
встать~
if !%arg.contains(за дерево)%
  wsend  %actor% Куда хотим встать?
  halt
end
*
wsend %actor% Вы вытянулись стрункой и слились с темным стволом дерева.
wait 2s 
wechoaround %actor% %actor.name% вытянул%actor.u% стрункой, делая вид, что он%actor.g% дерево.
*wechoaround %actor%  Кто-то вылез из кустов.
*wteleport %actor% 24269
*открываем вход в 69
wdoor 24264 south room 24269
wsend %actor% Немного постояв, Вы заметили то, что не видели раньше...
wsend %actor% Прямо перед Вами, в склоне холма была дверь, обложенная человечьими костями.
wforce %actor% юг
calcuid mz1 24269 room 
attach 24261 %mz1%
detach 24242 %self%
~
#24243
ЛеземЧерезШиповник1~
2 c 0
лезть пролезть~
if !%arg.contains(шиповник)%
  wsend  %actor% Куда это Вы собрались?
  halt
end
*
wsend %actor%  Осторожно раздвинув колючие кусты шиповника, Вы пролезли сквозь них.
wechoaround %actor%  Осторожно раздвинув кусты шиповника %actor.name% исчез%actor.q% из виду.
wteleport %actor% 24260
wechoaround %actor%  Кто-то вылез из кустов.
~
#24244
ЗдороваемсяСГаганой~
0 d 0
здравствуй~
wait 1s
г И тебе, здравствуй!
поклон .%actor.name%
wait 1s
г Вижу собрал%actor.u% ты в дорогу дальнюю...
г Вот возьми, на дорожку молочка моего и рыбку серебренну.
mload obj 24215
mload obj 24276
дать все .%actor.name%
эм ударила оземь красным крылом и улетела
wait 1
%purge% %self%
~
#24245
ЛеземЧерезШиповник2~
2 c 0
лезть пролезть~
if !%arg.contains(шиповник)%
  wsend  %actor% Куда это Вы собрались?
  halt
end
*
wsend %actor%  Осторожно раздвинув колючие кусты шиповника, Вы пролезли сквозь них.
wechoaround %actor%  Осторожно раздвинув кусты шиповника %actor.name% исчез%actor.q% из виду.
wteleport %actor% 24259
wechoaround %actor%  Кто-то вылез из кустов.
~
#24246
ЗдороваемсяСХвостаткой~
0 d 0
здравствуй~
wait 1s
дум
wait 1s
эм беспокойно забегала кругами по семивинтовому столу
wait 1s
г Не похож%actor.g% ты на разбойников.
г Наверно ты за ключами приш%actor.y%!
wait 1s
г Прилетал в начале осени кулик из-за море и отдал мне ключи золотые.
г Сказал придет за ними воин удалой, да вежливый.
%send% %actor% Мышка-хвостатка оценивающе осмотрела Вас с ног до головы.
%echoaround% %actor% Мышка-хвостатка оценивающе осмотрела %actor.vname% с ног до головы.
wait 1s
кив
wait 1s
г Вроде похож%actor.g%!
wait 1s
wecho _Мышка-хвостатка отперла хвостиком шкатулку.
wecho _Достала из шкатулки кошелек с голубыми цветочками.
wecho _А из кошелечка бережно достала золотые ключи Матери-Природы.
mload obj 24274
дать все .%actor.name%
эм пискнула и исчезла
wait 1
%purge% %self%
~
#24247
УсыпляемЕжку~
2 z 100
~
calcuid babka 24263 mob
if %babka%
  wait 1
  wpurge %babka%
  wload mob 24280
end
~
#24248
ЗаглядываемЗаПечь~
2 c 0
заглянуть~
if (!%arg.contains(за печь)% && !%arg.contains(за печку)%)
  wsend %actor% Куда заглядываем?
  halt
end
*
*wait 1s
%send% %actor% За печкой ничего интересного кроме пыли, Вы не обнаружили.
%send% %actor% Пыль тут же забилась к Вам в нос, и Вы громко чихнули.
%echoaround% %actor% %actor.name% сунул%actor.g% нос за печь и начал%actor.g% громко чихать.
wait 1s
wforce %actor.name% чих
wait 1s
wecho _Поднялась пыль столбом, открывая за печкой проход куда-то вниз.
wdoor 24236 down room 24237
detach 24248 %self%
~
#24249
ХочуОмутнице~
0 d 0
хочу~
wait 1s
кив
wait 1s
switch %random.3%
  case 1
    if ((%random.1000% < 301) && (%world.curobjs(221)% < 50))
      mload obj 221
    else
      mload obj 24217
    end 
  break
  case 2
    if ((%random.1000% < 301) && (%world.curobjs(232)% < 50))
      mload obj 232
    else 
      mload obj 24217
    end 
  break
  case 3
    if ((%random.1000% < 101) && (%world.curobjs(537)% < 3))
      mload obj 537
    else
      mload obj 24217
    end 
  break
  default 
  break 
done
г Вот возьми...
give all .%actor.name%
wait 1s
mecho _Омутница взмахнула хвостом и исчезла в темной пучине.
wait 1
%purge% %self%
~
#24250
УмерлаЛеснь~
0 f 100
~
if ((%random.1000% < 401) && (%world.curobjs(24218)% < 6))
  mload obj 24218
end
~
#24251
УмерлиВодяныеЖители~
0 f 100
~
switch %random.2%
  case 1
    if ((%random.1000% < 401) && (%world.curobjs(24219)% < 7))
      mload obj 24219
    end 
  break
  case 2
    if ((%random.1000% < 401) && ( %world.curobjs(24220)% < 7))
      mload obj 24220
    end 
  break
  default 
    halt
  break 
done
~
#24252
УмерлаДревяница~
0 f 100
~
switch %random.4%
  case 1
    if ((%random.1000% < 401) && (%world.curobjs(24221)% < 4))
      mload obj 24221
    end 
  break
  case 2
    if ((%random.1000% < 401) && (%world.curobjs(24222)% < 4))
      mload obj 24222
    end 
  break
  case 3
    if ((%random.1000% < 401) && (%world.curobjs(24223)% < 4))
      mload obj 24223
    end 
  break
  case 4
    if ((%random.1000% < 401) && (%world.curobjs(24224)% < 4))
      mload obj 24224
    end 
  break
done
~
#24253
УмерлаТравяница~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 401 ) && ( %world.curobjs(24225)% < 4 ))
      mload obj 24225
    end 
  break
  case 2
    if (( %random.1000% < 401 ) && ( %world.curobjs(24226)% < 4 ))
      mload obj 24226
    end 
  break
  case 3
    if (( %random.1000% < 401 ) && ( %world.curobjs(24227)% < 4 ))
      mload obj 24227
    end 
  break
done
~
#24254
УмерлаВорогуша~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24232)% < 3 ))
      mload obj 24232
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24233)% < 3 ))
      mload obj 24233
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24234)% < 3 ))
      mload obj 24234
    end 
  break
done
~
#24255
УмерлаЛисичка~
0 f 100
~
mload obj 24278
~
#24256
УмерОкаяшка~
0 f 100
~
mload obj 24211
~
#24257
УмерлаСкоропея~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 201 ) && ( %world.curobjs(24235)% < 6 ))
      mload obj 24235
    end 
  break
  case 2
    if (( %random.1000% < 201 ) && ( %world.curobjs(24236)% < 6 ))
      mload obj 24236
    end 
  break
  case 3
    if (( %random.1000% < 201 ) && ( %world.curobjs(24237)% < 6 ))
      mload obj 24237
    end 
  break
done
~
#24258
УмерлаГорлица~
0 f 100
~
if (( %random.1000% < 401 ) && ( %world.curobjs(24238)% < 4 ))
  mload obj 24238
end
~
#24259
УмерлаРусалка~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 201 ) && ( %world.curobjs(24239)% < 6 ))
      mload obj 24239
    end 
  break
  case 2
    if (( %random.1000% < 201 ) && ( %world.curobjs(24240)% < 6 ))
      mload obj 24240
    end 
  break
  case 3
    if (( %random.1000% < 201 ) && ( %world.curobjs(24241)% < 6 ))
      mload obj 24241
    end 
  break
done
~
#24260
ЛоадКлада~
2 z 100
~
wload obj 24213
~
#24261
ПоявляютсяЗвери~
2 b 100
~
wait 1s
* *листин
wecho _Вдруг зашуршали листья под чьим-то тихим шагом, навевая на Вас ужас!
wecho _Из ниоткуда возник слепышка Листин, шурша седой травой.
if !%exist.mob(24252)%
  wload mob 24252
end
wait 8s
* сорока
wecho _Затрещало небо заливистым щебетом!
wecho _Прилетела сорока-белобока, села на ветви.
wecho _Как бы Вас не заметила!
if !%exist.mob(24254)%
  wload mob 24254
end
*коза
wait 8s
wecho _Застучали-загремели по земле чьи-то копыта!
wecho _Шла Коза Рогатая по своим делам, да мимо Вас.
wecho _Как бы не забодала?
if !%exist.mob(24262)%
  wload mob 24262
end
*12 разбойников
wait 8s
wecho _Отворилась вдруг дверь из человечьих костей, заскрипела!
wecho _Вышли из подземелья двенадцать черных разбойников.
wait 1s
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
wecho Черный разбойник пришел снизу.
*
if !%exist.mob(24251)% 
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
  wload mob 24251
end
*
wait 1s
wecho _Постояли разбойники, позевали на месяц, сказали заклинание и пропали.
wait 1s
wforce разбойник г Чучело чумичело гороховая куличина подай челнок заметай шесток
wait 1s
wecho _&WЗаскрипела костями белая дверь... и открылась.&n
wait 1s
wdoor 24269 down room 24270
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wforce черный вниз
wdoor 24269 down purge
wait 1s
wecho _&WЗаскрипела костями белая дверь... и закрылась.&n
attach 24263 %self%
detach 24261 %self%
~
#24262
алена с петухом уходит~
0 n 100
~
wait 4s
west
~
#24263
ГоворимЗаветныеСлова~
2 d 0
*~
if %actor.vnum% != -1
  halt
end
wait 1s
*wecho 000 %speech%
set text Чучело чумичело гороховая куличина подай челнок заметай шесток 
if %speech% != %text%
  halt
end
wait 1s
wecho _&WЗаскрипела костями белая дверь... и открылась.&n
wdoor 24269 down room 24270
detach 24263 %self.id%
~
#24264
ВстречаетЗайчикИваныч~
0 q 100
~
wait 1s
г Привет, путник!
г Не ожидал уж гостей в стужу лютую.
г Садись, грейся!
эм подкинул еловых веток в маленькую печь посреди норы.
wait 1s
г Хочешь я тебе историю одну расскажу, чтоб не скучно было?
attach 24265 %self%
detach 24264 %self%
~
#24265
ХочуИванычу~
0 d 0
хочу да~
wait 1s
г Ну так слушай....
эм подвинулся ближе к печке
wait 1s
г Вышел я седмицу назад погулять ночью темной.
г А звезд много на небе, звезды разговаривают, только не слыхать.
г И вдруг вижу - звезда падает!
г Упала в самую чащу, где старые ели сплетаются мохнатыми ветвями и страшно гудят.
г Что делать?! Пошел я ее искать!
эм поежился
wait 1s
г Долго ходил...
г И наконец нашел! 
г Лежит звездочка - прозрачная, с синими грустными глазками на мягких еловых иглах.
г Взял я ее на руки, к себе домой принес!
эм кивнул на колыбельку, из которой вырывалось голубоватое свечение
wait 1s
г Одно худо - день ото дня гаснет родимая.
г Видно не могут они на земле-то жить, умирают.
гру
wait 1s
г Слышал я от медведя нашего, что можно ее обратно на небо взгромоздить.
г Но то - дело великое! Мне не по силам.
г Может ты сможешь?
attach 24266 %self%
detach 24265 %self%
~
#24266
ПомогуИванычу~
0 d 0
смогу~
wait 1s
кив
wait 1s
г Сам Велес тебя прислал мне в помощь!
г Слушай, что нужно сделать!
эм начал суетливо собирать звездочку в путь далекий.
wait 1s
г Есть за Холодными Горами хрустальный терем.
г Там солнышко спит, когда ночь на дворе.
г Так вот, через этот терем самая короткая дорога на Небо.
г Бросишь звездочку, а она крылышки распустит, да вернется к себе домой.
г Так медведь говорил...
взд
wait 1s
г Вот, держи ее...
г Да смотри, обращайся осторожно!
mload obj 24245
дать звез .%actor.name%
detach 24266 %self%
~
#24267
БросаемЗвездочку~
2 h 100
~
if %object.vnum% != 24245
  halt
end
*
wait 1
%purge% %object%
wait 1s
wsend %actor% _Вырвалась из Ваших рук звездочка, шелковое сбросила одеяльце.
wechoaround %actor% _Вырвалась из рук %actor.rname% звездочка, шелковое сбросила одеяльце.
wait 1s
wecho _Полетела родимая в небо синее!
wecho _Летела-летала, да на тучку наткнулась!
wait 1s
wecho _Распорола тучку краешком острым и выпали из тучки ключи золотые!
wload obj 24275
wait 1s
wecho _Засияла звездочка пуще прежнего на ночном небе!
wecho _С тех пор каждую ночь %actor.dname% кланяется...
~
#24268
ВошлиКЕгорию2(ПослеЗимы)~
0 q 100
~
wait 1s
г Пора уже на весну поворачивать!
г Только опять потерял я ключи золотые.
груст
wait 1s
г Без них весну дивную не вернуть!
г Найдешь их, так смогу весне небо открыть!
~
#24269
ДаемКлючиЕгорию2(ПослеЗимы)~
0 j 100
~
*если ключи
if (%object.vnum% != 24275)
  say Это еще зачем?
  return 0
  halt
end
wait 1
%purge% %object%
mecho _Загремел золотыми ключами Егорий...
wait 1s
mecho _Ушла зима с морозами, с трескучими.
mecho _Взошло солнце, согнало снега.
mecho _Пошла вода вольная, полноводная. Подмыла сучки, ветки, отросточки.
wait 1s
mecho _Весна приехала!
mecho _Пойте, птицы, и вечером и утром, - всем нам веселье!
*открываем вход к царевне в терем
mdoor 24296 east room 24200
wait 1
%purge% %self%
~
#24270
ВстречаетВедьмаМаруна~
0 q 100
~
wait 1s
хих
wait 1s
г Надо же, живые люди в гости пришли! 
г Человечьим духом снова в лесу студеном запахло!
эм принюхалась
wait 1s
г Слушай, путник, знаю я есть где-то цветок волшебный купальский.
г Найди мне его, а?
просить .%actor.name%
wait 1s
г А я тебя обучу умению важному!
г На любых праздниках людских нужное!
г Как головушке после зелена вина да водочки не болеть!
~
#24271
ДаемЦветокКупалыМаруне~
0 j 100
~
if ( %object.vnum% != 24212 ) 
  нет
  return 0
  halt
end
wait 1
%purge% %object%
wait 1s
рад
wait 1s
виз
г Ну, угодил%actor.g%
г Вот награда твоя...
г Только смотри, всему свой предел есть - на то воля богов наших!
*апаем опохмел
eval max 100+%actor.remort%*5
eval skl %actor.skill(опохмелиться)%+3
if (%skl% <= %max%)
  mskilladd %actor% опохмелиться 3
elseif (%actor.skill(опохмелиться)% < %max%)
  eval addskl %max%-%actor.skill(опохмелиться)%
  mskilladd %actor% опохмелиться %addskl%
else
  дум
  г Да тебе это и не нужно, а я-то думала!
  эм фыркнула и отвернулась
  wait 1s
end
detach 24271 %self%
~
#24272
УмерлиРазбойники~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24247)% < 4 ))
      mload obj 24247
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24248)% < 4 ))
      mload obj 24248
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24249)% < 4 ))
      mload obj 24249
    end 
  break
done
~
#24273
УмерОх~
0 f 100
~
if (( %random.1000% < 301 ) && ( %world.curobjs(24255)% < 5 ))
  mload obj 24255
end
~
#24274
УмерлаВеснянка~
0 f 100
~
if (( %random.1000% < 301 ) && ( %world.curobjs(24256)% < 5 ))
  mload obj 24256
end
~
#24275
УмерлиЛистотрясы~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 201 ) && ( %world.curobjs(24253)% < 4 ))
      mload obj 24253
    end 
  break
  case 2
    if (( %random.1000% < 201 ) && ( %world.curobjs(24254)% < 4 ))
      mload obj 24254
    end 
  break
  case 3
    if (( %random.1000% < 201 ) && ( %world.curobjs(24257)% < 4 ))
      mload obj 24257
    end 
  break
done
~
#24276
УмерлаСорока~
0 f 100
~
if (( %random.1000% < 401 ) && ( %world.curobjs(24242)% < 6 ))
  mload obj 24242
end
~
#24277
УмерлиМедведиПыхтуны~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24258)% < 4 ))
      mload obj 24258
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24259)% < 4 ))
      mload obj 24259
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24260)% < 4 ))
      mload obj 24260
    end 
  break
done
~
#24278
УмерлаЕжка~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24263)% < 4 ))
      mload obj 24263
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24264)% < 4 ))
      mload obj 24264
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24265)% < 4 ))
      mload obj 24265
    end 
  break
done
~
#24279
УмерлиМедведи~
0 f 100
~
if (( %random.1000% < 201 ) && ( %world.curobjs(24266)% < 6 ))
  mload obj 24266
end
~
#24280
УмерлиСестры~
0 f 100
~
switch %random.6%
  case 1
    if (( %random.1000% < 101 ) && ( %world.curobjs(24250)% < 4 ))
      mload obj 24250
    end 
  break
  case 2
    if (( %random.1000% < 101 ) && ( %world.curobjs(24251)% < 4 ))
      mload obj 24251
    end 
  break
  case 3
    if (( %random.1000% < 101 ) && ( %world.curobjs(24252)% < 4 ))
      mload obj 24252
    end 
  break
  case 4
    if (( %random.1000% < 101 ) && ( %world.curobjs(24267)% < 4 ))
      mload obj 24267
    end 
  break
  case 5
    if (( %random.1000% < 101 ) && ( %world.curobjs(24268)% < 4 ))
      mload obj 24268
    end 
  break
  case 6
    if (( %random.1000% < 101 ) && ( %world.curobjs(24269)% < 4 ))
      mload obj 24269
    end 
  break
done
~
#24281
УмерлаМаруна~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24270)% < 4 ))
      mload obj 24270
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24261)% < 4 ))
      mload obj 24261
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24247)% < 4 ))
      mload obj 24247
    end 
  break
done
~
#24282
УмерМороз~
0 f 100
~
if (( %random.1000% < 401 ) && ( %world.curobjs(24244)% < 5 ))
  mload obj 24244
end
~
#24283
УмерКудеяр~
0 f 100
~
if (( %random.1000% < 201 ) && ( %world.curobjs(24244)% < 4 ))
  mload obj 24243
end
~
#24284
УмерлиМетели~
0 f 100
~
switch %random.3%
  case 1
    if (( %random.1000% < 101 ) && ( %world.curobjs(24271)% < 4 ))
      mload obj 24271
    end 
  break
  case 2
    if (( %random.1000% < 101 ) && ( %world.curobjs(24262)% < 4 ))
      mload obj 24262
    end 
  break
  case 3
    if (( %random.1000% < 101 ) && ( %world.curobjs(24272)% < 4 ))
      mload obj 24272
    end 
  break
done
~
#24285
УлетаетГорлица~
0 q 100
~
wait 1s
if ( %world.curobjs(24238)% < 1 )
  wait 1s
  mecho _Вспорхнула потревоженная горлица, улетела в небесную высь, выронив сизое перышко.
  mload obj 24238
  wait 1
  %purge% %self%
end
~
#24286
ВошлиКЛебедь~
0 q 100
~
wait 1s
хму
wait 1s
г Ты чего тут без дела расхаживаешь? 
г Иди своей дорогой!
wait 3s
г Хотя, погоди...
дум
wait 1s
г Есть у меня для тебя поручение небольшое.
г Несложное, но и не простое...
г Выполнишь?
attach 24289 %self%
~
#24287
ПринеслиИнгридиентыЛебедь~
0 j 100
~
*пож
*г Велено пока ничего не давать!
*halt
if ((%object.vnum% == 24278) || (%object.vnum% == 24277) || (%object.vnum% == 24276) || (%object.vnum% == 24238))
  wait 1
  кив
else
  г Это не то!
  return 0
  halt
end
set num %object.vnum%
wait 1
mpurge %object%
switch %num%
  case 24278
    set item1 1
    global item1
  break
  case 24277
    set item2 1
    global item2
  break
  case 24276
    set item3 1
    global item3
  break
  case 24238
    set item4 1
    global item4
  break
done
if (!%item1% || !%item2% || !%item3% || !%item4%)
  say Но это еще не все...
  ждать
  halt
end
wait 1s
г Отлично!
г Теперь жди!
эм взмахнула руками словно крыльями, отчего распахнулись все окна впуская белый свет.
wait 1s
mecho _И заговорила царевна Лебедь тихим голосом:
mecho - Красное солнце, высоко ты плаваешь в синих сумрачных реках небес,
mecho - Там волнистые поля облаков неустанно бегут.
mecho - И ты, сын красного солнца, белый мой свет, ты озаряешь мать-землю.
mecho - Пусть одарят тебя силою светлой желание мое и сила Великих Богов.
mecho - Чтобы был с нами вместо ночи темяной, да зимы лютой.
wait 1s
эм бросила душистый ландыш на землю
wait 1s
mecho _&RОзарилась восточная сторона светом ярым!&n
mecho - Будет тебе свет белый Белобоже от Весны-Красны силы, сколько белых цветов в лесу!
wait 1s
эм бросила невесомое перышко на землю
wait 1s
mecho _&RОзарилась южная сторона светом ярым!&n
mecho - Будет тебе свет белый Белобоже от Лета Красного силы, сколько вольных птиц в поле!
wait 1s
эм бросила рыбку-серебрянку на землю
wait 1s
mecho _&RОзарилась западная сторона светом ярым!&n
mecho - Будет тебе свет белый Белобоже от осени Темной силы, сколько рыб на глубоком дне!
wait 1s
эм бросила еловую ветвь в инее белом на землю
wait 1s
mecho _&RОзарилась северная сторона светом ярым!&n
mecho - Будет тебе свет белый Белобоже от Зимы Лютой силы, сколько сосен стоит кругом!
wait 1s
mecho _Зашумел сильнее ветер, заалело ярче солнце!
mecho _Улыбнулась царевна Лебедь мыслям своим.
wait 1s
г Теперь пришло время наградить тебя!
ул .%actor.name%
wait 1s
*награда
switch %random.7%
  case 1
    if (( %random.1000% < 301 ) && ( %world.curobjs(24279)% < 4 ))
      mload obj 24279
    else
      mload obj 24217
    end 
  break
  case 2
    if (( %random.1000% < 301 ) && ( %world.curobjs(24280)% < 4 ))
      mload obj 24280
    else
      mload obj 24217
    end 
  break
  case 3
    if (( %random.1000% < 301 ) && ( %world.curobjs(24281)% < 4 ))
      mload obj 24281
    else
      mload obj 24217
    end 
  break
  case 4
    if (( %random.1000% < 301 ) && ( %world.curobjs(24282)% < 4 ))
      mload obj 24282
    else
      mload obj 24217
    end 
  break
  case 5
    if (( %random.1000% < 301 ) && ( %world.curobjs(24283)% < 4 ))
      mload obj 24283
    else
      mload obj 24217
    end 
  break
  case 6
    if (( %random.1000% < 301 ) && ( %world.curobjs(24284)% < 4 ))
      mload obj 24284
    else
      mload obj 24217
    end 
  break
  case 7
    if (%world.curobjs(414)% < %world.maxobj(414)%)
      mload obj 414
    end
  break
  default 
  done 
  дать все .%actor.name%
~
#24288
БудимЗайчонка~
0 c 0
разбудить растолкать~
if !%arg.contains(зайчонка)%
  msend  %actor% Кого будим?
  halt
end
mecho  Зайчонок открыл один глаз...
wait 1s
просну
mecho _Зайчонок опасливо принюхался и в тот же миг взвился кубарем и исчез.
mload obj 24277
бро все
wait 1
%purge% %self%
~
#24289
ВыполнюЛебедь~
0 d 0
выполню~
wait 1s
кив
wait 1s
г Хочу я...
эм мечтательно подняла глаза к небу
wait 1s
mecho - Хочу я, чтобы день белый не укорачивался зимой.
mecho - Чтобы летом не уходил с закатом, уступая черед ночи темяной.
mecho - Чтобы весной приходил пораньше, когда еще спят ранние пташки.
mecho - Таково желание мое!
взд
wait 1s
mecho - Наделила меня матушка моя силой колдовской.
mecho - Но для сотворения волшбы во имя белого света, 
mecho - Нужны мне вещи из каждого времени года наделенные силой своих богов.
mecho - Вот и твое задание: 
mecho - Пойдешь посолонь по земле русской, через все времена года и принесешь мне их!
mecho - Как отопрет Яр небо на Весну-красну, так ищи цветы белые с дивным запахом.
mecho - А как взойдешь на порог Лета Красного, так ищи перо сизой птицы, что в полях гуляет.
wait 1s
mecho - Коли и это добудешь...
mecho - Тогда среди ливней Осени Темной на озере с одинокой птицей ищи рыбку волшебную.
mecho - А когда небо на зиму Яр запрет, коли выживешь средь лютых метелей,
mecho - Так ищи в лесах хладных ветвь еловую, что прячет у себя в норе зверек малый.
mecho - Вот и все...
ул
wait 1s
mecho - Нехитрое дело-то, только непростые это вещицы и добыть потому непросто.
mecho - Ну, а коли добудешь, награжу по-царски!
mecho - А теперь, иди!
wait 1s
*открываем вход пентой в весну
calcuid r1 24200 room
exec 24290 %r1% 
attach 24287 %self%
detach 24286 %self%
detach 24289 %self%
~
#24290
ПентаЛебеди~
2 z 100
~
wportal 24201 2
~
$~
