#84200
резетим зону~
2 f 100
~
wait 1
context 842
eval portal842 0
WORLDS portal842
eval nogi842 0
WORLDS nogi842
eval ruki842 0
WORLDS ruki842
eval glaza842 0
WORLDS glaza842
eval telo842 0
WORLDS telo842
eval golova842 0
WORLDS golova842
~
#84201
смерть вампира~
0 f 100
~
if (%random.10% == 1)
  mload obj 84230
end
~
#84202
смерть библиотекаря~
0 f 100
~
if (%random.10% == 1) && (%world.curobjs(84231)% < 2)
  mload obj 84231
end
~
#84203
смерть огненного жука~
0 f 100
~
if (%random.10% == 1)
  mload obj 84241
end
~
#84204
смерть дервиша~
0 f 100
~
if %world.curobjs(84243)% < 5 && %random.100% < 15
  mload obj 84243
end
~
#84205
смерть паука птицееда~
0 f 100
~
if (%random.10% == 1)
  mload obj 84248
end
~
#84210
могила - триг на вход~
0 q 100
~
wait 1
say Ну вот хоть кто-то забрел сюда...
dg_affect %actor% сила подчинить_разум -5 0
dg_affect %actor% сила подчинить_разум -5 10000
dg_affect %actor% интеллект подчинить_разум -5 0
dg_affect %actor% интеллект подчинить_разум -5 10000
wait 1
mechoaround %actor% Дух зыркнул на %actor.vname% и прошептал "ingodium laveoso"
msend %actor% Дух зыркнул на Вас и прошептал "ingodium laveoso"
mecho Невесть откуда взявшаяся черная кошка пробежала у Ваших ног.
wait 1s
say Путник, теперь ты проклят и обречен. 
say В любом из твоих дел, в любом бою, тебя  будут преследовать неудачи!
улы .%actor.name%
wait 2s
say Но тебе повезло, поскольку только я могу снять с тебя заклятье.
say Хочешь знать как? Могу рассказать.
detach 84210 %self.id%
~
#84211
могила - согласились слушать духа.~
0 d 0
да хочу слушаю желаю расскажи рассказывай~
wait 1
say  Вот уже много лет прошло после смерти моего тела. 
say  А душа моя все никак не может покинуть грешную землю.
вздох
wait 1s
say Так наказал меня Господь за то, что при жизни я,
say будучи принцем, занимался Черной магией.
say Хорошие были времена. Бывало поймаешь крестьянку и...
say занимаешься черной магией тогда...
груст
wait 1s
say  Есть у меня тут идея - снова в мир живых вернуться.
say Это легко вообще-то. Но, без помощника никак.
wait 1s
say  Раз уж ты такой хороший человек, что помочь решил добровольно, то поищи 
say подходящие  части для моего будущего тела. Голову же принеси только
say от принца. Или от короля на крайний случай. Там внутри места много, 
say а мне только того и надо.
wait 1s
say Тут рядом есть кристалл. Я отправлю тебя с его помощью. Иди туда.
context 842
eval portal842 1
WORLDS portal842
detach 84211 %self.id%
~
#84212
могила - положили часть тела в углубление~
0 j 100
~
wait 1
context 842
if %object.vnum%==84226
  eval nogi842 1
  WORLDS nogi842
  mecho Дух положил огненные ноги в углубление со льдом.
  mecho Жидкость в углублении зашипела.
  г Всю жизнь мечтал о таких ногах. А то были при жизни кривые да волосатые.
  mpurge %object%
end
if %object.vnum%==84225
  eval ruki842 1
  WORLDS ruki842
  mecho Дух положил руки из воздуха в углубление со льдом.
  г Какие нежные ласковые ручки!
  mpurge %object%
end
if %object.vnum%==84217
  eval glaza842 1
  WORLDS glaza842
  mecho Дух положил глаза дракона в углубление со льдом.
  г Потом посмотрим.
  mpurge %object%
end
if %object.vnum%==84221
  eval telo842 1
  WORLDS telo842
  mecho Дух положил туловище красавицы в углубление со льдом.
  г Вот изверг! Все в шрамах! Не мог кромсать поаккуратнее?
  mpurge %object%
end
if ((%object.vnum%==84215) && (%golova842%==0))
  eval golova842 2
  WORLDS golova842
  mecho Дух положил череп с короной в углубление со льдом.
  г Думать такой вредно, а вот стены ей долбить самое то!
  mpurge %object%
end
if ((%object.vnum%==84227) && (%golova842%==0))
  eval golova842 1
  WORLDS golova842
  mecho Дух положил голову чудовища в углубление со льдом.
  г Ну и морда! Теперь моя будет.
  mpurge %object%
end
брос все
if ((%nogi842%==1) && (%ruki842%==1) && (%glaza842%==1) && (%telo842%==1) && (%golova842%==1))
  mecho Части тела лежавшие в углублении начали срастаться.
  mecho Дух озарился нестерпимо ярким светом и слился с мертвым телом воедино.
  mload mob 84217
  dg_affect %actor% интеллект подчинить_разум -5 0
  dg_affect %actor% сила подчинить_разум -5 0
  mecho Прекрасный принц встал на ноги.
  mpurge %self% 
  halt
end
if ((%nogi842%==1) && (%ruki842%==1) && (%glaza842%==1) && (%telo842%==1) && (%golova842%==2))
  mecho Части тела лежавшие в углублении начали срастаться.
  mecho Дух дьявольски захохотал, и в клубах красного дыма вошел в мертвое тело.
  mload mob 84218
  dg_affect %actor% интеллект подчинить_разум -5 0
  dg_affect %actor% сила подчинить_разум -5 0
  mecho Ужасный принц встал на ноги.
  mpurge %self% 
  halt
end
wait 3
say Но это еще не все тело! Ищи дальше.
~
#84213
могила - создание портала~
2 e 100
~
wait 1
context 842
if (%portal842%==1)
  wportal 84226 2
  wecho Из портала донеслись раскаты могучего рева и запах серы.
  halt
end
if (%portal842%==2)
  wportal 84263 2
  wecho Порывы ветра и приятное благоухание ударили Вам в лицо.
  halt
end
if (%portal842%==3)
  wportal 84230 2
  wecho Звуки кирки рассекли тишину.
  halt
end
~
#84214
могила - прекрасный принц телепортит.~
0 ab 100
~
поклон
г Я снял с тебя проклятье!
г Это самое малое что я могу сделать для тебя.
г И впредь будь поосторожнее.
wait 2s
г Теперь я отблагодарю тебя. Иди за своей наградой.
г А заодно избавь мир от великого зла!
улы
wait 1s
mteleport all 84257 horse
wait 1
mpurge %self%
~
#84215
могила - ужасный принц телепортит.~
0 ab 100
~
поклон
г Думаю не стоило бы снимать с тебя проклятье, 
г если у тебя хватило глупости попасть под него,
г но слово есть слово.
wait 2s
г А теперь я отблагодарю тебя. Иди и возьми свою награду!
г А по дороге убей всех кого встретишь.
хох
wait 1s
mteleport all 84260 horse
wait 1
mpurge %self%
~
#84220
дракон - создание перехода обратно~
2 e 100
~
wait 1
if (%world.curmobs(84203)% > 0 )
  halt
end
wportal 84244 2
wecho Наступила могильная тишина и могильный воздух заставил Вас передернуться.
context 842
eval portal842 2
WORLDS portal842
~
#84221
дракон - убили дракона~
0 f 100
~
mecho Последним ударом чудищу пронзили голову, выдернув глаза.
mload obj 84217
mload obj 84210
mload obj 84249
~
#84222
дракон - убили лича~
0 f 100
~
mecho Ужасная голова с короной свалилась на пол от Вашего могучего удара.
mload obj 84215
~
#84230
ангелы - создание перехода обратно~
2 e 100
~
wait 1
if (%world.curmobs(84211)% > 0 )
  halt
end
if (%world.curmobs(84212)% > 0 )
  halt
end
if (%world.curmobs(84213)% > 0 )
  halt
end
wportal 84244 2
wecho Наступила могильная тишина и могильный воздух заставил Вас передернуться.
context 842
eval portal842 3
WORLDS portal842
~
#84231
убили джинна-охранника~
0 f 100
~
mecho Только руки из воздуха остались на месте монстра.
mload obj 84225
~
#84232
ангелы - убили хранителя~
0 f 100
~
mecho Тяжело было у огня отрезать ноги, но похоже получилось...
mload obj 84226
~
#84240
рудокоопы - создание перехода обратно~
2 e 100
~
wait 1
if (%world.curmobs(84214)% > 0 )
  halt
end
if (%world.curmobs(84215)% > 0 )
  halt
end
if (%world.curmobs(84216)% > 0 )
  halt
end
wportal 84244 2
wecho Наступила могильная тишина и могильный воздух заставил Вас передернуться.
context 842
eval portal842 4
WORLDS portal842
~
#84241
рудокопы - убили белоснежку~
0 f 100
~
mecho Тело красавицы с отрубленными конечностями и без головы упало к вашим ногам.
mload obj 84221
~
#84242
рудокопы - убили чудовище~
0 f 100
~
mecho Чудовище издав протяжный рык распрощалось со своей головой.
mload obj 84227
~
#84250
вампиры - создание перехода обратно~
2 e 100
~
wait 1
wportal 84243 2
~
#84251
Настоятельница храма RIP~
0 f 100
*~
*if (%world.curobjs(579)% < 10) & (%random.10% <= 1)
*mload obj 579
*end
if (%world.curobjs(84232)% < 5) && (%random.10% <= 2)
  mload obj 84232
end
if (%world.curobjs(84233)% < 5) && (%random.10% <= 2)
  mload obj 84233
end
~
#84260
маги - создание перехода обратно~
2 e 100
~
wait 1
wportal 84243 2
~
#84261
маги - лоад буки~
0 f 100
*~
if (%world.curobjs(582)% < 10) && (%random.10% <= 1)
  mload obj 582
end
if (%world.curobjs(84234)% < 5) && (%random.10% <= 2)
  mload obj 84234
end
if (%world.curobjs(84235)% < 5) && (%random.10% <= 2)
  mload obj 84235
end
~
$~
