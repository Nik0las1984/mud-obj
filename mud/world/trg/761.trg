#76100
зашли к избе~
2 e 100
0~
if !(%exist.mob(76107)%)
  halt
end
wait 1
wecho _Увидев Вас, кто-то быстро залез в норку.
detach 76100 %self.id%
~
#76101
попали в грязь~
2 e 70
~
wait 1
wecho _Ну зачем Вы полезли в грязь, неужто мама не учила, что по дороге удобней?
eval buf %actor.move(-80)%
~
#76102
заходят к старичку~
0 q 100
~
if !%self.fighting%
  wait 1
  г Ой, сынок, чегой-то ренматизмь старику в спину ударил, 
  г Помоги до села дойтить..
  wait 1s
  mecho _Старичок нагнулся что-то прошептать Вам.
  wait 3s
  mtransform 76117
  wait 1
  mecho _Старичок вдруг дернулся, стремительно покрылся черной шерстью...
  mecho ___... И огромный черный пес-двоедушник кинулся на вас!
  eval buf %self.hitp(+100)%
  eval t %random.pc%
  if %t%
    mkill %t%
  end
  attach 76113 %self.id%
  detach 76102 %self.id%
end
~
#76103
вошли к юртаю~
0 r 100
0~
wait 1s
mecho _Портной встал заметив Вас и поклонился.
г Приветствую тебя, %actor.name%!
г К нам редко заходят гости.
взд
wait 1s
г Есть у меня просьба для тебя, смилуйся, не откажи.
г Уж я тебя то отблагодарю.
mecho _Портной сделал жалостливое лицо, умоляя ему помочь. 
wait 1s
г Возьмешься ли ты за эту непростую задачу?
attach 76104 %self.id%
~
#76104
юртай выдает квест~
0 d 1
возьмусь конечно~
wait 1
if %actor.vnum% != -1
  halt
end
if !%quest761%
  set quest761 %actor%
  global quest761
  г Вот расскажу тебе одну историю...
  г Жил я в деревне неподалеку отсюда. Занимался ремеслом своим.
  г Злой народ сейчас пошел - сошью я вещь неудачно-так мне в глаза тычут,
  г плохо мол ты шьешь, чтоб у тебя руки отсохли! А сошью хорошо-так говорят
  г молодец, Юртай, хорошо шьешь, только дорого берешь за свою работу,
  г чтоб тебя черт побрал!
  г И так и эдак людям не годится, вот и ушел я от них...
  wait 2s
  г Брел куда глаза глядят... тропами, лесами, болотами...
  г Встретил я Медведя... Подружились мы с ним, пошли вместе жилье искать.
  г Долго плутали мы холмами, полями, дремучими лесами...
  г Пока не нашли мы третьего.. Домового... Он то нам и рассказал о заброшенном доме.
  wait 2s
  г Поселились втроем в этом доме - Я, Медведь и Домовой чертяка.
  г Стали дружно хозяйство вести.
  г Я одежу шил, Медведь пиво варил, за садом ухаживал,
  г Домовой столярным и плотницким делом занимался.
  г Весело жили, трудились на славу, вечерком пивко потягивали...
  г Но в один прекрасный день, прознала ведьма Вирява, живущая в
  г окрестных лесах, про то что у нас в доме постоянно есть пиво...
  wait 2s
  ик
  г Стала ночью она наведываться к нам и пить по-тихому пиво.
  г Прошлые две ночи Медведь с Домовым по очереди охраняли бочку,
  г Дык получили от Вирявы по шеям...
  г А сегодня моя очередь сторожить, боюсь не сдобровать мне.
  г Выручи старика, ведь и не воин то я совсем..
  wait 1s
  if %actor.sex% == 1
    благослав %quest761.name%
    say Но я верю в тебя добрый молодец, ты справишся с ней...
  elseif %actor.sex% == 2
    благослав %quest761.name%
    г %quest761.name%, я верю в твои силы, ты ее одолеешь... 
  else
    благослав %quest761.name%
    г Я верю в тебя... 
  end
  detach 76103 %self.id%
elseif %quest761.id% == %actor.id%
  wait 1s
  г Тебя я уже просил о помощи, все бестолку...
  вздох
else               
  wait 1s
  г Нет друг, я уже попросил %quest761.vname%.о помощи.
end
~
#76105
юртай принимает квест~
0 j 100
~
wait 1
if (%object.vnum% != 76101)
  г Что за дрянь ты мне здесь суешь ??
  бро all
  halt
end
wait 1
mpurge %object%
if (%quest761.id% != %actor.id%)
  хвал .%actor.name%
  halt
end
wait 1s
detach 76104 %self.id%
г Глупо было сомневаться в тебе!
г Сила твоя превзошла все мои ожидания
покл .%actor.name%
г Вот теперь мы снова заживем как в сказке!
eval nagrad %random.100%
if ((%nagrad% < 20) && (%world.curobjs(76171)% < 4))
  eval nagrvnum 76171
elseif ((%nagrad% < 40) && (%world.curobjs(76172)% < 4))
  eval nagrvnum 76172
elseif ((%nagrad% < 60) && (%world.curobjs(76174)% < 4))
  eval nagrvnum 76174
end
if (!%nagrvnum%)
  г Возми-ка немного деньжат, припас я на черный день.
  eval temp %actor.gold(+5000)%
  %send% %actor% %self.iname% протянул Вам мешочек с кунами.
  %echoaround% %actor% %self.iname% протянул %actor.dname% мешочек с кунами.
else
  г Попробую-ка я прошить волосами Вирявы кое-что из своих изделий.
  mecho _Портной принялся прошивать что-то.
  mload obj %nagrvnum%
  дать all .%actor.name%
  брос all
end
~
#76106
медведь приветствие~
0 q 100
0~
wait 1
if !(%exist.mob(76107)%)
  halt
end
wait 1s
if (%actor.sex% == 1)
  г Здраве буде, мил человек.
  г Как добрался до наших мест, %actor.name%?
  г Ну коль ты смог найти дорогу сюда, ты сможешь выполнить мою просьбу...
  wait 1s
  г Стал лазить в наш сад какой-то зверь...
  г То ветки поломает, то плоды унесет, то кору пожрет.
  рыд %actor.name% 
  г Мы тебя в обиде не оставим, помоги избавиться от зверя.
elseif 
  (%actor.sex% == 2)
  г Здраве буде, красавица.
  г Как ты добрлась до наших мест %actor.name%?
  г Ну коль ты смогла найти дорогу сюда, ты сможешь выполнить мою просьбу...
  wait 1s
  г Стал лазить в наш сад какой-то зверь...
  г То ветки поломает, то плоды унесет, то кору пожрет.
  рыд %actor.name% 
  г Мы тебя в обиде не оставим, помоги избавиться от зверя.
  wait 1s
  г Но будь осторожен, мой шурин дворовой как с похмелья, так гостей не шибко то жалует!
end
~
#76107
медведь принимает квест~
0 j 100
0~
wait 1
if  (%object.vnum% != 76102)
  вопрос
  wait 1s
  г И зачем мне это?
  бросить все
  if %exist.mob(76107)%
    г Ты мне лучше шкуру зверя, что в наш сад лазает, принеси... 
  end
  хму
  halt
end
wait 1
mpurge %object%
улы %actor.iname%
wait 1s
г Ох, ты убил%actor.g% его!
г Хмм, это был всего лишь хомяк?
г Шкура большая, наверное, он был огромный.
дум
wait 1s
г Но теперь сад будет приносить свои плоды!
mload obj 76103 
дать красн.яблочко %actor.iname% 
wait 1s
г Ах да, пойду спрошу Юртая, может сделаем что из шкуры.
в
с
wait 3s
ю
з
eval nagrad %random.100%
if %nagrad% < 12
  if %world.curobjs(76104)% < 10
    г Наш Юртай на все руки мастер!
    г Смотри что у него получилось.
    mload obj 76104
    дать все .%actor.name%
  else 
    г Шкура оказалась подпорченной, ничего путного не вышло
    взд
  end
elseif %nagrad% < 24
  if %world.curobjs(76170)% < 8
    г Наш Юртай на все руки мастер!
    г Смотри что у него получилось.
    mload obj 76170
    дать все .%actor.name%
  else 
    г Шкура оказалась подпорченной, ничего путного не вышло
    взд
  end
else 
  г Шкура оказалась подпорченной, ничего путного не вышло
  взд
end
~
#76108
вошли к домовому~
0 r 100
0~
wait 1s
г Здравствуй %actor.name%!
г Не бойся меня, я хоть и нечисть, но добрая.
г Сам%actor.g% видишь, живем дружно с портным и медведем.
улы %actor.name%
wait 1s
if (%exist.mob(76106)%)
  г Все бы хорошо, да Вирява, старая ведьма, пиво наше ворует,
  г поговори с Юртаем, может ты чем-нибудь поможешь.
end
wait 2s
if (%exist.mob(76107)%)
  г Да еще зверь какой-то в наш сад повадился лазить, об этом с медведем переговори
  г А благодарность себя ждать не заставит.
end
~
#76109
хомяк убит~
0 f 100
~
mload obj 76102
~
#76110
Вирява убита~
0 f 100
~
mload obj 76101
if (%world.curobjs(76137)% < 8 ) && (%random.100% < 10)
  mload obj 76137
  *голова Вирявы
end
if (%world.curobjs(76137)% < 4 ) && (%random.100% < 7)
  mload obj 76107
end
~
#76111
крылатый черт убит~
0 f 100
~
mload obj 76100
if (%world.curobjs(76114)% < 4 ) && (%random.100% < 10)
  mload obj 76114
  *кольцо духа
end
if (%world.curobjs(76115)% < 4 ) && (%random.100% < 10)
  mload obj 76115
  *кольцо уверенности
end
~
#76112
Репоп~
2 f 100
~
calcuid urtay 76101 mob
detach 76103 %urtay.id%
detach 76104 %urtay.id%
attach 76103 %urtay.id%
wait 1
calcuid garden 76113 room
detach 76100 %garden.id%
attach 76100 %garden.id%
~
#76113
двоедушник возвращается~
0 ab 25
~
if !%self.fighting%
  mtransform 76116
  attach 76102 %self.id%
  detach 76113 %self.id%
end
~
#76114
леший убит~
0 f 100
~
if (%world.curobjs(76121)%) < 4 && (%random.100% < 2)
  mload obj 76121
  *пояс лекаря
end
~
#76115
упырь убит~
0 f 100
~
if (%world.curobjs(76129)%) < 6 && (%random.100% < 15)
  mload obj 76129
  *перст упыря
end
~
#76116
одевают челюсть~
1 j 60
~
wait 1s
osend       %actor% _Челюсть злобно ощерилась мелкими зубами.
oechoaround %actor% _Челюсть шакала злобно ощерилась на %actor.vname%.
~
#76117
вошли к Виряве~
0 q 100
~
wait 1
хох
wait 1s
г На ловца и зверь бежит, хорошие отвары сегодня из вас получатся !
~
#76118
лезть в нору~
2 c 1
лезть пролезть спуститься~
wait 1
if !%arg.contains(нор)%
  wsend %actor% _Куды лезешь?!
  halt
end
if !%actor.rentable%
  wsend %actor% Эвакуация временно прекращена.
  halt
end
calcuid cavern 76114 room
set test %cavern.pc%
if %test%
  wsend %actor% _Там уже кто-то есть!
  halt
end
wsend %actor% _Согнувшись в три погибели вы полезли в нору.
wechoaround %actor% Согнувшись в три погибели, %actor.name% полез%actor.q% в нору.
wteleport %actor% 76114
~
#76119
тенетник убит~
0 f 100
~
if (%world.curobjs(76113)% < 4) && (%random.100% < 8)
  mload obj 76113
  *шерстка паука
end
if (%world.curobjs(76130)% < 4) && (%random.100% < 12)
  mload obj 76130
  *брюхо тенетника
end
if (%world.curobjs(76136)% < 4) && (%random.100% <8)
  mload obj 76136
  *перчатки из паучьей нити
end
~
#76120
шакал убит~
0 f 100
~
if (%world.curobjs(76120)% < 5 ) && (%random.100% <8)
  mload obj 76120
  msend             %actor% Последним ударом Вы отрубили у шакала челюсть.
  mechoaround %actor% Последним ударом %actor.iname% вырвал%actor.g% у шакала челюсть.
  *челюсть шакала
end
~
#76121
Заходят к лешему~
0 q 50
~
wait 2
set victin %random.pc%
mechoaround %actor% _Лицо %actor.rname% вдруг приняло осоловелое выражение и он%actor.g% поплел%actor.u% куда глаза глядят.
msend %actor% Ваши ноги вдруг сами собой понесли вас куда-то в сторону.
switch %random.4%
  case 1
    mforce %actor% north
  break
  case 2
    mforce %actor% east
  break
  case 3
    mforce %actor% west
  break
  case 4
    mforce %actor% south
  done
~
$~
