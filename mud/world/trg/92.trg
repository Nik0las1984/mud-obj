#9200
приветствие~
0 q 100
~
wait 1s
г Здравствуй, %actor.iname%. Представляешь уехал я, значит, к родственникам на несколько седмиц..
г Приезжаю, а тут на тебе: жуков, пауков, мышей и прочей мерзости полный дом!
г А я хоть и жил все жизнь в деревне, всяких насекомых мелких терпеть с детства не могу.
г Ума теперь не приложу, что с ними можно сделать... 
г Может ты найдешь способ извести этих гадов?
%send% %actor% Хозяин дома с мольбой в глазах взглянул на Вас.
%echoaround% %actor% Хозяин дома с мольбой в глазах взглянул на %actor.vname%.
~
#9201
выдачаквеста~
0 d 0
найду~
if %speech% == найду
  if (!%q92%)
    г Спасибо, что не отказал%actor.g% в помощи!
    г Я тебя здесь подожду.
    г Скажешь когда все будет готово.
    %load% mob 9211
    %teleport% %world.mob(9211)% 9268
    откр кал с
    makeuid q92 %actor.id%
    global q92
    attach 9209 %self%
    detach 9200 %self%
    attach 9202 %world.mob(9211)%
    remote q92 %world.mob(9211)%
  else
    г Спасибо, тебе странник, но уже нашелся помощник... вот жду теперь.
  end
end
~
#9202
хозяевакозлы~
0 r 100
~
if (%actor.id% == %q92.id%)
  wait 1s
  хмур %actor.name%
  г Чего надобно?
  г А, хозяин от насекомых избавиться просил...
  г Сам виноват, уехал неизвестно куда, а мне даже поесть ничего не оставил.
  обид
  г Как я им на голодный желудок буду всю эту живность изводить?!
  вопр %actor.iname%
  wait 1s
  г Не знаешь? Вот и я не знаю...
  г Так ему и передай!
  attach 9205 %world.mob(9214)%
end
~
#9204
zonereset~
2 f 100
~
if (%world.curmobs(9211)% > 0)
  %purge% %world.mob(9211)%
end
rdelete q92 %world.mob(9214)%
detach 9205 %world.mob(9214)%
detach 9209 %world.mob(9214)%
attach 9200 %world.mob(9214)%
attach 9201 %world.mob(9214)%
detach 9206 %world.mob(9210)%
detach 9207 %world.mob(9210)%
~
#9205
чемкормить~
0 r 100
~
wait 1
if (%actor.id% == %q92.id%)
  %echo% Вы рассказали мужику про домового.
  эм виновато опустил глаза
  г Даа, не хорошо получилось...
  г Как же я так забыл про него? Что же теперь делать?
  дума
  wait 1s
  сият
  г Есть одна идея! Может получится его задобрить!
  г Говорят молоко домовые очень любят.
  г Надо найти где-нибудь кувшинчик молока, и дать его домовому.
  г Может забудет он свою обиду. Скажешь, как готово будет.
  attach 9206 %world.mob(9210)%
  attach 9207 %world.mob(9210)%
  detach 9205 %self%
end
~
#9206
доитькорову~
0 c 0
доить корову~
%send% %actor% Корова в ужасе попятилась от Вас.
%echoaround% %actor% Корова в ужасе попятилась от %actor.rname%.
г Ты чего творишь, а?!
wait 1s
г Ой! Ты наверно молока, всего лишь хочешь?
красн
г Давай сюда кувшин, наберу тебе молока.
set dk 1
global dk
~
#9207
наполняемкувшин~
0 j 100
~
wait 1s
if (!%dk% || %object.vnum% != 9204)
  %send% %actor% Корова испугано замычала и отошла от Вас подальше.
  %echoaround% %actor% Корова испугано замычала и отошла от %actor.rname% подальше.
  брос %object.iname%
  halt
end
if (%object.val1% != 0)
  г Не хорошо мое благородное молоко с чем то смешивать!
  г Давай мне пустой кувшин!
  give кувш %actor.iname%
  halt
end
%echo% Прихватив с собой глиняный кувшин, корова ушла в хлев.
%load% obj 9205
перел ведр кувш
%purge% ведр
wait 1s
%echo% Спустя несколько минут, корова вернулась с полным кувшином молока.
г Вот! Пей на здоровье!
give кувш %actor.iname%
rdelete dk %self.id%
attach 9208 %world.mob(9211)%
detach 9202 %world.mob(9211)%
~
#9208
кормятдомового~
0 j 100
~
wait 1s
if (%object.vnum% != 9204)
  г Чего это ты мне подсовываешь? Я такое не ем...
  морщ
  give %object.iname% %actor.iname%
  halt
end
эм заглянул в кувшин с молоком почти с головой
wait 1s
if (%object.val1% < 10)
  г Кто ж этим наестся? Неси больше!
  give кувш %actor.iname%
  halt
end
if (%object.val2% != 10 || %object.val3% == 1)
  г Сперва не кормят месяцами, потом еще и отравить хотят?!
  руг
  wait 1s
  г Да ну вас всех! Изводите всю эту мерзость сами! Пойду в другой дом!
  обид
  wait 1s
  эм развернулся и тут же исчез в темном углу
  %purge% %self%
  halt
end
сият
%echo% Домовой шумными глотками принялся хлебать молоко из кувшина.
%purge% %world.obj(9204)%
г Ой, вкуснатищ-ща! Ой, благодарю!
г Так уж и быть, изведу всю мерзость!
: достал коробочку с надписью "Насекомые и другая мерзость" и направился к выходу
set animals 9200 9201 9202 9203 9204 9205 9206 9207 9209 9215
foreach animal %animals%
  while (%world.curmobs(%animal%)% > 0)
    %purge% %world.mob(%animal%)%
  done
done
detach 9202 %self%
detach 9207 %world.mob(9210)%
detach 9206 %world.mob(9210)%
%purge% %self%
~
#9209
наградазаквест~
0 d 100
готово~
wait 1
if (%actor.id% != %q92.id%)
  г Что готово? Еда готова? Не.. спасибо, я не голоден..
  halt
end
set animals 9200 9201 9202 9203 9204 9205 9206 9207 9209 9215
foreach animal %animals%
  while (%world.curmobs(%animal%)% > 0)
    calcuid tmpanimal %animal% mob
    г Обманываешь ты меня... не всю мерзость ты убил!
    if (%tmpanimal.sex% == 2)
      г Одна %tmpanimal.iname% точно осталась!
    else
      г Один %tmpanimal.iname% точно остался!
    end
    halt
  done
done
г ЧТО?! И правда всех извел...
удивл
wait 1s
г Что ж по праву награду заслужил!
if (%random.num(1000)% < 100)
  г Вот возьми, вдруг пригодится.
  %load% obj 1703
  дать книга %actor.iname%
elseif (%random.num(1000)% < 200)
  г Вот возьми, вдруг пригодится.
  %load% obj 1712
  дать книга %actor.iname%
else
  г Вот прими немного денег в благодарность
  %self.gold(50)%
  give 50 кун %actor.iname%
end
rdelete q92 %self.id%
if (%world.curmobs(9211)% > 0)
  %purge% %world.mob(9211)%
end
detach 9201 %self%
detach 9209 %self%
detach 9209 %self.id%
~
#9210
Если даем фигню хозяину~
0 j 100
~
wait 1s
г Зачем мне это?
дум
бросить все
~
$~
