#6400
лягушка приветствует~
0 r 100
~
if %actor.sex%==1
  wait 1s
  : чуть не околела от счастья при виде Вас и начала прыгать около Ваших ног.
  wait 1s
  г Ой, я так долго тебя ждала, так ждала!!!
  г Ты даже не представляешь как я вас ждала!!!
  wait 1s
  г Это наверное ваша стрела прилетела ко мне.
  : показала каленую стрелу.
  г Попала прямо в меня, ранила. Рана неглубокая, но как настоящий мужчина, ты должен на мне женится.
  wait 2s
  г Ну поцелуйте же меня и мы будем жить вместе долго и счастливо!
  wait 1s
  г А потом умрем в один день
  wait 1s
  : вытягивает губы трубочкой для поцелуя
else
  вздох
end
~
#6401
рассказоцаревне~
0 r 100
~
wait 1s
say %actor.name%, как ты смог%actor.q% попасть сюда? Это моя территория!
say Раз прош%actor.y%, давай расскажу одну историю, не часто бывает у меня нормальный собеседник
wait 1s
г Так вот жила была на свете царевна. Ничего себе такая, вся из себя и вообще.
г Но считала она себя самой красивой и фыркала, когда видела некрасивых людей.
г Например навроде меня.
хих
wait 1s
г Задело это меня, изучил я много наук, много земель обошел, но нашел одно средство и...
хих
wait 1s
г Однажды, превратилась царевна как-то в земноводное не очень приятной наружности.
г В народе лягушками или жабами кличут.
wait 1s
г Правда за это мне тоже пришлось заплатить.
г Но за это я теперь могу питаться только жабами и лягушками.
г За каждую жабу буду платить 50 монет, за лягушку 25.
~
#6402
поцелуй~
0 c 1
поцеловать~
if !%arg.contains(лягушку)% && !%arg.contains(царевну)%
  return 0
  halt
end
wait 1
if %actor.sex%==1
  msend %actor% Вы закрыли от ужаса глаза и коснулись губами склизкой кожи.
  mechoaround %actor% %actor.name% наверное рехнулся, раз уж стал целовать лягушку
  mecho  Если бы лягушка не была зеленой, то покраснела бы со стыда.
  wait 1s
  г Ну на Вас управы уже никакой нет! Распустились совсем! Обнаглели!!!
  г Ходят по лесу маньяки к честным лягушкам пристают...
  г Много Вас тут таких с поцелуями... Зоофилы чертовы!
  wait 1s
  г Что Вы обо мне думаете такое? Я девушка честная! Я не позволю...
  красн
  wait 1s
  г Хоть бы кто догадался подарок сделать... нет ведь сразу целоваться лезут!
  if %world.curobjs(6404)% < 1
    mload obj 6404
    дать стрела %actor.name%
    г И забери эту стрелу - что мне все время ее теперь держать.
  end
else
  msend %actor% Ай-ай, заморскими пакостями заниматься, поищи лягушкек-царевичей мужеского полу.
end
~
#6403
дали подарок лягушке~
0 j 100
~
wait 1
if (%object.vnum% != 6402)
  брос все
  halt
end
wait 1
mpurge %object%
wait 1
г О! Вот это другое дело! А то сразу целоваться лезут! Давно бы так...
wait 1s
г Знаешь, ты конечно очень даже ничего, но вот замуж за тебя я не выйду!
г Стрела-то не твоя была, обманул ты меня, не мой ты жених!
г Но подарок ты принес мне непростой, теперь если жених настоящий найдется, то расколдует он меня.
wait 1s
г А чтоб утешить тебя... возьми на память обо мне вот это.
if (%world.curobjs(6405)% < 10) && (%random.4%==1)
  mload obj 6405
  дать перстень %actor.name%
else
  эмоц покраснела.
  wait 2s
  %self.gold(+300)%
  дать 300 кун %actor.name%
end
mecho Лягушка прыгнула в болото и спряталась от Вас.
mpurge %self%
~
#6404
встречастарик~
0 r 100
~
wait 1
msend %actor% Старик оторвался от рассматривания округ и повернулся к Вам.
msend %actor% Привет, помоги старику несчастному.
вздох
msend %actor% Сына непутевого найти не могу. Одно несчастье, не даром Иван-Дурак все кличут.
msend %actor% Как нормального вывел с остальными сыновьями по нашему обычаю выбирать невест.
msend %actor% На чей двор стрела попадет - там невеста и живет.
wait 1
msend %actor% Старшие не дураки - прицелились и один в купеческий двор попал, другой в боярский.
msend %actor% А этот в болото запустил, да ведь не близко, силища то огромная.
wait 1
msend %actor% Вот жду его с болота. Помоги старому, зрение никчемное, ничего не вижу.
msend %actor% Хожу еле-еле, приведи сынка, отблагодарю как положено.
~
#6405
убили кошку~
0 f 5
~
if %world.curobjs(6401)% < 10
  mload obj 6401
end
~
#6406
убили жабоглота~
0 f 100
~
mecho Жабаглот сказал: Мда... надо же, убил%actor.g% меня!
mecho Слушай один секрет: кто найдет и поцелует царевну, значить, расколдует ее.
хох
mecho Правда еще кое-что надо.
if %world.curobjs(6402)% < 10
  mload obj 6402
end
~
#6407
убили мышь~
0 f 25
~
if %world.curobjs(6403)% < 100
  mload obj 6403
end
~
#6408
убили буккена~
0 f 10
~
if %world.curobjs(6409)% < 10
  mload obj 6409
end
~
#6409
убили мымру~
0 f 5
~
if %world.curobjs(6407)% < 10
  mload obj 6407
end
~
#6410
парня нашли~
0 r 100
~
wait 1s
: удивился вашему появлению, потянулся с хрустом.
wait 2s
г Поспал неплохо, правда печи нет, холодно, да и ладно.
wait 1s
г Притомился я здесь бродить. Угораздило меня стрельнуть из лука куда-то в этом направлении.
г И должен я эту стрелу найти, не найду - позор мне, не вернусь из леса пока не сыщу.
~
#6411
дали стрелу парню~
0 j 100
~
wait 1
if (%object.vnum% != 6404)
  брос все
  halt
end
wait 1
mpurge стрела
if (%world.curobjs(6408)% < 1)
  wait 1s
  г Здорово. Где ты ее нашел?
  wait 1s
  г У лягушки с короной. И говорит, что я жениться на ней должен?
  вздох
  wait 2s
  г Ну что же делать, пойду к ней, значит судьба у меня такая.
  г Спасибо тебе за все, передай весточку отцу моему старому, вдруг не увижусь больше с ним.
  wait 2s
  mload obj 6408
  дать записка %actor.name%
else
  г Так дело не пойдет - по-моему стрела поддельная.
  хму
end
mecho Парень убежал куда-то в кусты.
mpurge %self%
~
#6412
дали записку старику~
0 j 100
~
wait 1
if (%object.vnum% != 6408)
  брос все
  halt
end
wait 1s
mpurge записка
mecho Старик прочитал записку и сморщился..
wait 1s
г Ладно, нашел ты его - весточку принес, дело сделано.
%self.gold(+100)%
дать 100 кун %actor.name%
wait 1s
г А я лучше, пойду к дому, другим сыновьям расскажу.
mecho Старик ушел.
mpurge %self%
~
#6413
разрыть~
2 c 1
разрыть~
if !%arg.contains(завал)% && !%arg.contains(разрыть)%
  return 0
  halt
end
wait 1
wdoor 6422 north room 6425
wsend %actor.name% Вы разрыли завал и открылся вход в нору.
wechoaround %actor% %actor.name% разрыл%actor.q% завал и открылся вход в нору.
detach 6413 %self.id%
~
#6414
пуржевыход~
2 f 100
~
wecho Разрытая земля обвалилась и закрыла завал.
if %self.north%
  wdoor 6422 north purge
end
*wdoor 6422 north purge
detach 6413 %self.id%
attach 6413 %self.id%
~
$~
