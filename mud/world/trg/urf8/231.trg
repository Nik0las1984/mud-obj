#23100
вход к кузнецу~
0 q 100
~
wait 10
mecho _Кузнец не отрываясь от работы взглянул на Вас и что-то проворчал по поводу 
mecho _вежливости нынешней молодежи.
~
#23101
ответ на приветствие~
0 d 1
здравствуй поклон приветствую здраве~
wait 15
г И тебе не болеть
ворч %actor.iname%
wait 30
mecho Кузнец сделал шаг к непримеченной Вами ранее кадке, полез туда руками.
руг
wait 30
г Ох горе мне с этой нечистью. Опять все растащили, проклятые!
взд
wait 30
г Вот что. Коли поможешь мне - в долгу не останусь.
г Самому-то мне уже не посилам это дело - стар стал уже...
wait 10
г Поможешь?
attach 23102 %self.id%
~
#23102
выдача задания~
0 d 1
да помогу говори слушаю~
г Ну слушай тогда...
взд
wait 8
г Для работы своей заготовил я прутьев железных. Да в болото кинул, чтоб вся гниль из железа вышла
wait 10
г Вот не далече как вчера забрал я их - хотел меч выковать. А вот кинулся - нету.
wait 7
г Искать я тебя не прошу - знаю где они. Анчутки - бесы болотные утащили.
г И что им до того железа, окаянным?!
wait 10
г Ладно. Принесешь - расплачусь, в долгу не останусь.
г Все! Иди.
ворч
attach 23103 %self.id%
detach 23100 %self.id%
detach 23101 %self.id%
detach 23102 %self.id%
~
#23103
оплата~
0 j 100
~
wait 1
if (%object.vnum% != 23103)
  г Ну и на кой мне это?
  эм нахмурил брови
  бросить %object.name%
  halt
else
  г Спасибо. Это то что нужно.
  if (%actor.class% == 11)
    wait 1
    г Я смотрю, ты тоже кузнечным делом занимаешься
    г Может смогу тебя чему-нибудь обучить.
    wait 1s
    if ((!%actor.skill(перековать)%) && (%actor.can_get_skill(перековать)%))
      mskillturn %actor% перековать set
    else
      г Извини, ты уже кузнец не хуже меня.
    end
  end
  г Вот, возьми, пригодится в хозяйстве.
  eval rnd %random.1000%
  if (%rnd% < 40) && (%world.curobjs(23104)% < 15)
    mload obj 23104
    дать колечко .%actor.name%
  elseif (%rnd% < 70) && (%world.curobjs(23105)% < 20)
    mload obj 23105
    дать нож .%actor.name%
  elseif (%rnd% < 100) && (%world.curobjs(23106)% < 30)
    mload obj 23106
    дать оберег .%actor.name%
  elseif (%rnd% < 300) && (%world.curobjs(23107)% < 30)
    mload obj 23107
    дать браслет .%actor.name%
  elseif (%rnd% < 400)
    mload obj 542
    дать книга .%actor.name%
  else
    msend %actor% Кузнец дал Вам немного денег.
    %actor.gold(+250)%
    msend %actor% Это составило 250 кун.
    mechoaround %actor% Кузнец дал %actor.dname% горстку монет.
  end
end
~
#23104
Убили анчтуку~
0 f 100
~
eval rnd 10*%world.curmobs(23101)%
if (%random.100% > %rnd%)
  mload obj 23103
end
~
$~
