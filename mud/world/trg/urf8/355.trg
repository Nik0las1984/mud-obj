#35500
умер командир(00)~
0 f 100
*~
if (%random.1000% < 80) && (%world.curobjs(35514)% < 6)
  mload obj 35514
end 
if (%random.1000% < 155) && (%world.curobjs(35523)% < 6)
  mload obj 35523
end 
if (%random.1000% < 210) && (%world.curobjs(35526)% < 6)
  mload obj 35526
end 
~
#35501
умер витязь(01)~
0 f 100
~
if (%random.1000% < 100) && (%world.curobjs(35518)% < 6)
  mload obj 35518
end 
if (%random.1000% < 175) && (%world.curobjs(35524)% < 6)
  mload obj 35524
end 
if (%random.1000% < 225) && (%world.curobjs(35526)% < 6)
  mload obj 35526
end 
~
#35502
умер богатырь(02)~
0 f 100
~
if (%random.1000% < 100) && (%world.curobjs(35512)% < 6)
  mload obj 35512
end 
if (%random.1000% < 175) && (%world.curobjs(35525)% < 6)
  mload obj 35525
end 
if (%random.1000% < 225) && (%world.curobjs(35526)% < 6)
  mload obj 35526
end 
~
#35503
умер охранник1(03)~
0 f 100
~
if (%random.1000% < 100) && (%world.curobjs(35508)% < 6)
  mload obj 35508
end 
if (%random.1000% < 175) && (%world.curobjs(35515)% < 6)
  mload obj 35515
end 
~
#35504
умер охранник2(04)~
0 f 100
~
if (%random.1000% < 100) && (%world.curobjs(35509)% < 6)
  mload obj 35509
end
~
#35505
вооружаем купца(05)~
0 n 100
~
if (%random.1000% < 100) && (%world.curobjs(35519)% < 6)
  mload obj 35519
  воор дротик
end
~
#35506
умер молодой охотник(06)~
0 f 100
~
if (%random.1000% < 80) && (%world.curobjs(35520)% < 6)
  mload obj 35520
end 
~
#35507
умер опытный охотник(07)~
0 f 100
*~
if (%random.1000% < 80) && (%world.curobjs(35521)% < 6)
  mload obj 35521
end 
if (%random.1000% < 140) && (%world.curobjs(35517)% < 6)
  mload obj 35517
end 
~
#35508
умер тать(м14)~
0 f 100
~
if (%world.curobjs(35507)% < 1)
  mload obj 35507
end 
~
#35509
умер монах(м07)~
0 f 100
~
if !%exist.obj(35531)%
  %load% obj 35531
end
~
#35510
грузим странника (р38)~
2 f 100
~
if %random.1000%<=250
  if !%exist.mob(35513)%
    %load% mob 35513
  end
end
~
#35511
витязь дозорного отряда оценивает (m00)~
0 q 100
~
msend       %actor% Витязь внимательно осмотрел Вас, наклонился к командиру и стал шептать что-то тому на ухо.
mechoaround %actor% Витязь внимательно осмотрел %actor.vname%, наклонился к командиру и стал шептать что-то тому на ухо.
wait 1s
if (%actor.sex%==1)
  mecho До Вас донеслось: "может этот сгодится?" 
  wait 1s
  msend       %actor% Командир дозорного отряда недоверчиво взглянул на Вас и пробормотал: "Хлипкий какой-то..."
  mechoaround %actor% Командир дозорного отряда недоверчиво взглянул на %actor.vname% и пробормотал: "Хлипкий какой-то..."
else 
  mecho До Вас донеслось: "может эта сгодится?" 
  wait 1s
  msend       %actor% Командир дозорного отряда недоверчиво взглянул на Вас и пробормотал: "Хлипкая какая-то..."
  mechoaround %actor% Командир дозорного отряда недоверчиво взглянул на %actor.vname% и пробормотал: "Хлипкая какая-то..."
end
calciud commander 35501 mob
%commander.wait(2)%
~
#35512
Игрок говорит в клетке с командиром отряда(m01)~
0 d 1
испытай~
wait 1s
if %actor.sex%==1
  say Испытать, говоришь... ну что ж я смотрю ты парень не промах...
else
  say Испытать, говоришь... ну что ж я смотрю ты девица хоть куда...
end
wait 1s
say Хм, ну слушай. 
wait 1s
say Послал князь отряд наш изловить зверя лютого, что завелся в водах Ильмень-озера 
say и топит купеческие ладьи. 
wait 1s
say Да не можем мы его найти, а тут как на грех отряд урманей в окрестностях Русы замечен был. 
wait 1s
say Не иначе как к набегу готовятся. Некогда нам зверя этого искать, дозором ходить надобно, 
say чтоб проклятые врасплох не застали.
wait 1s
say Так вот, коли найдешь зверя и принесешь доказательства смерти его - я отдам тебе княжескую награду. 
wait 1s
say И тебе хорошо, и мы свое задание выполним.
wait 1s
вопр .%actor.name%
detach 35511 %world.mob(35500)%  
detach 35512 %world.mob(35501)%  
~
#35513
вошли к монаху(m07)~
0 q 100
~
wait 1s
mecho Монах перекрестился, внимательно посмотрел на Вас, но не перестал перебирать четки.
wait 2s
say Вижу я человек ты праведный. 
say А потому не должен отказать мне в просьбе, за то Господь тебе зачтет! 
wait 1s
mecho Монах обратил свой взор к небесам.
wait 2s
say И в миру средь христиан тебе почет и уважение будет. 
wait 1s
say Вижу, что готов слушать - так слушай!
say Послала меня братия Юрьева монастыря в эти места, 
say дабы молитвой святой изгнал я духа окаянного волхва, 
say что скрывается где-то здесь вот уже много лет, причиняя христианам всякие страдания.
wait 1s
say Да в дороге занемог я, и сил нету идти больше. 
wait 1s
say Вот сижу и Господу молюсь, чтобы дал он мне время закончить дело сие благое, 
say да чтобы после мог я с чистой душой пред ним на Суд предстать.
say Кабы ты вот нашел то место, где дух этот обитает, да извел бы его - то уж благословлен
say был бы и сам ты и род твой до седьмого колена.
мечт
wait 1s
say Да задача не каждому такая под силу. 
waiy 1s
say Волхв тот и при жизни был хитрою подлюкою, а уж дух его после смерти и подавно 
say во сто крат хитрее и изворотливее. 
say Говорят, ничем его извести нельзя, кроме как молитвой монаха, но ты же ведь не монах!
дум
wait 2s
say Ходят, правда байки, что помогает извести подобную нечисть какая-то побрякушка местных рыбаков. 
wait 1s
пож
wait 1s
say Да только ересь это все и мерзость перед Богом! 
say Ибо не верю я, что кроме молитвы помочь что-то может!
say Знать мне самому придется сил набираться и дальше поиск продолжать...
взд
~
#35514
Борята перевозит через реку(m18)~
0 m 0
~
if %amount%<10
  say Тююю, этого маловато...
else
  if %amount%>10
    mecho Борята широко улыбнулся.
    say Добро пожаловать на борт!
  end
  mecho Борята пересчитал куны, довольно крякнул и сел за весла. 
  wait 1s
  mecho Через десять минут Вы уже стояли на другом берегу Ловати.
  mat 35548 mecho Кто-то выбрался на берег из лодки.
  %teleport% all 35548
end
~
#35515
свистим Борятку(r48)~
2 c 1
свистнуть~
%send% %actor% Вы заложили два пальца в рот и засвистели так, что аж уши заложило!
%echoaround% %actor% %actor.name% заложил%actor.g% два пальца в рот и громко свистнул%actor.g%.
wait 1s
%echo% Борята услышал свист, махнул рукой и направился к вашему берегу.
%echo% Борята сказал: "За доставку возьму не дорого - всего десять кун со всех"
wait 2s
if %actor.gold%<10
  %echo% Борята возмущенно сказал: "Эй, зачем звали, если денег нету?!"
else
  eval buf %actor.gold(-10)%
  %echo% Борята пересчитал куны и бодро взялся за весла.
  wait 2s
  %echo% И через несколько минут вы уже были на другом берегу Ловати.
  wat 35550 wecho Кто-то выбрался на берег из лодки.
  %teleport% all 35550
end
~
#35516
вошли к местному рыбаку(m23)~
0 q 100
~
wait 1s
%echo% Местный рыбак поднял на Вас глаза с надеждой.
wait 1s
say А нет ли у тебя, мил человек, лишней сети?
say Вчера на рыбалке неведомо что зашло в мою сеть и изорвало ее в клочья.
say Чем мне теперь кормить семью?
рыдать
~
#35517
вошли к восточному купцу(m15)~
0 q 100
~
wait 1s
Say Ох, и народец здесь, глаз да глаз за всем нужен! Одно ворье!
Mecho Купец оценивающе взглянул на Вас как бы решая кто Вы - покупатель или вор?
~
#35518
покупаем сеть(m15)~
0 d 0
купить куплю~
wait 1s
if %speech.words%>2
  say Говори четко, проси мало, уходи быстро!
  halt
end
if !%speech.contains(сеть)%
  say Не понял я, что купить то ты хочешь?
else
  wait 1s
  say Сеть купить хочешь? 
  say Понимаешь ли... у меня всего одна... и везу я ее в Новгород - там заказ на нее есть.
  say Ты все равно мне столько не заплатишь, сколько в Новгороде обещали.
  wait 1s
  дум
  wait 1s
  осм .%actor.name%
  wait 1s
  say Хотя... если тебе сеть нужна очень, я готов заключить с тобой сделку. 
  say Приключилось с нами в этих местах неприятность. 
  say Кто бы мог подумать, что в здешних лесах такое жулье живет! 
  say Сперли у моего охранника его кинжал, да так, что тот и не заметил когда именно сперли!
  wait 1s
  say Ну ворье!
  wait 1s
  say Ты ведь местный, может тебе проще будет узнать кто это сделал? 
  say А коли вернешь кинжал - так я тебе сеть бесплатно отдам!
  дум
end
~
#35519
принимаем предложение купца(m15)~
0 d 1
договорились хорошо~
wait 1s
say Отлично! По рукам! 
Ул
say А в помощь тебе, я хочу дать вот эту штуку! 
say Очень она полезна для поимки всяких проходимцев!
wait 1s
if %exist.obj(35544)%
  %echo% Купец открыл сундук и начал что-то искать внутри...
  wait 1s
  морг
  wait 1s
  say ...странно, пропала...
  wait 1s
  say а ведь одна такая на весь свет!
else
  %echo% Купец вытащил из сундука чудо-посох.
  %load% obj 35544
  Дать все .%actor.name%
end
detach 35518 %self.id%
detach 35517 %self.id%
attach 35520 %self.id%
detach 35519 %self.id%
~
#35520
даем вещи купцу(м15) ~
0 j 100
~
wait 1s
if ((%object.vnum%<>35507)&&(%object.vnum%<>35545))
  say Это мне? А оно мне нужно?
  брос все
  halt
end
if (%object.vnum%==35507)
  say О, да! Это кинжал моего охранника!
  say Абдула, вот твое оружие!
  wait 1
  %echo% Купец отдал кинжал охраннику
  wait 1
  say  Ну, как и договаривались, в награду за свою службу получай новенькую сеть!
  wait 1s
  %purge% %object.id%
  if !%exist.obj(35547)%
    %load% obj 35547
    Дать сеть .%actor.name%
  else
    say ...странно. Пропала сеть! Единственная и неповторимая! 
    say Что творится, ужас!
  end
end
if (%object.vnum%==35545)
  say О! да ты серьезный покупатель, как я погляжу!
  say Это хорошая цена за мой товар!
  say Вот держи!
  %purge% %object.id%
  if !%exist.obj(35548)%
    %load% obj 35548
    дать горш %actor.name%
  else
    wait 1s
    дум
    say Слушай, не могу найти... Извини... Свентовит его знает, куда подевалось... 
  end
end
~
#35521
карабканье (р18)~
2 c 0
карабкаться~
if %random.100%<=25
  %send% %actor% Вы начали ловко карабкаться по стенке оврага
  %echoaround% %actor% %actor.iname% начал%actor.g% ловко карабкаться по стенке оврага 
  switch %random.3%
    case 1
      %teleport% %actor% 35514 horse
    break
    case 2
      %teleport% %actor% 35516 horse
    break
    case 3
      %teleport% %actor% 35522 horse
    break
  done
else
  %send% %actor% Вы соскользнули со стенки оврага и больно ушиблись.
  eval buf %actor.hitp(-20)%
end
~
#35522
дали сеть рыбаку(м23)~
0 j 100
~
wait 1s
if (%object.vnum%==35547)
  %echo% Рыбак чуть не подскочил на месте от радости, но тут же помрачнел.
  say Мне нечем заплатить за эту сеть, если ты подождешь сутки, я могу отдать тебе весь завтрашний улов.
  дум
  wait 1s
  say Если же не хотите ждать, все что у меня есть, так это амулет рыбацкой надежды. 
  say Говорят, он приносит счастье. Даже в самом проклятом месте достаточно бросить его на землю, 
  say как эта территория станет самой счастливой для вас.
  wait 1s
  say Вот, возьмите его!
  if !%exist.obj(35546)%
    %load% obj 35546
    дать амулет %actor.name%
  else
    wait 1s
    дум
    say Слушай, не могу найти... Извини... Свентовит его знает, куда подевалось... 
  end
  detach 35516 %self.id%
  detach 35522 %self.id%
else
  say Да не нужно мне это...
  брос все
end
~
#35523
входим к страннику (м13)~
0 q 100
~
say Я не случайно здесь оказался, труден был мой путь.
wait 1
say Я пришел издалека, что бы лучше узнать эту страну и ее народ.
wait 1
say Мне хочется знать ее будущее.
wait 1
say Помоги мне найти символы старого и нового этой страны и я открою тебе тайну того, что 
say будет с этим народом.
~
#35524
даем вещи страннику (м13)~
0 j 100
~
wait 1s
if ((%object.vnum%<>35531)&&(%object.vnum%<>35532))
  say В странствиях я видел и не такое...
  брос %object.name%
  halt
end
if (%object.vnum%==35531)
  say Отлично...
  wait 1
  say Ты принес%actor.g% символ нового.
end
if (%object.vnum%==35532)
  say Молодец...
  wait 1
  say Ты отыскал%actor.g% символ старого.
end
if (%self.haveobj(35531)%)&&(%self.haveobj(35532)%)
  хох
  %purge% %world.obj(35531)%
  %purge% %world.obj(35532)%
  say Ты хотел знать, что будет с этой страной?! 
  say Так вот - ты сам своими действиями предрешил ее судьбу.
  say Отныне не будет на Руси мира между людьми и брат будет завидовать брату.
  say И ревнители старины будут проклинать сторонников нововведений. 
  say А сами будут преследоваться за мракобесие.
  say И все соседи этой страны будут считать ее варварской, так как увидят, 
  say что русичи не могут договориться меду собой.
  say И будет так во веки веков
  хохот
  say Но ты достоин награды за свои действия.
  if !%exist.obj(35533)%
    %load% obj 35533
    Дать яблоко .%actor.name%
  else
    say Нет! Не достоин! Нету награды, не нашел! Или наградил уже кого, не помню.
    say Проваливай!
  end
  %echo% Странник сделал пассы руками и исчез в облаке вонючего сизого дыма.
  %purge% %self.id%
end
~
#35525
умер странник(м13)~
0 f 100
~
if (%random.1000% <= 50) && (%world.curobjs(35510)% < 1)
  mload obj 35510
end 
if (%random.1000% <= 250) && (%world.curobjs(35522)% < 5)
  mload obj 35522
end
~
#35526
вход к старому рыбаку (м03)~
0 q 100
~
wait 1
say Ума не приложу, куда она могла запропаститься!
say Ты не видел%actor.g% здесь уключины? Как же я теперь рыбачить без весла буду?
say Вот досада! И кто ее мог взять?
~
#35527
вход к девушке (м04)~
0 q 100
~
wait 1
if %actor.sex%==1
  say Ты не встречал Ванятку-молодца? Он обещал, что как только придет из похода, мы поженимся.
  груст
  say А его все нет и нет.
  ждать
  say Может он разлюбил меня?
  плак
  wait 1s
  say Слушай, я ведь знаю, тебя дозорные послали, чудище убить, ты не первый, кто сюда приходит, 
  say да только никто чудище найти не может.
  wait 1s
  say Давай я помогу тебе найти чудище, а ты мне за это раздобудешь приворотное зелье! 
  say И тогда я опою им Ванятку и будем с ним жить долго и счастливо.
  рад
  wait 1s
  say Я вижу ты согласен, ну так запоминай - все жители нашей деревни знают, 
  say что чудище живет в ильмень-озере, однако не все ведают, 
  say что ночью оно выходит на берег к востоку отсюда.
  wait 1s
  say Дождись ночи и приходи за его шкурой!
  wait 1s
  say Я тебе помогла, помни - ты обещал мне приворотного зелья!
end
if %actor.sex%==2
  say Ты не встречала Ванятку-молодца? Он обещал, что как только придет из похода, мы поженимся.
  груст
  say А его все нет и нет.
  ждать
  say Может он разлюбил меня?
  плак
  wait 1s
  say Слушай, я ведь знаю, тебя дозорные послали, чудище убить, ты не первая, кто сюда приходит, 
  say да только никто чудище найти не может.
  wait 1s
  say Давай я помогу тебе найти чудище, а ты мне за это раздобудешь приворотное зелье! 
  say И тогда я опою им Ванятку и будем с ним жить долго и счастливо.
  рад
  wait 1s
  say Я вижу ты согласна, ну так запоминай - все жители нашей деревни знают, 
  say что чудище живет в ильмень-озере, однако не все ведают, 
  say что ночью оно выходит на берег к востоку отсюда.
  wait 1s
  say Дождись ночи и приходи за его шкурой!
  wait 1s
  say Я тебе помогла, помни - ты обещала мне приворотного зелья!
end
~
#35528
умер коркодел (м12)~
0 f 0
~
if (%random.1000% <= 250) && (%world.curobjs(35537)% < 1)
  mload obj 35537
end 
if (%random.1000% > 250) && (%world.curobjs(35538)% < 1)
  mload obj 35538
end
~
#35529
молимся (р18)~
2 c 1
молиться~
if %actor.religion%==1
  %echo% %actor.name% начал%actor.g% усиленно молиться.
  %send% %actor% Вы начали молиться и Господь услышал Вас и указал путь. 
  %send% %actor% Теперь Вы отчетливо видите, что по корням деревьев, 
  %send% %actor% торчащих из южной стенки оврага, можно подняться вверх.
  %door% %self.vnum% up room 35567
  %door% 35567 down room %self.vnum%
else
  %send% %actor% Давно ли Христу молишься!?
end
~
#35530
жертвуем (р18)~
2 c 1
жертвовать~
if %actor.religion%==0
  %echo% %actor.name% раскрошил%actor.g% краюху хлеба и подбросил%actor.g% крошки вверх, принося жертву Стрибогу.
  %send% %actor% Стрибог принял вашу жертву и теперь Вы отчетливо видите, что по корням деревьев, 
  %send% %actor% торчащих из южной стенки оврага, можно подняться вверх.
  %door% %self.vnum% up room 35567
  %door% 35567 down room %self.vnum%
else
  %send% %actor% Давно ли языческими обрядами балуешься?! 
end
~
#35531
вошли к волхву (м05)~
0 q 100
~
wait 1s
say Ну наконец-то хоть кто-то из смертных пренебрег этими бреднями о моей злой натуре 
say и пожаловал на старое капище! 
say Послушайте меня, я знаю, что обитатели здешних мест рассказали про меня кучу небылиц, 
say но я здесь не с целью творить зло! Нет! Я маюсь уже несколько десятилетий,
say потому, что монахи из Юрьева монастыря совершают молитвы, и они не дают мне обрести покой. 
say Я чувствую великую силу этих молитв, но не понимаю их. 
wait 1s
say Принесите мне разъяснения их молитв, я пойму суть их учения и упокоюсь с миром навечно!
~
#35532
пытаются бить волхва(м05)~
0 k 10
~
хох
say Дурни! Я же бессмертный! Как вы хотите меня убить?
~
#35533
бросили амулет у волхва (р67)~
2 h 100
~
if %object.vnum%==35546
  %purge% %object.id%
  %echo% Амулет рассыпался в прах, едва коснувшись земли, но теперь это место всегда будет приносить вам удачу.
  wait 1s
  %echo% Дух закричал: "Откуда вы узнали про амулет?! Это не должно было случиться!"
  %purge% %world.mob(35505)%
  wait 1s
  %load% mob 35506
end
~
#35534
даем посох монаху (м07)~
0 j 100
~
if %object.vnum%==35532
  wait 1s
  %purge% %object.id%
  %echo% Монах с изумлением поднял на Вас глаза! 
  say Вы изгнали духа волхва!? 
  say Я не буду расспрашивать как вам это удалось, чтобы не вводить себя вогрех.
  say Главное - дело сделано!
  wait 1s
  say Я могу предстать перед господом с чистой совестью. Мое послушание выполнено!
  say Вы заслужили награды, но я монах и давал обет нестяжательства. 
  wait 1s
  say У меня нет ничего ценного, но вот эту вещь я привез с горы Афон, когда был там паломником. 
  say Теперь она ваша по праву.
  if !%exist.obj(35542)%
    %load% obj 35542
    дать лампада %actor.name%
  else
    wait 1s
    дум
    say Слушай, не могу найти... Извини... Свентовит его знает куда подевалось... 
  end
  say А сейчас мне пора уходить.
  %purge% %self.id%
else
  взд
  say Я не принимаю пожертвований, друг мой...
  брос все
end
~
#35535
Умер дух волхва (м06)~
0 f 0
~
if !%exist.obj(35532)%
  %load% obj 35532
end
~
#35536
даем евангелие духу волхва(м05)~
0 j 100
~
wait 1s
if %object.vnum%==35531
  %purge% %object.id%
  if %actor.sex%==1
    say О-ха-ха, да ты оказался еще глупее, чем я о тебе подумал при первой встрече!
  end
  if %actor.sex%==2
    say О-ха-ха, да ты оказалась еще глупее, чем я о тебе подумал при первой встрече!
  end
  wait 1s
  say Теперь мне известна суть учения монахов Юрьева монастыря! И значит я знаю как им возразить! 
  say Теперь их молитвы меня уже не остановят в моем извечном служении Перуну!
  if %actor.sex%==1
    say Но ты, глупец, заслуживаешь награды! Да-да! Ведь ты совершил благое дело во имя Рода и всей Прави!
  end
  if %actor.sex%==2
    say Но ты, глупая, заслуживаешь награды! Да-да! Ведь ты совершила благое дело во имя Рода и всей Прави!
  end
  say Вот держи - это теперь твое!
  if !%exist.obj(35543)%
    %load% obj 35543
    дать огонь %actor.name%
  else
    wait 1s
    дум
    say Я передумал! Не будет тебе награды! Раздал уже все! 
  end
  detach 35533 %world.room(35567)%
else
  взд
  say Я не принимаю пожертвований, друг мой...
  брос все
end
~
#35537
даем вещи командиру (м01)~
0 j 100
~
wait 1s
if ((%object.vnum%<>35537) && (%object.vnum%<>35538) && (%object.vnum%<>35510))
  say Я на службе. Взяток не беру...
  брос все
  halt
end
if (%object.vnum%==35537)
  %purge% %object.id%
  say Ай да молодец! Вот услужил%actor.g% так услужил%actor.g%! И как же ты его отыскал%actor.g%! 
  say Ведь сколько искали - все никак не могли!
  say Ну да ладно - вот тебе награда
  if !%exist.obj(35545)%
    %load% obj 35545
    Дать мешок .%actor.name%
  else
    wait 1s
    say Ты не поверишь! Уперли награду твою!
  end
  if (%random.1000% <= 100) && (%world.curobjs(35513)%<5)
    say А вот тебе особая награда от князя - он мне вручал, 
    say думал что я зверя споймаю, да видать судьба тебе им владеть!
    %load% obj 35513
    Дать шестопер .%actor.name%
  end
end
if (%object.vnum%==35538)
  say Да ты что - издеваться над князем надумал%actor.g%? 
  say Как он эти лохмотья гостям покажет! 
  say За службу спасибо, молодец, что зверя лютого извел%actor.g%, 
  say но награду я себе оставлю - а ты впредь иди и учись шкуры снимать! Растяпа!
end
if (%object.vnum%==35510)
  %echo% Командир аж крякнул от изумления.
  say Да-аа хорош клинок, ну что ж - давай меняться! 
  say Мне есть что предложить за него! Вещь тоже очень стоящая!
  %purge% %object.id%
  if (%world.curobjs(35511)%<5)
    %load% obj 35511
    Дать все .%actor.name%
  else
    упрек
    say Представляешь, все раздал уже.. Тебе не досталось!
  end
end
~
#35538
входим к хозяйке(м10)~
0 q 100
~
wait 1s
say Вот незадача-то, разбила я горшок-то, а мужики мои голодные небось, ругают меня уж поди на чем свет.
груст
~
#35539
даем горшочек хозяйке(m10)~
0 j 100
~
wait 1s
if %object.vnum%==35548
  %purge% %object.id%
  say Ой какая прелесть!
  say Выручил%actor.g% ты меня разок, выручи и другой.
  say Я тут испекла пирог с рыбой - возьми его и отнеси мужу или сыну. 
  say Они у меня весь день промышляют. Поди голодные! 
  say Кого первого встретишь - тому пирог и отдай, они уж там сами поделят его.
  if !%exist.obj(35529)%
    %load% obj 35529
    Дать пирог .%actor.name%
  else
    красн
    say ...кончились пироги
  end
  detach 35538 %self.id%
  detach 35539 %self.id%
else
  брос все
end
~
#35540
обыскиваем телегу (о30)~
1 c 4
обыскать~
if !%arg.contains(телегу)% 
  %send% %actor% Что Вы хотите обыскать?
else
  if %exist.mob(35515)%==1
    %send% %actor% Как вы хотите это сделать при живом хозяине? 
  else
    %send% %actor% Вы перерыли всю телегу вверх дном, и даже отломали у нее колеса
    if (%random.1000% < 200) && (%world.curobjs(35548)% < 1)
      %send% %actor% Вы нашли замечательный горшочек. Когда-то в нем держали мед!
      %load% obj 35548
    else
      %send% %actor% Вы не нашли ничего стоящего, кроме пары цветастых тряпок, видимо купец успел все продать в Русе.
    end
    if !%exist.obj(35555)%
      %load% obj 35555
    end
    %purge% %self.id%
  end
end
~
#35541
даем Боряте вещи (м18)~
0 j 100
~
wait 1s
if (%object.vnum%!=35529)&&(%object.vnum%!=35545)
  say Занятная вещица.
  say Только мне она к чему?
  брос все
  halt
end
if %object.vnum%==35529
  %purge% %object.id%
  say О! Наконец-то жена вспомнила про голодного мужа.
  say А то сижу тут не жрамши, путников не много, скучаю, вот со скуки лягушек ловлю и ноги им отрываю.
  Хох
  wait 1
  say Все потеха, хоть какая-то.
  wait 1
  say Извольте полюбопытствовать.
  if !%exist.obj(35534)%
    %load% obj 35534
    дать лапки .%actor.name%
  else
    say Извините, недавно отдал последние.
  end
end
if %object.vnum%==35545
  %purge% %object.id%
  say Хм, да за такие деньжищи, хоть всю лодку забирайте!
  say Что, вся не нужна? ну держите тогда уключину
  say Я и с одним веслом Ловать переплыву.
  if !%exist.obj(35550)%
    %load% obj 35550
    дать уключина .%actor.name%
  else 
    say Опа! А ведь осталась у меня только одна - совсем без весел не поплыву, извините.
  end
end
~
#35542
даем вещи молодому охотнику (м09)~
0 j 100
~
wait 1s
if (%object.vnum%!=35529)&&(%object.vnum%!=35522)
  %echo% Молодой охотник задумчиво покрутил %object.iname% в руках.
  пож
  брос все
  halt
end
if (%object.vnum%==35529)
  %purge% %object.id%
  say О! Так вы только что от матушки моей?!
  рад
  say Пирог то еще горячий! Благодарствую, надо будет кусок и тятеньке отнести.
  wait 1
  say День сегодня не очень удачный, да и снаряженьице у меня не-ахти какое.
  взд
  wait 1
  say Зато я сегодня перед ручьем, там где заросли калины и полно рябины, видел странного незнакомца. 
  say Вот у него лук - вот это да! Просто загляденье! Эх, кабы мне такой! Уж я бы настрелял дичи!
  wait 1
  say А за пирог матушкин еще раз благодарствую.
  say Если бы был какой трофей - я бы с вами поделился с радостью, да только сегодня вот мне что досталось.
  ухм
  if !%exist.obj(35549)%
    %load% obj 35549
    Дать перо .%actor.name%
    say Вот может на шляпу приколете, али подругу пощекочете.
    Хих
  else
    say ...да и этого уже не осталось
  end 
end
if (%object.vnum%==35522)
  %purge% %object.id%
  say О! Вот точно такой я видел у того незнакомца, что возле переправы! Ну теперь я настреляю дичи!
  say А Вам за доброту Вашу пусть Белобог ниспошлет всяких благ!
  if %actor.level%>5
    %send% %actor%  Вы почувствовали себя опытнее.
    %actor.exp(+20000)%
  end
end  
~
#35543
входим к знахарю (м24)~
0 q 40
~
wait 1s
say Вот невезенье какое! 
say Последнюю лягушку на моих глазах цапля сожрала, а где глухаря искать вообще ума не приложу!
эм осмотрел Вас отсутствующим взглядом и принялся опять что-то бормотать под нос.
~
#35544
даем вещи знахарю (м24)~
0 j 100
~
wait 1s
if (%object.vnum%!=35534)&&(%object.vnum%!=35549)
  эм задумчиво осмотрел %object.iname%
  эм попробовал %object.iname% на зуб
  эм сплюнул
  брос все
  halt
end
if (%object.vnum%==35534)
  %purge% %object.id%
  say Ах, какой редкий ингридиент! Да я за него отдам вам свое лучшее зелье!
  sae Вот держите, а если найдете еще лапки - приносите мне немедленно.
  %load% obj 35535
  Дать зелье .%actor.name%
end
if (%object.vnum%==35549)
  %purge% %object.id%
  say Ради Сварога, скажите где вы нашли это чудо? 
  say Да я вам за это перо вот что дам! 
  %load% obj 35536
  Дать зелье .%actor.name%
  рад 
  say Если еще будут перья - обязательно приносите!
end
~
#35545
даем зелье девушке (м04)~
0 j 100
~
wait 1s
if %object.vnum%==35535
  %purge% %object.id%
  рад
  if (%actor.sex%==1)
    say Вот здорово! Ты - первый мужчина, который сдержал свое обещание!
  end 
  if (%actor.sex%==2)
    say Вот здорово! Ты - первая моя подруга, которая не обманула!
  end
  красн
  say Теперь мы с Ваняткой вовек не разлучимся и никуда он, дурачок, от меня не сбежит больше.
  апл
  say А в награду я тебе подарю вот что.
  %load% obj 35551
  Дать колечко .%actor.name%
  say Это мне досталось от бывшего моего ухажера.
  say Только я его разлюбила и подарков мне от него больше не надобно!
  хмур
  say А если кольцо тебе не впору придется - можешь ему и отдать.
  say Пусть подавится своей безделушкой.
  say Мне моя Ванятка подарит золотое.
  рад
  detach 35527 %self.id%
  detach 35528 %self.id%
else
  say Я девушка скромная, подарков не беру.
  брос все
end
~
#35546
даем колечко скороходу (м11)~
0 j 100
~
wait 1s
if %object.vnum%==35551
  %purge% %object.id%
  хмур
  say Ах, она гадюка! Мало того, что эта вертихвостка ушла от меня к Ваньке по кличке "рябое яйцо", 
  say так она еще и подарком моим погребовала!
  say Уж я ей задам, дай только выйти с этого болота проклятого!
  wait 2s
  say Послушай, мил человек, я тут на службе - надо в Русу грамоту отнести, да ревность глаза застилает.
  say Надо мне девку проучить!
  say Будь добр%actor.g%, как встретишь Сидора "косолапого" из Русы, отдай ему грамотку, 
  say только сам%actor.g% в нее не заглядывай - а то ослепнешь! Да и князь на кол посадит!
  say А я побегу в деревню, поговорю с паскудиной!
  if !%exist.obj(35553)%
    %load% obj 35553
    Дать грамота .%actor.name%
  else 
    say Нет, все-таки сам грамоту отнесу.
  end
  %echo% Скороход так вчистил по болоту - что только его и видели.
  %purge% %self.id%
else
  say Я на службе - это не понесу.
  брос все
end
~
#35547
даем уключину старому рыбаку (м03)~
0 j 100
~
wait 1s
if %object.vnum%==35550
  %purge% %object.id%
  say Вот спасибо, дружок, выручил%actor.g% старика!
  say А где ты ее разыскал%actor.g%? Поди детишки закатили куда?
  say Вот шалуны - говорил же им, чтоб к лодке не подходили!
  wait 1s
  say За помощь твою - держи вот это кольцо.
  say Оно конечно не золотое, на базаре за него много не выручишь
  say Но мы рыбаки - такие кольца очень ценим - в путину без них никуда
  say Силы такие кольца придают!
  %load% obj 35552
  Дать кольцо .%actor.name%
  detach 35526 %self.id%
  detach 35547 %self.id%
else
  say На это рыба не клюнет.
  брос все
end
~
#35548
даем шкурки опытному охоту (м08)~
0 j 100
~
wait 1s
if (%object.vnum%!=35537)&&(%object.vnum%!=35538)
  эм задумчиво осмотрел %object.iname%
  эм сплюнул
  брос все
  halt
else
  say Ого! Да ты смотрю самого лютого ящера изловил! 
  say Ну чтож - я не токмо охотник, а и мастерить со шкур могу.
  say Давай-ка в награду за избавление деревни от коркодела я попробую что-нибудь сделать.
  эм достал из мешка нож, иглу, и сухожильные нити и принялся что-то делать со шкурой.
end
if (%object.vnum%==35537)
  %purge% %object.id%
  if (%random.1000%<=100)
    say Извини, не вышло!
    halt
  end
  switch %random.3%
    case 1
      %load% obj 35539
      Дать все .%actor.name%
    break
    case 2
      %load% obj 35540
      Дать все .%actor.name%
    break
    case 3
      %load% obj 35541
      Дать все .%actor.name%
    break
  done
end
if (%object.vnum%==35538)
  %purge% %object.id%
  if (%random.1000%<=250)
    say Извини, не вышло!
    halt
  end
  switch %random.3%
    case 1
      %load% obj 35539
      Дать все .%actor.name%
    break
    case 2
      %load% obj 35540
      Дать все .%actor.name%
    break
    case 3
      %load% obj 35541
      Дать все .%actor.name%
    break
  done
end  
~
#35549
даем доспех охраннику (м17)~
0 j 100
~
wait 1s
if %object.vnum%==35539
  %purge% %object.id%
  эм зацокал языком разглядывая доспех
  say Какой хороший доспех! Совсем не рвется!
  say Так и быть! Я буду с тобой меняться!
  say Держи мой любимый кынжал!
  if (%world.curobjs(35554)%<1)
    %load% obj 35554
    Дать кинжал .%actor.name%
  else
    say Ан нет, не дам.
    say Потерял!
  end
else
  эм хмуро посмотрел на Вас
  брос все
end
~
#35550
входим к урюпу (м19)~
0 r 100
~
wait 1s 
say Господи, чего ж я маленьким не сдох! Когда ж это все кончится...
say Чего вам? На тот берег Полисти?
say Тогда десять кун давайте - бесплатно не повезу!
~
#35551
даем куны урюпу~
0 m 1
~
wait 1s
if %amount%<10
  say Ну вы что, считать до десяти не умеете?
  halt
end
if %amount%>10 
  кашл
  say Сказал же - десять кун.
  say Мне лишнего не надо.
  кашл
  eval change %amount%-10
  дать %change% кун .%actor.name%
end
wait 1s
%echo% Урюп всех усадил в свою лодку и принялся грести.
wait 1s
%echo% Через десять минут вы оказались на другом берегу Полисти.
mat 35520 mecho Кто-то выбрался на берег из лодки.
%teleport% all 35520 horse
end
~
#35552
даем зелье урюпу (м18)~
0 j 100
~
wait 1s
if %object.vnum%==35536
  %purge% %object.id%
  say Вылечусь говорите? А не помру?
  say Смотрите если помру - то кто вас повезет на другой берег?
  эм залпом осушил зелье
  эм порозовел, покрылся испариной и издал непонятный звук
  пукн
  wait 2s
  say А ведь действительно отпустил кашель! Вот спасибо! Вот угодили!
  if %actor.level%<15
    %load% obj 35516
    say На вот держи подгон босяцкий, я обычно беру его с собой на всякий случай.
    say Но тут берег тихий, никогда никто ко мне не приставал!
    say А тебе мож пригодится.
    Дать рогат .%actor.name%
  end
  if (%random.1000% <= 200) && (%world.curobjs(242)% < 60)
    %load% obj 242
    г Держи вот еще, мало ли когда пригодится.
    дать рун .%actor.name%
  elseif %random.1000% <= 500
    %load% obj 549
    дать книга .%actor.name%
  end
else
  кашл
  эм замахал руками
  брос все
  кашл
end
~
#35553
зовем урюпа(r20)~
2 c 1
позвать~
if !%arg.contains(лодочника)%
  %send% %actor% а кого Вы звать собираетесь? Все ваши уже собрались.
else
  %echoaround% %actor.name% истошно завопил%actor.g%
  wait 1s
  %echo% Урюп услышал на том берегу ваши истошные крики сел в лодку и поплыл к вам.
  %echo% Выйдя на берег Урюп сказал: "за десять кун всех отвезу"
  wait 2s
  if %actor.gold%<10
    %echo% Урюп возмущенно сказал: "Эй, зачем звали если денег нету?!"
  else
    eval buf %actor.gold(-10)%
    %echo% Урюп пересчитал деньги и бодро взялся за весла.
    wait 2s
    %echo% Не прошло и десяти минут, как вы оказались на другом берегу Полисти.
    %teleport% all 35532
    wat 35532 wecho Кто-то выбрался на берег из лодки.
  end
end
~
#35554
Даем грамоту Сидору~
0 j 100
~
wait 1s
if %object.vnum%==35553
  %purge% %object.id%
  г Во! Она то мне и нужна!
  г А где тот парень, который ее должен был принести?
  хмур
  wait 1s
  г Ну да черт с ним, главное, что грамота у меня.
  wait 1s
  г Держи за службу!
  if (%random.1000% <= 300) && (%world.curobjs(245)% < 50) 
    %load% obj 245
    дать руна %actor.name%
  elseif %random.1000% <= 500
    %load% obj 522
    дать книга %actor.name%
  else
    дать 1000 кун %actor.name%
  end
  wait 1s
  %echo% Кивнув головой на прощание, Сидор деловито пошел в глубь города.
  %purge% %self.id%
else
  г На черта оно мне нужно?!
  брос все
end
~
#35555
Смерть знахаря~
0 f 100
~
if %random.1000% <= 100
  %load% obj 1726
end
~
#35599
репоп зоны~
2 f 100
~
%door% 35518 up purge
%door% 35567 down purge
~
$~
