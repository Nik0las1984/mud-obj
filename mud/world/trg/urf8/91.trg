#9100
волк встречает~
0 r 100
~
*этот триггер  нельзя вызывать из другого,  так  как не будет определена  
*переменная %actor% - и получится полная ерунда вплоть до креша
wait 1 s
*а эти три строчки зачем ?
*eval room %self.realroom%
*eval actors %room.pc%
*eval actor %actors.car%    
* проверка на левел то зачем такая :) на 31+ мтелепорт не работает
* лучше в другом триггере (9118) все эти проверки повставлять
*if (%actor.level% < 31)
if !%actor.quested(9100)%
  mecho %actor% Волк улыбнулся хитро.
  say Здравствуй, здравствуй, %actor.name%!
  say Заходи, дедуля уже ждет тебя.
  отпереть дверка
  открыть дверка
  msend %actor% Легким пинком волк помог вам войти внутрь.
  mechoaround %actor% Легким пинком волк помог %actor.dname% войти внутрь.
  *мало ли юмористов есть еще толпой у волка незачем собираться, потом
  *выхода не будет и будут взывать как выбраться...
  *имх лучше телепортом их отправлять отсюда в школу 
  %actor.setquest(9100)%
  mteleport %actor% 9101
  *mforce %actor% север 
  *set %actor.setquest(9100)% 1  - по моему тут сеты не нужны
  закрыть дверка
  запереть дверка   
else   
  %actor.wait(2)%
  msend %actor% Волк смерил вас оценивающим взглядом и нахмурился.
  mechoaround %actor% Волк смерил %actor.vname% оценивающим взглядом и нахмурился.
  say Помню, %actor.name%, заходил%actor.g% ты уже к нам как-то.
  say Что ж, по второму разу тебя обучать дедуля будет? Нет, извини, некогда ему...
  msend %actor% Волк пристально взглянул на вас, у вас закружилась голова, и на миг вы потеряли сознание.
  mechoaround %actor% Под пристальным взглядом волка ~~%actor.name% растворил%actor.u% в воздухе.
  mteleport %actor.name% %actor.loadroom%
  mat %actor.loadroom% mechoaround %actor% ~~%actor.name% появил%actor.u% тут с виноватым видом. Да, второй раз в школу уже не пускают...  
end
~
#9101
котенок играет~
0 b 100
~
exec 9105 %self.id%
if (%self.vnum% == 9101)
  эм мурлыкнул и потерся об ваши ноги, заглядывая вам в глаза
else
  лиз
end
~
#9102
котенка погладили~
0 c 1
погладить~
if (%self.vnum% == 9101)
  msend %actor% Вы погладили черного котенка.
  mechoaround %actor% %actor.iname% погладил черного котенка.
  эм выгнул спину и что-то мурлыкнул, зажмурившись от удовольствия
else
  msend %actor% Вы погладили белого котенка.
  mechoaround %actor% %actor.iname% погладил белого котенка.
  эм выгнул спину и поскреб коготками по полу
end
~
#9103
котенку дают что-то (надо рыбку)~
0 j 100
~
wait 1
if (%object.vnum% == 9161)
  if (%self.vnum% == 9101)
    mecho Черный котенок съел рыбку.
  else
    mecho Белый котенок съел рыбку.
  end
  mpurge %object%
  сесть
  эм принялся умываться
else
  эм возмущенно фыркнул
  брос %object.iname%
end
~
#9104
котенок вошел в комнату с блюдцем~
0 t 100
~
run 9105 %self.id%
~
#9105
котенок проверяет блюдце~
0 z 100
~
if (%self.realroom% == 9107)
  взять рыбка блюдце
  if (%self.haveobj(рыбка)%)
    if (%self.vnum% == 9101)
      mecho Черный котенок съел рыбку.
    else
      mecho Белый котенок съел рыбку.
    end
    calcuid fishy 9161 obj
    mpurge %fishy%
    сесть
    эм принялся умываться
  else
    if (%self.vnum% == 9101)
      эм обнюхал блюдце, разочарованно мяукнул и с надеждой взглянул на вас
    else
      эм обнюхал блюдце и разочарованно мяукнул
    end
  end
end
~
#9106
лисица готовит~
0 b 50
~
if (%random.2% == 1)
  эм приподняла крышку кастрюли и помешала содержимое
else
  эм глянула в книгу, наморщила лоб и уставилась перед собой, что-то прикидывая
end
~
#9107
лисица грозит котенку~
0 g 100
~
wait 1
set mobname %actor.name%
if (%mobname.contains(котенок)%)
  смотреть котенок
  г Смотри, к продуктам не лезь!
  эм погрозила котенку половником
  mforce %actor% ужас
end
~
#9108
лисица угощает~
0 d 1
угости спасибо~
wait 5
if (%speech% == угости)
  mecho Лисица широко улыбнулась и взяла пирожок из печи.
  mload obj 9166
  дать пирожок %actor.iname%
  г Кушай на здоровье!
  улы %actor.iname%
elseif (%speech% == спасибо)
  г Пожалуйста!
  г Хороший аппетит - лучшая похвала повару.
  улы
end
~
#9109
дедуля встречает~
0 r 100
~
wait 1
шляпа %actor.name%
if (%direction% == west)
  * скорее всего в первый раз
  msend %actor% - Рад тебя видеть в добром здравии, %actor.name%! - произнес дедуля.
  msend %actor% - Почитал%actor.g% досочки-то? Узнал%actor.g% что-нибудь новое?
  msend %actor% - Узнал%actor.g%, по глазам вижу!
  msend %actor% Дедуля улыбнулся.
  msend %actor% - Ну что ж... Осталось самое главное - бою научиться.
  * пару слов о профессии
  wait 1s 
  exec 9110 %self.id%    
  wait 2s
  msend %actor% - Проходи на север, там тебя научат всему необходимому для боя.
  msend %actor% - Здесь написано, как с дверями разобраться, - кивнул на доску дедуля.
  msend %actor% - А в комнатке на севере подробнее рассказано, как и чему учиться.
  msend %actor% - Как выучишься, спускайся вниз, там потренируешься сражаться.
  msend %actor% Дедуля откинулся на спинку кресла.
elseif (%direction% == north)
  * вернулся от учителей
  msend %actor% - Ну что, научился? Вот и ладушки, - произнес дедуля.
  msend %actor% - Спускайся вниз, посражаешься.
elseif (%direction% == down)
  * вернулся из боев
  msend %actor% - Ну что ж, если ты уже разобрался и со сражениями... - произнес дедуля.
  msend %actor% - Пожалуй, ты теперь готов к приключениям.
  msend %actor% - Коли готов ты покинуть школу, скажи 'домой'.
  msend %actor% - Я верну тебя туда, откуда ты пришел (команда 'г домой', если ты еще не забыл).
end
~
#9110
дедуля объясняет профессию~
0 z 100
~
if (%actor.class% == 0)
  msend %actor% - Силушкой ты похвастаться не можешь, зато лечить будешь.
  msend %actor% - И себя, и товарищей своих ратных.
elseif (%actor.class% == 1)
  msend %actor% - Силушкой ты похвастаться не можешь, зато заклинания такие узнаешь...
  msend %actor% - В страхе враги бежать будут... если успеют, конечно.
  msend %actor% Дедуля подмигнул вам.
elseif (%actor.class% == 2)
  msend %actor% - Магия тебе недоступна, ну да это тебе и не нужно.
  msend %actor% - Обчистишь карманы у зазевавшегося, вот и прибыль, так ведь?
  msend %actor% - Ну а уж коли до схватки дело дойдет, ловкостью возьмешь.
  msend %actor% - Повалишь врага на землю ловкой подножкой, да и заколешь в сердце.
elseif (%actor.class% == 3)
  msend %actor% - Магия тебе недоступна, ну да зачем она тебе?
  msend %actor% - С такими кулачищами пудовыми, тебе и оружие-то не нужно.
  msend %actor% - Богатырский твой удар не каждый выдержит!
elseif (%actor.class% == 4)
  msend %actor% - Магия тебе недоступна, ну да это тебе и не нужно.
  msend %actor% - Тьма густая да ножик острый, вот и смерть врагу.
  msend %actor% - Повалишь его на землю ловкой подножкой, да и заколешь в сердце.
elseif (%actor.class% == 5)
  msend %actor% - Магия тебе недоступна, ну да это тебе и не нужно.
  msend %actor% - Было бы оружие по руке, так ведь?
  msend %actor% - От многих ворогов одновременно отбиваться сможешь.
  msend %actor% - Ну а други твои об остальном позаботятся.
  msend %actor% - И врагам раны нанесут смертельные, и твои излечат вовремя.
elseif (%actor.class% == 6)
  msend %actor% - Силушкой ты похвастаться не можешь, зато заклинания такие узнаешь...
  msend %actor% - Подчинять разум научишься, будут твари неразумные за тебя сражаться.
elseif (%actor.class% == 7)
  msend %actor% - Силушкой ты похвастаться не можешь, зато заклинания такие узнаешь...
  msend %actor% - Обездвиживать врагов будешь, а другам защиту давать. 
elseif (%actor.class% == 8)
  msend %actor% - Силушкой ты похвастаться не можешь, зато заклинания такие узнаешь...
  msend %actor% - Из мертвых поднимать станешь страшные создания.
  msend %actor% - Будут они за тебя сражаться, а ты им магией черной помогать станешь.
elseif (%actor.class% == 9)
  msend %actor% - Не самый ты сильный воин, и маг не очень умелый.
  msend %actor% - Зато оружием владеешь мастерски, точные твои удары поражать будут всех.
  msend %actor% - А заклинаний хоть и немного узнаешь, но зато нужные тебе для боя.
elseif (%actor.class% == 10)
  msend %actor% - Магия тебе недоступна, ну да это тебе и не нужно.
  msend %actor% - Был бы глаз верный, да силушка, да лук по руке!
elseif (%actor.class% == 11)
  msend %actor% - Магия тебе недоступна, ну да это тебе и не нужно.
  msend %actor% - Выкуешь себе оружие, заточишь его, доспех укрепишь. 
  msend %actor% - Была бы силушка в руках, и тогда держитесь, вороги!
elseif (%actor.class% == 12)
  msend %actor% - Силушкой ты похвастаться не можешь, хотя и ею тебя не обидели.
  msend %actor% - Но зачем в бой лезть, когда можно нанять кого-нибудь?
  msend %actor% - Пусть он и выполнит грязную работу, а твое дело - врага попридержать.
  msend %actor% - Да раны нанятому воину перевязать, чтоб не умер он раньше времени.
elseif (%actor.class% == 13)
  msend %actor% - Силушкой ты похвастаться не можешь, и заклинаний не знаешь.
  msend %actor% - Зато с рунами необходимыми - нет тебе равных.
  msend %actor% - Будут руны - будут и заклинания, как защитные, так и атакующие.
end
~
#9111
дедуля возвращает~
0 d 1
домой~
wait 3
mecho - Ой, чуть не забыл! - воскликнул дедуля, подвинулся к столу и быстро начал писать.
mload obj 9167
дать записка %actor.iname%
mecho - Там тебе еще пара советов, пригодятся, почитаешь на досуге.
пока %actor.iname%
mecho Дедуля сделал несколько странных пассов.
msend %actor% У вас закружилась голова, и на миг вы потеряли сознание.
mechoaround %actor% ~~%actor.name% растворил%actor.u% в воздухе.
mteleport %actor.name% %actor.loadroom%
mechoaround %actor% ~~%actor.name% появил%actor.u%, полн%actor.w% новых впечатлений. Видимо, только что из школы.
~
#9112
тренировочный моб прекращает бой~
0 k 100
~
if (%actor.hitp% <=5)
  mecho - Случись такое не в школе, ты бы был%actor.g% уже мертв%actor.g%, - вдруг произнес чей-то голос.
  mecho - Соразмерять же надо свои силы...
  mecho Раздался легкий хлопок, и %self.name% рассыпал%self.u% прахом.  Вовремя, однако...
  mpurge %self%
end
~
#9113
запуск репопа червей~
2 e 100
~
if (%direction% == east)  
  calcuid rum 9115 room
  exec 9114 %rum.id%
elseif (%direction% == north)
  calcuid rum 9116 room
  exec 9114 %rum.id%
elseif (%direction% == west)
  calcuid rum 9117 room
  exec 9114 %rum.id%
elseif (%direction% == south)
  calcuid rum 9118 room
  exec 9114 %rum.id%
end
~
#9114
репоп червя~
2 z 100
~
if (%world.people(%self.vnum%)% < 1)
  wload mob 9107
end
~
#9115
запуск репопа мух~
2 e 100
~
if (%direction% == east) 
  calcuid rum 9120 room
  exec 9116 %rum.id%
elseif (%direction% == north)
  calcuid rum 9121 room
  exec 9116 %rum.id%
elseif (%direction% == west)
  calcuid rum 9122 room
  exec 9116 %rum.id%
elseif (%direction% == south)  
  calcuid rum 9123 room
  exec 9116 %rum.id%
end
~
#9116
репоп мухи~
2 z 100
~
if (%world.people(%self.vnum%)% < 1)
  wload mob 9108
end
~
#9117
кабан приветствует~
0 r 100
~
wait 1
if (%direction% == up)
  пож %actor.name%
end
~
#9118
войти  в школу~
2 c 1
войти~
*триггер должен стоять только на рентах, этот дополнительный тригг не позволит
*чарам зарентеным в первой родовой проходить из второй или третьей в школу
*и возвращаться в первую - нужно просто присваивать ему тот лоадрум, откуда
*он в школу вошел.
if !%arg.contains(школ)%
  return 0
  wait 1s
  halt
end
if %actor.level% > 9
  wsend %actor% Ты, %actor.name%, сам%actor.g% можешь других учить.
  wsend %actor% А ты в школу просишься. Не стыдно ?
  wait 1s
  halt
end
if !%actor.rentable%
  wsend %actor% Драчуны нам в школе не нужны, пойди утихомирься сначала.
  wait 1s
  halt
end
wait 1 
wsend %actor% Вы прошли в школу.
wechoaround %actor% В воздухе на мгновение появилась дверь.
wechoaround %actor% И %actor.name% исчез%actor.q% в ней. 
*присвоение чару той ренты откуда он зашел               
set start %self.vnum% 
eval buffer %actor.loadroom(%start%)%  
wteleport %actor% 9055
wait 2s
~
$~
