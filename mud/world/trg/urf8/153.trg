#15300
нюхающий ежик~
0 r 100
~
wait 7s
if %random.5% == 1
  mecho _Мохнатый ежик обнюхал вас.
end
~
#15301
лягушка квакает~
0 r 100
~
wait 7s
if %random.5% == 1
  mecho _Лягушка заквакала при виде вас.
end
~
#15302
хрю-хрю~
0 r 100
~
wait 7s
if %random.5% == 1
  mecho _Кабан разразился сытым похрюкиванием.
end
~
#15303
охотник ругается~
0 q 100
~
wait 1s
say А вы чего сюда приперлись?!
рыч
атак .%actor.name%
~
#15304
шишига хихикает~
0 q 50
~
say Ой, кто к нам пришел...
эм мерзко захихикала
~
#15305
леший бурчит~
0 r 100
~
wait 1
say Ходют тут всякие, отдыхать мешают...
mecho Леший сделал глубокий глоток браги
ик
~
#15306
рыбак квестит~
0 q 100
~
mecho  _Рыбак, кряхтя, приподнялся.
wait 1s
if %actor.sex% == 1
  say Здравствуй, путник, в недобрый час ты посетил нас.
  say Завелись в нашем озере девки-русалки.
  say Неводы наши путают, сети гребнями своими рвут
  say Никакой ловли из-за них не стало.
  wait 1s
  mecho  _Рыбак горько вздохнул.
  wait 2s
  say И не прогнать их ведь никак, косо на них посмотришь - так защекочут до смерти.
  say Молвят, правда, что полыни они жуть как боятся.
  say Но где ж я полыни найду, не растет она тут...
  say Может поможешь нам, добрый молодец, изгонишь русалок?
  wait 2s
  mecho  _Старый рыбак вздохнул и принялся за починку невода.
else
  say Здравствуй, путница, в недобрый час ты посетила нас.
  say Завелись в нашем озере девки-русалки.
  say Неводы рыбачьи путают, сети гребнями своими рвут,
  say Никакой ловли из-за них не стало.
  wait 1s
  mecho  _Рыбак горько вздохнул.
  wait 2s
  say И не прогнать их ведь никак, косо на них посмотришь - так защекочут до смерти.
  say Молвят, правда, что полыни они жуть как боятся.
  say Но где ж я полыни найду, не растет она тут...
  say Может поможешь нам, добра девица, изгонишь русалок?
  wait 2s
  mecho  _Старый рыбак вздохнул и принялся за починку невода.
end
~
#15307
оживление лесуньки~
1 c 4
завыть~
wait 1s
%echo% Вдруг полено жутко затрещало, запрыгало по земле и внезапно превратилось в девушку!
%echo% Лесунька быстро посмотрела по сторонам.
%echo% Лесунька сказала : 'Это вы избавили меня от этих дураков?'
%echo% Лесунька радостно запищала.
wait 1s
%echo% Лесунька сказала : 'Спасибо Вам! Я домой побегу, к лешему.'
%echo% Лесунька сказала : 'Заходите к нему, он обязательно Вас отблагодарит.'
%echo% Лесунька быстро побежала в сторону леса, и вскоре исчезла из виду.
%load% mob 15310
%teleport% %world.mob(15310)% 15318
attach 15312 %world.mob(15304)%
detach 15309 %world.mob(15304)%
detach 15310 %world.mob(15304)%
%purge% %self%
~
#15308
берем квест у рыбака~
0 d 1
хорошо помогу согласен изгоню~
set questor153 %actor.id%
global questor153
say Ох, благодарствую тебе, %actor.name%.
say Ты это, сходи к лешему, что живет неподалеку,
say Авось подскажет чего, али скажет где полыни добыть.
calcuid leshiy 15304 mob
detach 15305 %leshiy.id%
attach 15309 %leshiy.id%
attach 15314 %self.id%
detach 15306 %self.id%
remote questor153 %leshiy.id%
detach 15308 %self.id%
~
#15309
леший спрашивает~
0 r 100
~
wait 1s
say Ну чего тебе надобно?
хмур 
attach 15310 %self.id%
~
#15310
леший квестит~
0 d 1
полынь~
wait 1s
if %actor.id% == %questor153.id%
  if (%world.curobjs(15306)% > 0)
    г Сперва выполни мою просьбу, потом дам тебе полынь
    halt
  end
  say Начерта тебе полынь?
  say А-а-а...от русалок чтоль избавиться хочешь?
  чес
  say Ну мне-то оно все равно, они мне не родня.
  say Дам я тебе полынь, если поможешь мне.
  wait 3s
  say Поселились на юге люди злые, охотники.
  say Так-то охотников я не трогаю, кушать-то оно всем надо.
  say Но эти ироды, смеху ради зверье бьют,
  say Загонят, поизмываются, убьют да и бросят прям там же.
  плю
  say А недавно мою сердечную зазнобушку уволокли.
  say Лесуньку, мою красавицу зеленую.
  рыд
  wait 1s
  say Я б и сам их того... Да вот хозяйство бросить никак нельзя.
  say Да и вообще...
  mecho Леший попытался украдкой спрятать большую бутыль с брагой за спину.
  wait 2s
  say Верни мне ее, и я помогу тебе с русалками справиться.
  say Думаю, не далась моя лесунечка в жадные да похотливые руки,
  say А превратилась в бревно. В этом состояние ничем ее не пронять. 
  say Вот только волков она боится с детства,
  say Завой возле нее по волчьи - и превратится она обратно в лесуньку.
  attach 15315 15337
  run 15315 15337
end
~
#15311
русалки щекотят~
0 u 100
~
if (%actor.id% == %self.id%)
  halt
end
eval item %actor.eq(12)%
if %item.vnum% != 15308
  if %actor.sex% == 1
    say Ишь ты какой! Подружки, давайте проучим этого наглеца!
    msend %actor% _Русалки подбежали к вам и яростно начали Вас щекотать!
    msend %actor% _Их пальцы пробираются в самые чувствительные части Вашего тела. Это невыносимо!
    msend %actor% _НЕТ! НЕЕ-Е-Е-Е-Т!!! Вы уже не контролируете себя.
    msend %actor% _Вы готовы отдать все что угодно, лишь бы изгнать это противное чувство, затмевающее весь мир вокруг Вас.
    msend %actor% _Вас всего колотит, Вы чувствуете как разум покидает Вас...
    mechoaround %actor% _Русалки подбежали к %actor.vname%, и начали его яростно щекотать!
    mechoaround %actor% _Вы с ужасом видите как %actor.name% падает бессознания, и исчезает под водой.
    %actor.hitp(1)%
    calcuid rusroom 15361 room
    exec 15316 %rusroom.id%
  else
    say Ишь ты какая! Подружки, давайте проучим эту дуру!
    msend %actor% _Русалки подбежали к вам и яростно начали Вас щекотать!
    msend %actor% _Их пальцы пробираются в самые чувствительные части Вашего тела. Это невыносимо!
    msend %actor% _НЕТ! НЕЕ-Е-Е-Е-Т!!! Вы уже не контролируете себя.
    msend %actor% _Вы готовы отдать все что угодно, лишь бы изгнать это противное чувство, затмевающее весь мир вокруг Вас.
    msend %actor% _Вас всего колотит, Вы чувствуете как разум покидает Вас...
    mechoaround %actor% _Русалки подбежали к %actor.vname%, и начали ее яростно щекотать!
    mechoaround %actor% _Вы с ужасом видите как %actor.name% падает бессознания, и исчезает под водой.
    %actor.hitp(1)%
    calcuid rusroom 15361 room
    exec 15316 %rusroom.id%
  end
else 
  msend %actor% _Русалка попыталась подойти к Вам, но при виде плаща из полыни ее лицо исказилось в страшной муке!
  mechoaround %actor% _Русалка попыталась подойти к %actor.dname%, но при виде плаща из полыни ее лицо исказилось в страшной муке!
end
return 0
~
#15312
леший дает полынь~
0 q 100
~
wait 1s
if %actor.id% == %questor153.id%
  say Ох, благодарствую тебе, %actor.name%.
  say Помог%char.q% ты мне, спас ты мою зелененькую лесунечку.
  цел лесунька
  say Теперь и моя очередь тебе помогать.
  say Есть у меня несколько плащей старых из полыни,
  say Для отвода хворьбы различной делал, очень помогают.
  say Но, думаю, и для твоего дела тоже пойдут.
  wait 4s
  %echo% Леший шумно начал возиться в углу своей берлоги в поисках плаща.
  wait 4s 
  say Ага, нашел! Держи вот.
  say Укроешься им, и русалки близко к тебе подойти не смогут.
  %load% obj 15308
  дать плащ.полын %actor.name%
  wait 2s
  say Да, если еще понадобится - скажи, у меня их еще несколько где-то должно быть
  if %random.1000% <= 75
    %load% obj 1710
    г Держи и вот этот трактат. Раз ты мою лесуньку спас - я научу тебя как варить лучшую брагу на Руси!
    дать тракт %actor.name%
  end
  attach 15313 %self.id%
  detach 15312 %self.id%
end
~
#15313
леший дает еще полынь~
0 d 1
еще плащ~
if %actor.id% == %questor153.id%
  say Что? Еще один нужен?
  say Сейчас поищу...
  wait 4s
  if %world.curobjs(15308)% < 5
    %echo% После долгого поиска в своем хламе, леший достал на свет еще один плащ.
    wait 3s
    say Ну нужно, так нужно - держи вот.
    %load% obj 15308
    дать плащ.пол .%actor.name%
  else
    %echo% После долгого поиска в своем хламе, леший вернулся с пустыми руками.
    wait 3s
    say А нету, кончились плащи-то!
    взд
  end
end
~
#15314
рыбак награждает~
0 q 100
~
if %actor.id% == %questor153.id%
  if %exist.mob(15316)%
    say Ну как там с русалками?
  else
    say Ох, вы прогнали этих русалок?
    say Ну прямо гора с плеч, а то совсем ведь рыбачить не давали.
    say Подождите, у меня наверняка что-нибудь найдется для Вас.
    say Что только не попадает в наши сети...
    say Да и путники иногда дарят, в благодарность за ночлег и еду.
    wait 3s
    %echo% Рыбак начал что-то искать в сундуке.
    wait 3s
    say Вот, нашел! Вдруг Вам пригодится.
    eval rnd %random.100%
    if      %rnd% < 3
      mload obj 1722 // книга с водянистым переплетом
      дать все .%actor.name%
    elseif  (%rnd% < 18) && (%world.curobjs(208)% < 50)
      mload obj 208 // руна разрушения
      дать все .%actor.name%
    elseif %rnd% < 30
      mload obj 500 // книга кислы
      дать все .%actor.name%
    elseif (%rnd% < 40) && (%world.curobjs(15309)% < 30)
      mload obj 15309
      дать все .%actor.name%
    elseif (%rnd% < 50) && (%world.curobjs(15310)% < 10)
      mload obj 15310
      дать все .%actor.name%
    elseif (%rnd% < 60) && (%world.curobjs(15311)% < 10)
      mload obj 15311
      дать все .%actor.name%
    elseif (%rnd% < 70) && (%world.curobjs(15312)% < 10)
      mload obj 15312
      дать все .%actor.name%
    elseif (%rnd% < 80) && (%world.curobjs(15313)% < 10)
      mload obj 15313
      дать все .%actor.name%
    elseif (%rnd% < 90) && (%world.curobjs(15314)% < 10)
      mload obj 15314
      дать все .%actor.name%
    elseif (%rnd% <= 100)
      mload obj 501 // книга о создании сетей
      дать все .%actor.name%
    end
    detach 15314 %self.id%
  end
end
~
#15315
лоадится полено~
2 z 0
~
%load% obj 15306
~
#15316
русалки телепортят игрока~
2 z 100
~
%teleport% %actor% 15361
%send% %actor%  Вы с трудом пришли в себя на берегу озера...
%send% %actor%  Видимо когда Вы потеряли сознание, русалки отстали от Вас, и Ваше тело прибило к берегу озера.
%send% %actor%  Чудовищная слабость наполняет Вас...
%echoaround% %actor% Вы видите как тело %actor.rname% медленно прибивает к берегу.
%echoaround% %actor% Вы спешно вытащили его из озера, и попытались привести его в сознание.
%echoaround% %actor% Наконец %actor.iname% очнулся...
~
#15317
смерть медведя-берендея~
0 f 100
~
if %random.1000% <= 80
  %load% obj 1718
end
~
$~
