#12900
Приветствие лесовичка.~
0 q 100
~
wait 1
if ((%actor.class% == 13) && !(%actor.skill(сглазить)%))
  осм %actor.name%
  г О, волхв, сами боги привели тебя в наш лес!
  г Сможешь ли ты нам помочь?
end
if ((%actor.class% == 13) && (%actor.skill(сглазить)%))
  поклон %actor.name%
  г Спасибо тебе %actor.name%. Мы помним твое доброе дело.
  г Ты всегда желанный гость в нашем лесу.
end
~
#12901
Согласились помочь лесовичку.~
0 d 0
да конечно смогу помогу могу~
if ((%actor.class% == 13) && !(%actor.skill(сглазить)%))
  г Ужасное чудище завелось у нас в лесу. 
  г Найдет лесного зверя какого, так сразу убьет.
  г Ладно бы если било себе на еду, 
  г а то похоже вобще всех перевести решило.
  г Я бы сам с ним справился, да вот беда - хитрое оно. 
  г Как человека увидит, сразу прячется, да так что не отыскать.
  г Только волхвов почему-то не видит вобще.
  г Найди его и избавь лес от напасти!
  calcuid roomchud 12915 room
  attach 12911 %roomchud.id%
  detach 12900 %self.id%
  detach 12901 %self.id%
end
~
#12902
Убили чудище лесное~
0 f 0
~
mecho Страшный рев вырвался из пасти чудовища и его труп упал к Вашим ногам.
~
#12903
Вернулись с победой к лесовичку.~
0 q 100
~
if (%world.curmobs(12906)% > 0 )
  halt
end
wait 1
if ((%actor.class% == 13) && (!%actor.skill(сглазить)%))
  рад %actor.name%
  г Спасибо, %actor.name%!
  г Теперь я отблагодарю тебя.
  брос все
  mload obj 12901
  дать клубо %actor.name%
  брос все
  wait 5
  г Вот возьми клубочек. 
  г Бросишь его тут рядом у здоровой коряги.
  г Он поведет тебя по звериной тропке 
  г и приведет к великой волшебнице.
  г А уже она отблагодарит тебя!
  detach 12903 %self.id%
end
~
#12904
Бросили клубочек у коряги.~
1 h 100
~
if (%actor.realroom% != 12921)
  halt
end
if (%actor.class% != 13)
  halt
end
if (%actor.skill(сглазить)%)
  halt
end
wait 1
osend %actor.name% Ударившись о землю, клубочек подпрыгнул и покатился.
osend %actor.name% Вы побежали за ним по совершенно незаметным тропкам.
wait 1s
osend %actor.name% Вы побежали на север.
oechoaround %actor% Клубочек покатился, и %actor.name% куда-то побежал%actor.g% за ним.
wait 2
oteleport %actor.name% 12922
opurge %self%
~
#12905
Подошли к лешему.~
0 r 100
~
wait 1
эмоц посмотрел на Вас мутным взглядом
г Ой как мне не хорошо.
г Что было вчера - все как в дыму.
г Всех закружу у себя в лесу.
~
#12906
Зашли в последнюю клетку звериной тропки.~
2 e 100
~
wait 1
if (%world.curmobs(12901)% > 0 )
  wteleport %actor.name% 12925
end
~
#12907
Дали лешему выпивку.~
0 j 100
~
wait 1
if ((%object.type%==17 || %object.type%==23) && (%object.val2%==1 || %object.val2%==2 || %object.val2%==3 || %object.val2%==5 || %object.val2%==7 || %object.val2%==8))
  эмоц громко захохотал и моментально исчез среди деревьев
  mpurge %object%
  mpurge %self%
  halt
end
сплюн %actor.name%
брос все
~
#12908
Стоим перед избушкой на курьих ножках.~
2 ab 100
~
wecho Вдруг раздался стук да гром.
wecho Вы быстро спрятались за ближайшее дерево.
wait 1s
wecho Баба-яга прилетела сверху.
wecho Баба яга сказала 'избушка стань к лесу задом'
wecho Избушка повернулась.
wecho Баба-яга ушла на север.
~
#12909
Сказали - избушка стань к лесу задом~
2 d 1
избушка стань к лесу задом~
wecho Избушка повернулась.
wait 2
wecho Вы пошли на север.
wteleport %actor.name% 12931
~
#12910
Вошли к бабе-яге.~
0 r 100
~
г Слыхала, слыхала про тебя.
г Если хочешь - научу чего сама умею.
г А потом проваливай - работать не мешай.
%actor.setquest(12900)%
~
#12911
Нашли чудище лесное.~
2 e 100
~
wait 1
if ((%actor.class% == 13) && (!%actor.skill(сглазить)%))
  wload mob 12905
  wload mob 12906
  wforce олень удар чудище
  calcuid roomchud 12900 mob
  attach 12903 %roomchud.id%
  detach 12911 %self.id%
end
~
#12912
Резетим зону.~
2 f 100
~
calcuid roomchud 12915 room
detach 12911 %roomchud.id%
context 4000
set druidstory 0
worlds druidstory
~
$~
