#30300
убили руку скелета~
0 f 100
~
if (%world.curobjs(30312)% < 5) && (%random.100% < 21)
  mload obj 30312
end
~
#30301
зашли в клетку к ноге~
0 r 100
~
mecho _Нога скелета запрыгала от радости.
wait 1
msend %actor% _Нога скелета изогнулась и отвесила Вам смачный пинок.
mechoaround %actor% _Нога скелета изогнулась и отвесила %actor.dname% смачный пинок.
mdamage %actor% 150
mkill %actor%
~
#30302
зашли в клетку к домовичку~
0 r 100
~
mecho _Старичок-домовичок нахмурился и отложил метлу в сторону.
wait 1s
говор Не дам грабить мой погреб!
msend %actor% _Старичок-домовичок выхватил из сапога острую щепку и набросился на Вас.
mechoaround %actor% _Старичок-домовичок выхватил из сапога острую щепку и набросился на %actor.rname%.
mload obj 30316
mkill %actor%
~
#30303
зашли в клетку к банщику~
0 r 100
~
mecho _Старичок-банщик обрадовано потер ручки.
wait 1s
говор Ну щас я тебя попарю!
msend %actor% _Старичок-банщик набросился на Вас с березовым веником на перевес.
mechoaround %actor% _Старичок-банщик набросился на %actor.rname% с березовым веником на перевес.
mkill %actor%
~
#30304
зашли в клетку к амбарщику~
0 r 100
~
mecho  Старичок-амбарщик перестал наводить порядок и присмотрелся к Вам.
wait 1s
mecho _Старичок-амбарщик разгневано топнул ногой.
говор Повадились в чужие амбары лазить! Пора бы вас уже проучить!
mkill %actor%
~
#30305
зашли в клетку к овиннику~
0 r 100
~
mecho _Старичок-овинник подгреб под себя все сено.
wait 1s
говор Не дам сена! Мое оно!
mkill %actor%
~
#30306
зашли к толстому мельнику~
0 q 100
~
wait 1s
msend %actor% _Толстый мельник изучающе посмотрел на Вас.
mechoaround %actor% _Толстый мельник изучающе посмотрел на %actor.vname%.
wait 1s
tell %actor.name% Я не видел тебя раньше у нас.
tell %actor.name% Ты наверно прибыл%actor.g% сюда из дальних стран.
tell %actor.name% Быть может ты захочешь помочь мне избавиться
tell %actor.name% от обитающего в Волхове водяного. Сил нет терпеть
tell %actor.name% его непотребства, а недавно он хотел сломать мою мельницу.
tell %actor.name% Ты уж избавь нас от него, а я отблагодарю чем смогу.
wait 1s
msend %actor% _Толстый мельник подмигнул Вам и похлопал по кошелю.
mechoaround %actor% _Толстый мельник подмигнул %actor.dname% и похлопал по кошелю.
~
#30307
толстому мельнику дали голову водяного~
0 j 100
~
wait 1
if %object.vnum% == 30700 then
  wait 1
  mpurge %object%
  wait 1s
  mecho _Толстый мельник улыбнулся и спрятал голову за пазуху.
  wait 1s
  tell %actor.name% Продам на ярмарке!
  хих
  wait 1s
  tell %actor.name% Вот твоя оплата.
  mload obj 30318
  дат коше %actor.name%
  wait 1s
  mecho _Толстый мельник захихикал и выбежал из дома.
  wait 1
  mpurge %self%
end
~
#30308
зашли к рыжему волхву~
0 r 100
~
wait 1
mecho _Рыжий волхв обрадовался Вашему приходу.
wait 1s
говор Вас послал сам Перун, дабы я принес Вас ему жертву.
msend %actor% _Рыжий волхв выхватил ритуальный нож и с криком набросился на Вас.
mechoaround %actor% _Рыжий волхв выхватил ритуальный нож и с криком набросился на %actor.rname%.
if (!%self.haveobj(30316) && (%world.curobjs(30316) < 30))
  mload obj 30316
end
mkill %actor%
~
#30309
зашли к волхву Ящера~
0 r 100
~
mecho Волхв Ящера расхохотался и перед ним разверзлась земля.
wait 1s
говор Вот кто отправится на обед моему повелителю!
msend %actor% _Волхв Ящера указал посохом в Вашу сторону.
mechoaround %actor% _Волхв Ящера указал посохом в сторону %actor.rname%.
mkill %actor%
~
#30310
веселые ручки~
0 ab 50
~
if %random.5%==1 then
  mecho _Одна из костлявых рук показала Вам Кукиш.
else
  if %random.4%==1 then
    mecho _Одна из костлявых рук поманила Вас пальцем.
  else
    if %random.3%==1 then
      mecho _Одна из костлявых рук сложила пальцы в кулачок и погрозила Вам.
    else
      if %random.2%==1 then
        foreach i %self.pc%
          msend %i.name%  Одна из костлявых рук удлинилась, больно хватанув Вас за нос.
          mechoaround %i.name%  Одна из костлявых рук удлинилась, больно хватанув %i.rname% за нос.
          mdamage %i.name% 50
        done
      else
        if %world.curmobs(30348)% <2
          mecho  Похоже одному из скелетов удалось все-таки выбраться.
          mload mob 30348
        end
      end
    end
  end
end   
~
#30311
убили призрака мельника~
0 f 100
~
if (%world.curobjs(30305)% < 1)
  mload obj 30305
end
~
#30312
зашли к призраку мельника~
0 q 100
~
mecho _Призрак мельника прекратил ходить кругами.
wait 1s
говор Меня пришибло мельничным крылом!
говор И никто даже не удасужился похоронить мои останки.
плак
wait 1s
говор Ты наверно приш%actor.y% ограбить мое бренное тело.
говор Будь я жив, я зарубил бы тебя топором, 
говор а так придется просто разорвать на части.
mkill %actor%
~
#30313
закапываем останки мельника~
2 c 100
закопать~
if !(%arg.contains(останки)%)
  wsend %actor% Что это Вы хотите здесь закопать???
  return 0
  halt
end
if %actor.haveobj(30305)%
  wsend %actor% _Вы начали раскапывать могилу.
  wechoaround %actor% _%actor.iname% начал%actor.g% раскапывать могилу.
  wait 1s
  calcuid remains 30305 obj
  wpurge %remains%
  wsend %actor% Вы положили человеческие останки в могилу и закопали ее.
  wechoaround %actor% %actor.iname% положил%actor.g% человеческие останки в могилу и закопал%actor.g% ее.
  wait 1s
  wsend  %actor% _Тонкий луч света осветил Вас и могилу.
  wechoaround %actor% _Тонкий луч света осветил %actor.vname% и могилу.
  wsend       %actor% _За доброе дело Вы получили 5000 очков опыта...
  eval temp %actor.exp(+5000)%  
end
~
#30314
УмерВолхвЯщера~
0 f 100
~
mload obj 24124
~
#30315
ОбнаружениеКвестера6401~
2 e 100
~
if %actor.quested(640101)%
  if %exist.mob(30347)%
    *атачим триг лоад кафтана у волхва (посмертно)
    calcuid m2 30347 mob
    attach 30314 %m2.id% 
  end
end
~
#30316
ВошлиКСестрицам~
0 r 100
~
if !%actor.quested(303101)%
  wait 1s
  say Здравия тебе, путник, и роду твоему
  mecho - Сам бог послал тебя к нам в это тяжелое время...
  mecho - Может поможешь ты нам в нашей беде?
  вопрос
  attach 30317 %self.id%
end
~
#30317
ПомогуСестрам~
0 d 0
помогу да~
wait 1s
ул
mecho - Спасибо тебе путник за отзывчивую душу.
поклон 
wait 1s
mecho - Так вот наша беда какая:
mecho - Испокон веков изготавливает наша семья ткань атласную, луноликую...
mecho - Вот и мы с сестрицами всю жизнь веретенцами стучим, да волшбу творим.
mecho - Чтобы помогали нам в работе птицы малые, да удалые.
mecho - Как прилетают они, так сразу закрутятся веретенца, засверкают на солнце нити шелковые.
mecho - А однажды не прелетели они в нашу светлицу...
взд 
wait 1s
mecho - Мертвая тишина вместо шороха крыльев птичек-невеличек
mecho - Ведаю я, что беда с ними случилась.
mecho - Поймал их в сети свои птицелов-охотник.
mecho - Где искать его не ведаю я...
mecho - Только знаю, что искать его надо в здешних краях.
*лоад птицелова  
if !%exist.mob(30355)% 
  calcuid r1 30367 room
  attach 30319 %r1.id% 
  exec 30319 %r1.id% 
  detach 30319 %r1.id% 
end
*флаг
%actor.setquest(303101)% 
эм заплакала
wait 2s
mecho - Верни наших птичек, путник, век благодарна буду...
mecho - А принесешь доказательство сего, так за наградой не постою...
эм в печальном ожидании застыла у окна
detach 30316 %self.id%
detach 30317 %self.id%
~
#30318
УмерПтицелов~
0 f 100
~
mecho _Последние силы покинули птицелова.
mecho _Мертвое тело тяжело опустилось на землю.
mecho _Вдруг Вы заметили серебренный ключик, что со звоном выпал из кармана убитого.
mload obj 30319
~
#30319
ЛоадПтицелова~
2 z 100
~
wload mob 30355
~
#30320
РезетЗоны~
2 f 100
~
*квест 3031 вошли к сестрице
calcuid nMob 30317 mob
attach 30316 %nMob.id%
~
#30321
ДалиКлетьСестрице~
0 j 100
~
if %object.vnum% = 64066
  wait 1s
  if %actor.quested(303103)%
    *награда
    wait 1s 
    рад
    say Сдержа%actor.q% ты обещание свое! Так сдержу его и я!
    эм взмахнула красным платочком
    wait 1s 
    mecho _Тут же налетело птиц видимо-невидимо.
    wait 1s 
    mecho _Завертелось веретено-веретенушко, засияли нити шелковые в ткань сияющую превращаясь.
    wait 1s
    mecho - Вот награда твоя - ткань белоснежная!
    mecho - Нигде больше такой не найдешь!
    wait 1s
    mload obj 24123
    %purge% %object.id%
    дать все %actor.name%
    mecho - Коли захочешь сшить что из нее, так сходи к Ягиничне, что тут неподалеку живет.
    mecho - Она хоть и нечисть, а шить любит!
    хи
    wait 1s
    mecho - Ну, а коли не хочешь, так сам думай как ею распорядиться.
    эм вернулась к вышиванию
    *снимаем флаги (перенесено в 24116)
    *снимаю здесь чтобы если вдруг игрок потеряет ткань сможет еще раз взять квест* 
    %actor.unsetquest(303101)% 
    *%actor.unsetquest(303103)% 
  else
    wait 1s
    say И что мне с ней делать?
    пож
    wait 1s
    дать все %actor.name%
  end
else
  wait 1s
  say Возьми где взя%actor.q%!
end
~
$~
