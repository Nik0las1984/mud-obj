#11000
зашел к атаману~
0 q 100
~
wait 1
stand
msend %actor% Атаман схватил кочергу и с криком набросился на Вас!
убить .%actor.name%
~
#11001
Вход к раненому воину~
0 q 100
~
if %actor.getquest(11010)% == взят
  wait 2
  emot оглядел Вас мутным взором
  г Кто ты? Ты не похож%actor.g% на разбойника.
  взд
  emot с надеждой посмотрел на Вас
end
~
#11002
Пьяный разбойник~
0 b 50
~
щупа девк 
wait 2
switch %random.9%
  case 1
    ласка
  break
  case 2
    шлеп
  break
  case 3
    танц
  break
  case 4
    рыг
  break
  case 5
    ржать
  break
  case 6
    крушить
  break
  default
    петь
  done
~
#11003
Бородатый разбойник~
0 b 50
~
щупа девк 
wait 2
switch %random.9%
  case 1
    вмаза пьян
  break
  case 2
    петь
  break
  case 3
    мычать
  break
  case 4
    вмаз бород
  break
  case 5
    похме
  break
  case 6
    рыг
  break
done
~
#11004
Зашли к девке~
0 q 100
~
wait 1
ск %actor.name% Помоги, добрый человек, выручи из неволи.
ск %actor.name% Не могу я сама разорвать веревку.
~
#11005
Освободили девку~
0 c 100
развязать~
wait 5
поклон %actor.name%
msend %actor% Спасибо, добрый человек.
msend %actor% Скажу тебе одно, на севере у ворогов сокровищница.
msend %actor% Возьми все, что найдешь себе.
msend %actor% Слышала только то, что когда кто туда входит, 
msend %actor% Постоянно говорит "сезам откройся".
msend %actor% Что это такое, мне не ведомо, но наверное что-то важное.
mecho Девка быстро убежала.
mpurge %self%
~
#11006
телепорт~
2 d 100
сезам откройся~
wait 1
wechoaround %actor% %actor.name% исчез%actor.q%.
wteleport %actor% 11062
wsend %actor% Что-то утащило вас вниз.
wat 11062 wechoaround %actor% %actor.name% с криком упал%actor.g% сверху.
~
#11007
Пленник на дыбе.~
0 b 100
~
switch %random.5%
  case 1
    mecho Пленник завопил от боли.
  break
  case 2
    mecho Пленник поднял голову и посмотрел на вас.
    wait 2
    г Кто вы? Откуда пришли?
    г Сделайте доброе дело, убейте. Все одно я на свете не жилец.
    г И не жалейте тут никого, лихие это люди. 
    г Да поищите тут в комнате или рядом.  
    г Ключ от своей сокровищницы они тут прячут.
  break
  case 3
    mecho Пленник потерял сознание.
  break
done
~
#11008
Открыли ларец~
1 p 100
~
set name2 %actor.id%
if %actor.leader%
  set name1 %actor.leader%
  if (%name1.vnum% == -1) && (%actor.vnum% != -1))
    set name2 %actor.leader%
  end
end
%name2.wait(8)%
wait 1
oecho Письмена на ларце загорелись синим светом.
osend %name2% Вы остолбенели при одном взгляде на это.
wait 1
oecho Из него вырвался черный дым,
oecho который через секунду сгустился и превратился 
oecho в страшного, морозящего душу своим взглядом, духа. 
wait 1
oecho Призрак посмотрел на вас.
oecho Призрак сказал "Ты не мой хозяин!"
oecho Призрак сказал "Будь ты проклят как и я на веки вечные!"
wait 1
oload mob 11024
calcuid ghost 11024 mob
oforce %ghost% mkill %name2%
detach 11008 %self.id%
~
#11009
Зашли к купцу~
0 q 100
~
wait 1
ск %actor.name% Ты убил атамана? 
ск %actor.name% Многие пытались. Где теперь они?
ск %actor.name% Я богатый купец, вот и держат тут меня,
ск %actor.name% Ждут большого выкупа.
ск %actor.name% Но я припрятал от них кошель, они такие тупые, что даже не смогли нормально обыскать.
ск %actor.name% Я думал подкупить кого-нибудь, и убежать, но в благодарность за освобождение возьми эти деньги себе.
%self.gold(+2000)% 
дать 2000 кун %actor.name%
mecho Пленник скрылся в дверном проеме.
wait 1
%purge% %self%
~
#11010
Приветствие сердобольной бабули~
0 q 100
~
wait 1
if %actor.getquest(11010)% == взят || %actor.getquest(11011) == %date.yday%
  halt
end
if %actor.getquest(11010)% == сделан
  emot отвлеклась от домашних дел
  г Вижу по глазам, что есть вести из притона разбойничьего.
  г Ну, не томи, рассказывай, как там человек добрый жив али мертв?
  вопр %actor.name%
  halt
end
emot отвлеклась от домашних дел
г Здравствуй, %actor.name%! Проходи, присядь, отдохни с дороги неблизкой.
г Расскажи чего новенького деется нынче в городах и деревнях русских.
г Не случилось ли где беды какой?
всхли
г А то я теперь совсем одна осталась, да еще и человека доброго сгубила!
рыд
г Ой, что ж это я говорю, ведь недавно совсем человек на подмогу пошел.
г Может жив есче, сидит в плену, ждет чтобы выручили.
г Все ж сорванец мой не убивец какой, вор и прохвост всего лишь.
г Может подсобишь?
вопр %actor.name%
~
#11011
Вошли к старушке~
0 d 1
помогу подсоблю жив мертв~
switch %speech%
  case помогу
    case подсоблю
      wait 1
      if  %actor.getquest(11011)% == %date.yday%
        дум .%actor.name%
        г Чего? Головушку то тебе разбойники не повредили?
        г Помогать то уже и не в чем.
        взд
        halt
      end
      if %actor.getquest(11010)% == взят || %actor.getquest(11010)% == сделан
        г Чего? Рассказала ведь тебе уже чем помочь.
        г Добрый человек в притоне разбойничьем, что к западу от города, томится.
        г Каравай ржаной дан тебе был для него.
        г Слопал%actor.g%, чтоли?
        ворч .%actor.name%
        halt
      end
      рад
      г Вот счастье то!
      г Ну, слушай тогда.
      г Есть у меня внучок-сорванец, сиротинушка совсем.
      взд
      г Мамка то его совсем недавно в мир иной отошла.
      г С тех пор он к разбойничкам ушел.
      г Не хочу, говорит, также с голоду умереть.
      г Оставил меня одну, разбойник, собрался стать атаманом.
      груст
      г А давече был у меня в гостях воин - я ему эту историю то и рассказала.
      г Так решил он вразумить внучка моего глупого.
      г Как неделю назад умчался в притон разбойничий, так и нет вестей от него.
      г Я вот и думаю, обманул его сорванец мой, заманил в полон хитростью.
      г А еды то у них лишней нет, кормить пленников не станут.
      г Помрет ведь добрый человек зазря.
      всхли
      г Возьми вот хлебушек с собой, сам%actor.g% только не слопай весь.
      mload obj 2005
      дать all.каравай .%actor.name%
      %actor.setquest(11010 взят)%
      г Притон тот разбойничий к западу от города находится.
      г Найди ты доброго человека, не дай помереть с голоду.
    break
    case жив
      if %actor.getquest(11010)% == сделан
        wait 1
        рад
        г Счастье то какое! Прям камень с души свалился!
        рад .%actor.name%
        г Сорванец то, стало быть, не захотел возвращаться.
        г Ну да Боги рассудят кто прав а кто виноват.
        взд
        г Награжу тебя чем смогу за такую добрую весть!
        dg_cast 'доблесть' .%actor.name%
        %actor.setquest(11010)%
        %actor.setquest(11011 %date.yday%)%
        halt
      end
    break
    case мертв
      if %actor.getquest(11010)% == сделан
        wait 1
        рыд
        г Горе то какое!
        emot схватилась за сердце, как бы не померла
        рыд .%actor.name%
        wait 1
        emot немного успокоилась
        г Что ж, если сорванец мой сгубил человека, не внук он мне больше!
        взд
        г А ты награду все же заслужил%actor.g%.
        switch %actor.class%
          case 0
            eval skill 23 30 31 141 154
          break
          case 1
            eval skill 20 23 30 136
          break
          case 2
            eval skill 9 24 28 30 131 133 135 136 138 139 144 147 148 157 160
          break
          case 3
            eval skill 1 4 8 30 134 136 165 178
          break
          case 4
            eval skill 6 9 22 30 131 133 136 140 144 147 148 160 167
          break
          case 5
            eval skill 8 12 22 30 31 132 134 136 137 140 141 142 143 144 145 147 148 150 151 158 
          break
          case 6
            eval skill 8 20 23 30 136
          break
          case 7
            eval skill 20 23 30 136 168
          break
          case 8
            eval skill 20 23 30 136
          break
          case 9
            eval skill 8 20 21 23 30 31 132 134 136 137 140 141 142 143 144 145 146 147 147 150 151 158
          break
          case 10
            eval skill 7 8 11 23 30 32 132 134 137 140 150 151 154 155 156 157 159 170
          break
          case 11
            eval skill 5 8 13 23 29 30 132 134  136 137 141 142 143 146 150 151 161 164 177 180
          break
          case 12
            eval skill 8 20 23 30 130 133 134 136 139 141 144 147 148
          break
          case 13
            eval skill 20 23 30 136 141 144 145 148 166
          break
        done
        set numofskill %skill.words%
        eval n %random.num(%numofskill%)%  
        eval skillnum %skill.words(%n%)%
        if (%random.100% < 21)
          set s 2
        else
          set s 1
        end
        mload obj 11026
        set items %self.objs% 
        set book %items.(1)%
        %book.val0(2)%
        %book.val1(%skillnum%)%
        %book.val2(%s%)%
        %book.val3(-1)%
        say Вот... От мужа маво осталось. Уж и не знаю, что тут написано, но дорожил он им.
        give свит .%actor.name%
        %actor.setquest(11010)%
        %actor.setquest(11011 %date.yday%)%
        halt
      end
    break
  done
~
#11012
Дали каравай раненому воину~
0 j 100
~
wait 1
if %object.vnum% == 2005
  emot съел ржаной каравай и тяжело вздохнул
  %purge% %object%
  г Спасибо тебе, %actor.name%.
  покл .%actor.name%
  wait 1
end
if %actor.getquest(11010)% == сделан
  г Не в первый раз ты мне помогаешь уже.
  г А я все пытаюсь сорванца уму-разуму научить.
  г Ты бы сходил%actor.g% к бабуле то, сказал%actor.g% спасибо ей за заботу.
  г Да пусть не беспокоится за меня, добрые люди всегда помогают - не помру.
  wait 1
end
if %actor.getquest(11010)% == взят
  г Пытался я сорванца этого уму-разуму научить.
  г Мне даже показалось, что он хочет сам назад, к бабуле.
  г Так он и заманил меня в эту яму, мол боится при всех говорить.
  г Теперь он у разбойников в почете.
  взд
  г Не смогу я после этого бабуле в глаза смотреть.
  г Скажи ей, прошу тебя, что умерли мы с мальчишкой когда бежали из притона.
  г Не достоин он такой семьи, такой бабушки.
  груст
  %actor.setquest(11010 сделан)%
  wait 1
end
встать
открыт решетка вверх
emot тихонько выбрался из ямы
%purge% %self%
~
#11013
Рип девки~
0 f 1
~
%load% obj 11016
~
#11014
Вход к сорванцу~
0 q 100
~
if %actor.getquest(11010)% == взят
  wait 1
  язык .%actor.name%
  г Ага, еще один доброжелатель пожаловал!
  г Никого ты вызволить отсюда не сможешь! Проваливай подобру-поздорову!
  г Ключ от ямы под защитой такого детины, что тебе в жизни не одолеть!
  хихи
end
~
$~
