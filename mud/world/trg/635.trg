#63500
голодный зверюга (приветствие)~
0 r 100
~
*Если квест занят, то кирдык
if %questor635.name% != %nil%
  halt
end 
*Коли не занято то приветствуем
wait 1s
кашл
г Хто ты тахой?
г Чехо прихол сюды?
вопрос %actor.iname%
wait 1s
г А, не ухто ты заплутал%actor.g%?
хих %actor.iname%
кашл
wait 10
г Ну тоды тебе от сюда уже не выйте без чухой помощи.
кашл 
г А вот я хнаю тайное заклинание, хоторое тебя обратно на дороху ведет.
г Но сразу схажу, только услуха за услуху.
г Ты мне помохаеш, а я тебе от сюды помохаю выбраться.
вопрос %actor.iname%
~
#63501
голодный зверюга (дает квест)~
0 d 1
да согласен хорошо помогу~
*Опять же проверка на занятость квеста
if %questor635.name% != %nil%
  halt
end 
set questor635 %actor%
global questor635
кашл
г Так вот слухай.
г Жило нас тут...
кашл
wait 5
г Так жило нас тут целое семейство... жило не тужило.
г Пока не пришли от кухато с севера странные зверюхи.
г Одни тахие с золотистым животом, а другие кахие-то изохнутые...
г Страхные одним словом.
wait 10
г Так они всю живность повывели хады, нас на холод обрехли!
г Все мои сородичи ушли в друхие места, а меня бросили.
г Говорят, ты уже слаб и будешь только обузой.
wait 10
г Помохи, добр человека.
г Что бы они ушли, убий матку златобрюхов и кособрюхов.
~
#63502
голодный зверюга (принимает квест)~
0 r 100
~
*Проверка на выполнение квеста и смерть обоих папских мобов
if (%questor635.name% == %actor.name%) && (!%exist.mob(63526)%) && (!%exist.mob(63527)%)
  wait 5
  г Ох, спасибо тебе!
  г Я хотел бы тебе что-нибудь подарить в блаходарность за твою помощь.
  взд
  г У меня было храсивое тахое резное запястье.
  г Да тольхо толи потерял я ехо, толи хад кахой-то ухрал...
  wait 3
  рыд
  г А тахое удобное ведь запястье было!
  г Позволяло летать, михать, видеть чудищ невидимых, под водой дышать.
  г Я в нем в сто раз лучше вещи запоминал! Я бы даже сказал в сто пять раз!
  г И все мои махические штучки проходили намнохо чаще...
  г На семьдесят-восемьдесят...протсе...пратце...ну ты понял, лучше!
  тоск
  wait 5
  if (%random.1000% <= 40)
    mload obj 103
    г &WВот тут волхвы приходили. И хто из сюды звал-то.... Хороче оставили... Отдаю тебе...&n
    дать чекуш %actor.name%
  else
    г Словом не обессудь, нечем мне тебя вознахрадить.
  end
  г Ладно, ках я и обещал, я перемещу тебя на дороху, с которой ты приш%actor.y%. 
  бросить все
  г Прощай.
  mecho Все вокруг померкло.
  mecho Когда же вы пришли в сознание, вы уже были на дороге.
  mteleport все 60939 
  detach 63500 %self.id%
  detach 63501 %self.id%
  detach 63502 %self.id%
end
~
#63503
пинаем пень~
1 c 100
пнуть~
if  !%arg.contains(пень)%
  osend %actor.iname% _Чего или кого пинаем?
  halt
end      
if (%arg.contains(пень)%)
  wait 10
  oechoaround %actor.iname% _Мощным пинком %actor.iname% разнес%actor.q% старый пень на мелкие щепки.
  osend %actor.iname% _Мощным пинком вы разнесли старый пень на мелкие щепки.
  wait 1s
  oecho _Из останков пня с диким шипением вылетело какое-то животное и... 
  oecho _Уносимое силой пинка летело на север.
  calcuid from_rm 63528 room
  exec 63504 %from_rm.id%  
  opurge %self%
end
~
#63504
разъяренный еж (лоадим)~
2 z 100
~
wecho _Какое-то животное прилетело с юга и с силой ударившись о сосну упало на землю.
wload mob 63522
~
#63505
разъяренный еж (говорит о белке)~
0 r 100
~
wait 5
г Чего творишь, человек?
г Начто ты пень мой сломал!
г Где теперь я жить буду?!
wait 1
г Ну ладно, я все равно переселяться хотел.
г Местная белка меня совсем замучила, приходит специально сюда, что бы поиздеваться над мной.
г Каждую ночь она кидает орехи на мой пень, спать не дает.
думать %actor.iname%
г Вот что я тебе скажу, разрушитель пней.
г Коли ты это белку зашибешь так же умело, как мой пень, я тебя вознагражу.
г Только вот я не знаю, где ее жилище, так что сам%actor.g% ищи.
wait 10
плю
г Чуть не забыл.
г Белку можно проучить, только специальной антибелковской палкой.
г Вот, я одну припас, но в ход пустить не успел.
mload obj 63501
дать дубин %actor.iname%
detach 63505 %self.id%
~
#63506
шатаем сосну~
1 c 100
шатать~
if  !%arg.contains(сосна)%
  osend %actor.iname% _Чего шатаем то? Если хотите, чтобы шатало Вас, идите выпейте!
  halt
end 
oechoaround %actor.iname% _%actor.iname% вцепил%actor.u% в сосну и начал%actor.g% ее усиленно раскачивать.
oechoaround %actor.iname% _Из дупла, расположенного на самом верху донесса страшное шипение.
oechoaround %actor.iname% _Что-то диким свистом рухнуло на землю.
osend %actor.iname% _Вы вцепились в сосну и начали ее усиленно раскачивать.
osend %actor.iname% _Через какое-то время что-то со свистом упало на землю.
oload mob 63524 
detach 63506 %self.id%
~
#63507
плющим белку~
0 c 0
расплющить плющить~
if  !%arg.contains(белка)%
  msend %actor.iname% _Кого будем так сильно бить?   
  halt
end
eval weapon %actor.eq(16)%
if (%weapon.vnum% != 63501)
  msend %actor.iname% _Сказали же: "Специальная дубина!"
  halt
end 
mechoaround %actor.iname% _Ужасающим ударом %actor.iname% накрыл%actor.g% белку своей увесистой дубиной.
mechoaround %actor.iname% _Повторив это движение еще по крайней мере раз десять %actor.iname% с облегчением вздохнул%actor.g%.               
mechoaround %actor.iname% _От стараний %actor.dname% белка превратилась в волосатую котлету.
msend %actor.iname% _Ужасающим ударом Вы накрыли белку липовой дубиной.
msend %actor.iname% _Повторив движение еще раз десять в вскоре успокоились.
msend %actor.iname% _От белки осталась лишь волосатая котлета. 
calcuid from_rm 63548 room
exec 63510 %from_rm.id%    
mpurge %self%
~
#63508
разъяренный еж (дает награду)~
0 j 100
~
if (%object.vnum% == 63503)
  wait 10
  mpurge %object%
  г Как ты ее, прям дух захватывает.
  г Ну, думаю ты славно потрудил%actor.u% и я вознагражу тебя.
  switch %random.4%
    case 1
      if %world.curobjs(64504)% < 10 
        г Вот держи, это те орехи, что в меня эта белка кидала.
        г Может они тебе пригодятся.
        mload obj 63504
        дать all .%actor.iname%
      end
    break
    case 2
      if %world.curobjs(64505)% < 10     
        mecho _Еж утопал за ближайшее дерево и от дуда донеслись грязные ругательства.
        msend _%actor.iname% Когда же он вернулся, Вы заметили, что он протягивает Вам какую-то иглу.                                                             
        mload obj 63505
        дать all .%actor.iname%
        г Вот, держи. Она мне давно мешалась, а тебе еще долго прослужит.
        г Она очень остра и прочна как, если бы была сделана из стали.
      end
    break
    case 3
      if %world.curobjs(64506)% < 10 
        mecho _Еж начал производить какие-то манипуляции с телом толстой белки.
        msend _%actor.iname% Через некоторое время он закончил работу и повернулся в Вам.
        mload obj 63506
        дать all .%actor.iname%
        г Вот. Этот воротник настолько же удобен, на сколько и хорошо защищает от магии.
      end
    break
    case 4
      if %world.curobjs(64507)% < 10    
        mecho _Еж начал производить какие-то манипуляции с телом толстой белки.
        msend _%actor.iname% Через некоторое время он закончил работу и повернулся в Вам.
        mload obj 63507
        дать all .%actor.iname%
        г Этот браслет сделает тебя еще привлекательней.
        msend _%actor.iname% Еж ухмыльнулся глядя на Вас.
      end
    break
  done
else
  г На кой мне это?
  брос all
end
~
#63509
репоп~
2 f 100
~
* моб разъяренный еж делитится на репопе
* calcuid from mb 63522 mob
* attach 63505 %from mb.id%
calcuid from_oj 63502 obj
attach 63506 %from_oj.id%
~
#63510
лоадим расплющенную белку~
2 z 100
~
wload obj 63503 
~
$~
