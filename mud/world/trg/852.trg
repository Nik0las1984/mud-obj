#85200
ВстретилиВечногоДеда~
0 q 100
~
wait 1s
say Здравия тебе, путник...
эм улыбнулся и лучики морщин рассыпались по его лицу
wait 2s
mecho - Молод смотрю ты - совсем зеленый, а по таким страшным местам ходишь.
эм покачал головой
wait 1s
mecho - Много легенд сложено об этой земле...
wait 1s
mecho - Вот например, сказывают:
switch %random.4%
  case 1
    mecho - Если привидится тебе в лесу чудище о шести головах,
    mecho - То надо остановится и сказать "я пришел"!
    эм задумчиво почесал бороду
    wait 1s
    mecho - Тогда вмиг прилетит орел златокрылый, силой своей могущий устрашить даже звезды!
    mecho - Подхватит медным клювом, да унесет тебя в край неведомый...
    mecho - Нечасто возвращались оттуда храбрые воины...
    mecho - Но бывало и такое - вертаются со златом-серебром, 
    mecho - Верхом на коне-огне Перуновом!
  break
  case 2
    *
    mecho - Что бродит по лесным тропам дивный кот Баюн. 
    mecho - Кто услышит его колыбельную вмиг засыпает мертвым сном! 
    эм задумчиво почесал бороду
    wait 1s
    mecho - Котик тот, мастер сказки сказывать...
    mecho - Да что ни сказка, то чистая правда!
    mecho - Все знает, все ведает кот Баюн!
    mecho - Кто совладает с ним, узнает много тайн великих!
  break
  case 3
    *
    mecho - Что живет посреди лесной глуши старуха-шептуха...
    mecho - А в избе у нее под дубовым столом корзина, а в корзине петух черный.
    mecho - И раз в сто лет сносит тот петух яйцо волшебное, темной силой наделенное.
    эм задумчиво почесал бороду
    wait 1s
    mecho - Коли его жаба согреет - родится из него зверь могучий Василиском звать.
    mecho - Зверь тот могуч, всю княжескую дружину положить может!
    
  break
  case 4
    *
    mecho - Что местный леший слаб стал на голову.
    mecho - И крадет он дороги в лесу, да посвистывает!
    mecho - Тешит мохнатые свои совьи глаза...
    эм задумчиво почесал бороду
    wait 1s
    mecho - Скоро совсем не станет дорог в лесу, один бурелом проклятый!
    эм сокрушенно покачал головой
    wait 1s
    mecho - А были времена, когда тропы лесные сквозь самые дремучие леса вились!
    mecho - И ходил простой люд теми тропами до самого Шендерского леса...
    mecho - Ну, а оттуда до покоев самой Морены рукой подать...
    
  break
  default 
    halt
  break 
done  
wait 1s
mecho - Тако сказывают...
~
#85201
ВходВПеруновуКрепь~
2 d 0
*~
*mecho %speech%
if %speech% == я пришел
  wait 1s
  *mecho %speech% 
  wecho _Из глубины лесной что-то стало надвигаться прямо на Вас!
  wait 1s
  wecho _Перед Вашими глазами искрой промелькнул чудовищный коготь.
  wait 1s
  wecho _В голубом, ослепляющем свете возникла невероятного размера птица...
  wait 1s
  wecho _Перед Вами предстал удивительно величественный орел, могущий устрашить даже звезды.
  %send% %actor% Горящие глаза орла с яростью впились в Вас, а из его разверстого клюва вырвался жуткий клекот.
  %echoaround% %actor% Горящие глаза орла с яростью впились в %actor.vname%, а из его разверстого клюва вырвался жуткий клекот.
  %send% %actor% Железные когти сомкнулись вокруг Вас, как стены темницы.
  %echoaround% %actor% Железные когти сомкнулись вокруг %actor.rname%, как стены темницы и унесли в небо.
  wait 1s
  if (( %actor.religion% == 0 ) && ( %actor.class% != 4 ))
    *еслит языч
    %send% %actor% _Вас укутала ночь...
    wait 3s
    *телепорт в перунову крепь
    *если зону пока не надо подключать закоментарьте следующую строчку
    wteleport %actor% 69600  
    %send% %actor% Сознание вновь вернулось к Вам!
  else
    %send% %actor% Но едва коснувшись Вашего тела, орел брезгливо бросил Вас на землю.
    %echoaround% %actor% Но едва коснувшись тела %actor.rname%, орел брезгливо бросил Вас на землю.
    wait 1s
    if %actor.class% == 4
      wecho  Следующий раз в живых не оставлю, убойца! - донеслось с небес.
      %send% %actor% Не место на священной земле тем, кто кровь зря проливаепт!
    else
      wecho  Следующий раз в живых не оставлю, христианская собака! - донеслось с небес.
    end
  end
end
~
#85202
ВошлиКЛодочнику~
0 r 100
~
*телепортер на велесово дворище 
wait 1s 
if %actor.religion% == 0
  *еслит языч
  поклон %actor.name% 
  say Здравия тебе, путник!
  mecho - Велено мне волхвами местными, перевозить любого желающего на священный остров.
  mecho - Лежит тот остров посреди озера Неро!
  эм махнул куда-то на восток
  wait 1s 
  mecho - Там говорят, обитает сам Велес!
  wait 1s 
  mecho - Хоть и святое это дело, но мне тоже кушать надобно.
  mecho - Посему положи кун 100 за перевозку.
else 
  эм начал испуганно оглядываться
  say Ты чего тут бродишь, православный?
  wait 1s
  mecho - Коли незнаешь, что здесь земля древних богов?
  mecho - А я занимаюсь перевозкой на священную скалу посреди Неро.
  эм махнул куда-то на восток
  wait 1s
  mecho - Там говорят, обитает сам Велес!
  mecho - Поэтому тебе ходить туда не стоит!
  mecho - Да и волхвы местные страсть, как ваше племя не любят.
  mecho - Коль отвезу тебя и мне достанется!
  эм задумчиво почесал затылок
  wait 1s
  mecho - Впрочем я могу и рискнуть за кун тыщу...
  mecho - Только смотри, я тебя предупредил!
end 
~
#85203
ТелепортНаВелесовоДворище~
0 m 0
~
if %actor.vnum% != -1
  halt
end
if %actor.religion% == 0
  *еслит языч
  if %amount% < 100
    дать %amount% кун %actor.name%
    say Обижаешь, добрый человек! Сам плыви за столько кун-то...
  else
    mecho _Старый лодочник сноровисто столкнул лодку в воду.
    %send% %actor% И через миг Вы уже прощались взглядом с уходящим берегом.
    wait 3
    mechoaround %actor% Старый лодочник взялся за весла и скоро они с %actor.tname% скрылись из виду.
    mteleport %actor% 85090 
    %send% %actor% Ваша лодка с шуршанием ткнулась в каменистый берег.
    mechoaround %actor% Лодка с %actor.tname% на борту с шуршанием ткнулась в каменистый берег.
  end
else
  if %amount% < 1000
    дать %amount% кун %actor.name%
    say Обижаешь, православный! Я и так из-за тебя рискую своей головой...
    mecho - Коли незнаешь, что здесь земля древних богов?
    mecho - Там говорят, обитает сам Велес!
    mecho - Поэтому тебе ходить туда не стоит!
    mecho - Да и волхвы местные страсть, как ваше племя не любят.
    mecho - Коль отвезу тебя и мне достанется!
    mecho - Впрочем я могу и рискнуть за кун тыщу...
  else
    mecho _Старый лодочник сноровисто столкнул лодку в воду.
    %send% %actor% И через миг Вы уже прощались взглядом с уходящим берегом.
    wait 3
    mechoaround %actor% Старый лодочник взялся за весла и скоро они с %actor.rname% скрылись из виду.
    mteleport %actor% 85090 
    %send% %actor% Ваша лодка с шуршанием ткнулась в каменистый берег.
    mechoaround %actor% Лодка с %actor.tname% на борту с шуршанием ткнулась в каменистый берег.
  end
end
~
#85204
ТелепортВСарскоеГородище~
0 m 0
~
if %actor.vnum% != -1
  halt
end
if %actor.religion% == 0
  *еслит языч
  if %amount% < 100
    дать %amount% кун %actor.name%
    say Обижаешь, добрый человек! Сам плыви за столько кун-то...
  else
    mecho  Молодой волхв сноровисто столкнул лодку в воду.
    %send% %actor% И через миг Вы уже прощались взглядом с уходящим берегом.
    wait 3
    mechoaround %actor% Молодой волхв взялся за весла и скоро они с %actor.tname% скрылись из виду.
    mteleport %actor% 85233
    %send% %actor% Ваша лодка с шуршанием ткнулась в каменистый берег.
    mechoaround %actor% Лодка с %actor.tname% на борту с шуршанием ткнулась в каменистый берег.
  end
else
  if %amount% < 1000
    дать %amount% кун %actor.name%
    say Обижаешь, православный! Добирайся в таком случае вплавь!
  else
    mecho _Молодой волхв сноровисто столкнул лодку в воду.
    %send% %actor% И через миг Вы уже прощались взглядом с уходящим берегом.
    wait 3
    mechoaround %actor% Молодой волхв взялся за весла и скоро они с %actor.rname% скрылись из виду.
    mteleport %actor% 85233
    %send% %actor% Ваша лодка с шуршанием ткнулась в каменистый берег.
    mechoaround %actor% Лодка с %actor.tname% на борту с шуршанием ткнулась в каменистый берег.
  end
end
~
#85205
ВошлиКЛодочнику2~
0 r 100
~
*телепортер в сарское городище 
wait 1s 
if %actor.religion% == 0
  *еслит языч
  эм поднял голову от работы
  say Здравия тебе, путник!
  wait 1s 
  mecho - На большую землю желаешь?
  mecho - Сто кун на жертву Велесу и вмиг будешь там! 
  ул
else 
  эм поднял голову от работы
  wait 1s
  хму
  wait 1s
  say Ты откуда тут взялся, православный?
  wait 1s
  mecho - Коли незнаешь, что здесь земля древних богов?
  wait 1s
  mecho - Незнаю уж как ты сюда забрался, но назад повезу за тыщу кун!
  mecho - И не меньше!
end 
~
#85206
ТопимПечь~
2 c 0
растопить затопить поджечь зажечь~
if !%arg.contains(печь)%
  wsend %actor.name% Что топим?
else
  wait 2
  %send% %actor% Сноровисто порубив толстые березовые поленья, Вы положили их в черную пасть каменки.
  %echoaround% %actor% Сноровисто порубив толстые березовые поленья, %actor.name% положи%actor.q% их в черную пасть каменки.
  *wait 1s
  %send% %actor% Сняв с пояса огниво, Вы подожгли сухие дрова.
  if %actor.sex% == 1
    %echoaround% %actor% Сняв с пояса огниво, %actor.name% поджег сухие дрова.
  else
    %echoaround% %actor% Сняв с пояса огниво, %actor.name% подожгла сухие дрова.
  end
  wait 1s
  wecho _&RВесело затрещал яр-огонь в каменной печи.&n
  calcuid pech 85206 obj
  %purge% %pech%
  wload obj 85203
  wait 5s
  *лоад лешего
  if !%exist.mob(85200)% 
    wload mob 85200 
    wecho _Вдруг дверь избы со скрипом открылась...
    wait 1s
    wecho _На пороге стояло невероятное существо похожее на большую шишку!
    wait 3s
    wecho _Немного отойдя от поглотившей Вас паники, Вы сообразили, что гость никто иной как леший!
    wait 3
    wecho _Древообразное чудище встало у каменки и протянуло лапищи к тлеющим углям.
    wait 3
    wecho А-а-а...  - довольно ухмыльнулся леший. - А-а-а... Тепло у тебя...
    wforce леший г Да-а-а-а... В лесу совсем холодно... или я совсем старый стал...
    wforce леший г Погреться бы мне...
  end
  detach 85206 %self%
end
~
#85207
ПрисаживайсяЛешему~
0 d 0
присаживайся садись грейся~
wait 1s
mecho - Благодарствую... - прокряхтел леший, усаживаясь на табурет.
wait 1s
mecho - Да-а-а... - со скрипом покивал головой-шишкой леший.
mecho - Совсем другое дело - не в лесу хладном тропы собирать...
wait 1s
mecho - Зато я уж почти все тропы лесные укра... то есть собрал!
mecho - Да в клубки смотал!
эм гордо потряс невзрачной берестяной корзинкой
wait 1s
mecho - Правда, скоро уже у нас спячка...
mecho - На кой мне эти тропы?
эм почесал за ухом шишки-головы
wait 1s
mecho - Тебе случайно не нужны?
attach 85209 %self%
detach 85207 %self%
~
#85208
ПодменяемПроснуться~
2 c 0
засунуть запихнуть ловить поймать посадить проснуться проснуть просну прос про~
* *совместный триг на подмену проснуться и  запихивание кота в мешок
* исправлено связи с лаганием моба и неадекватным поведением трига
if ( ( %cmd% == засунуть ) || ( %cmd% == запихнуть ) || ( %cmd% == ловить ) || ( %cmd% == поймать ) || ( %cmd% == посадить ) )
  if !%arg.contains(кота)%
    wsend %actor.name% Кого хотим поймать?
  else
    if !%exist.mob(85006)%
      wsend %actor.name% А где Вы его видите?
      halt
    end
    * * * * * * * * *если в инвентаре мешок
    if %actor.haveobj(85008)%
      wforce %actor.name% г Кис-кис!.. Киса! 
      wait 1s
      %send% %actor% Вы подняли над головой мешок, осторожно приблизились к озлобленно шипящему зверю.
      %echoaround% %actor% %actor.name% подня%actor.q% над головой мешок, осторожно приблизил%actor.u% к озлобленно шипящему зверю.
      wait 1s
      wecho _Котище яро отмахивался, пытался перевернуться на спину, но огромный мешок накрыл его с гловой.
      wait 1s
      %send% %actor% Вы вынули из котомы толстенные ремни, и сноровисто стянули ими мешок с котом Баюном.
      %echoaround% %actor% %actor.name% выну%actor.q% из котомы толстенные ремни, и сноровисто стяну%actor.q% ими мешок с котом Баюном.
      *лоадим мешок с котом
      wload obj 85000
      * удаляем пустой мешок
      calcuid nObjM 85008 obj
      wpurge %nObjM%
      * удаляем свободно ходящего кота
      calcuid mKisa 85006 mob 
      wpurge %mKisa% 
    else
      %send% %actor% А куда запихиваем?
    end
  end  
  halt
end
~
#85209
НужныЛешему~
0 d 0
нужны конечно~
wait 1s
mecho - Ну вот и ладно!
mecho - А то я грешным делом выбросить хотел...
хих
wait 1s
if %world.curobjs(85208)% > 0
  эм озабоченно стал лазить по пустой корзинке
  wait 1s
  calcuid klubok 85208 obj
  mecho - Странно, как так получилось, но их у меня украли!
  mecho - Но вот что скажу тебе...
  if %klubok.carried_by%
    set i %klubok.carried_by%
    mecho - Заныкал%actor.g% их %i.name%...
  end
  set z %klubok.room%
  mecho - Находятся они вот где: %z.name% - именно там их ищи!
  эм растворился в тишине
else
  *mload obj 85207
  mload obj 85208
  mload obj 85208
  mload obj 85208
  mload obj 85208
  mload obj 85208
  * пол все корз
  дать все .%actor.name%
  mecho - Держи!
  wait 1s
  mecho - Вдруг задумаешь клубки с тропами распустить, так помни:
  mecho - В том месте тропа распустится откуда я ее взял, и ни на каком другом!
  mecho - Как увидишь место странное - без единого дерева, без единого куста -
  mecho - Значит и есть место то - заповедное!
  wait 1s
  mecho - Там и кидай клубки.
  mecho - Только учти места там странные, дремучие...
  mecho - Коли далеко зайдешь в лапы самой Морены попадешь!
  mecho - Смотри не стань еще одной головой на тыне Мариного Чертога!
  ухм
  wait 1s
  mecho - Ну а я пошел... 
  mecho - Зверей еще пересчитаю, да и спать пойду...
  эм махнул на прощанье коряжистой лапищей и исчез
end
%purge% %self%
~
#85210
ВыманиваемКота~
2 e 100
~
*если в инвентаре мышка
if ( ( %actor.haveobj(85006)% ) && ( %actor.quested(85001)% ) )
  wait 1s
  %send% %actor% Вдруг вязаная мышка у Вас в руках ожила - запищала!
  %echoaround% %actor% Вдруг вязаная мышка у %actor.rname% в руках ожила - запищала!
  if !%exist.mob(85006)%
    if %random.100% < 50
      wload mob 85006
      wecho _Неожиданно Вы заметили кота Баюна, прятавшегося в ветвях.
      wecho _Громко мяукнув, громадный котище шваркнулся оземь, приземляясь на все четыре лапы.
      wait 1s
      
      wecho - Мяу-с-с-со-о-о! 
      wait 1s
      wecho _Заметив, что добыча не так уж проста кот Баюн замурлыкал колыбельную -
      wecho _Пусть добыча размякнет, станет сонной и ленивой...
      wait 1s
      wecho _Треугольный кошачий рот раскрылся, блеснув острейшими клыками, 
      wecho _И оттуда полилась сладкая благозвучная колыбельная, 
      wecho _Так и приглашающая закрыть глаза и вздремнуть часок... а лучше вечность...
      *цикл по чарам в клетке
      *киса не щадит никого :-) 
      foreach i %self.pc%
        *log -------- Test 85210 ( i.level --- %i.level% )
        if %i.level% > 0
          *log -------- Test 85210 ( i нет! )
          *  end
          set gn %i.eq(6)%
          *wecho %gn.name% 8888888888
          *если одет диковинный колпак на чара  значит колыбельную он не слышит
          if %gn.vnum%  != 85007
            %actor.wait(3)%
            *меняем колыюельные для разнообразия
            switch %random.2%
              case 1
                wforce баюн сказ %i.name% Пошел котик во лесок,
                *
                wforce баюн сказ %i.name% Нашел котик поясок,
                *wait 1s
                wforce баюн сказ %i.name% Чем бы зыбку подцепить
                *wait 1s
                wforce баюн сказ %i.name% Да %i.rname% положить.
                *wait 1s
                wforce баюн сказ %i.name% %i.name% будет спать,
                *wait 1s
                wforce баюн сказ %i.name% Котик %i.rname% качать.
                *wait 1s
                wforce баюн сказ %i.name% Сон да Дрема,
                *wait 1s
                wforce баюн сказ %i.name% Усыпи мое дитя... 
              break
              case 2
                wforce баюн сказ %i.name% Пошел котик во торжок,
                *wait 1s
                wforce баюн сказ %i.name% Купил котик пирожок.
                *wait 1s
                wforce баюн сказ %i.name% Идет котик по лавочке,
                *wait 1s
                wforce баюн сказ %i.name% Держит киску за лапочку.
                *wait 1s
                wforce баюн сказ %i.name% Ходят вместе они,
                *wait 1s
                wforce баюн сказ %i.name% А ты, маленький усни...
              break
            done
            %send% %i% Вы слушали, слушали, слушали... 
            %send% %i% Перед глазами все поплыло... 
            %send% %i% Нахлынула темнота...
            wforce %i.name% спать
          else
            %send% %i% Но колыбельную Вы не услышите, поскольку глухи как пробка.
          end
        end
      done
      *   wecho -
      wait 3
      wforce баюн атак .%actor.name%
      detach 85210 %self%
    end
  end
end
~
#85211
ЗапихиваемКисуВМешок~
0 c 0
засунуть запихнуть ловить поймать посадить~
if !%arg.contains(кота)%
  msend %actor.name% Кого хотим поймать?
else
  * * * * * * * * *если в инвентаре мешок
  if %actor.haveobj(85008)%
    if %self.hitp% < 4000
      %send% %actor% Кот слишком здоров и полон сил!
      halt
    end
    *
    *
    *wait 1s
    mforce %actor.name% г Кис-кис!.. Киса! 
    wait 1s
    %send% %actor% Вы подняли над головой мешок, осторожно приблизились к озлобленно шипящему зверю.
    %echoaround% %actor% %actor.name% подня%actor.q% над головой мешок, осторожно приблизил%actor.u% к озлобленно шипящему зверю.
    wait 1s
    mecho _Котище яро отмахивался, пытался перевернуться на спину, но огромный мешок накрыл его с гловой.
    wait 1s
    %send% %actor% Вы вынули из котомы толстенные ремни, и сноровисто стянули ими мешок с котом Баюном.
    %echoaround% %actor% %actor.name% выну%actor.q% из котомы толстенные ремни, и сноровисто стяну%actor.q% ими мешок с котом Баюном.
    *лоадим мешок с котом
    calcuid nTroom 85211 room
    attach 85219 %nTroom.id%
    exec 85219 %nTroom.id% 
    detach 85219 %nTroom.id% 
    * удаляем пустой мешок
    calcuid nObjM 85008 obj
    mpurge %nObjM%
    * удаляем свободно ходящего кота
    mpurge %self.id% 
    
  else
    %send% %actor% А куда запихиваем?
  end
end                     
~
#85212
ЛоадМешкаСКотом~
2 ez 100
~
wload obj 85000
~
#85213
ЛоадПечки~
2 z 100
~
wload obj 85206
~
#85214
КинулиКлубок17~
2 h 100
~
*открываем ход в шендерский лес
if %object.vnum% == 85208
  wait 1
  wpurge %object.id%
  wait 1s
  wecho _Упал клубок спутанных троп на землю - покатился!
  wait 1s
  wecho _Расступилась земля открывая путь в неведомый лес...
  wdoor 85217 west room 85043
  wait 10s
  wdoor 85217 west purge
end 
~
#85215
РезетЗоны~
2 f 100
~
calcuid stan 85276 room
attach 85206 %stan%
halt
if %world.curobjs(85203)% > 0
  *убираем горящую печь
  calcuid pechka 85203 obj
  %purge% %pechka%
end
*лоадим обычную печь
if %world.curobjs(85206)% == 0  
  calcuid stan 85276 room
  attach 85213 %stan%
  exec 85213 %stan% 
  detach 85213 %stan% 
  *аатч трига топим печь
  attach 85206 %stan%
end
*выманивание кисы
calcuid rkisa 85212 room
attach 85210 %rkisa% 
~
#85216
ВстретилиМанилу~
0 q 10
~
*телепорт в рэндомныю комнтау зоны
*%actor.wait(1)%
wait 1s 
сказ %actor.name% Ишь расходились тут! 
сказ %actor.name% Да блуждать тебе по глухим лесам вечно!
%send% %actor% Резко заболевшая голова заставила Вас на миг потерять сознание.
%echoaround% %actor% %actor.name% схватил%actor.u% за голову и побрел%actor.g% в обратную сторону.
switch %random.5%
  case 1
    %teleport% %actor% 85223
  break
  case 2
    %teleport% %actor% 85294
  break
  case 3
    %teleport% %actor% 85269
  break
  case 4
    %teleport% %actor% 85277
  break
  case 5
    %teleport% %actor% 85254
  break
  default 
    halt
  break 
done
~
#85217
БьемТуросика~
0 k 100
~
снять все
бро все
mecho _Туросик запаниковал и сбежал на север.
%purge% %self%
~
#85218
АгрНаЗверушек~
0 p 100
~
mecho _Как только %damager.name% замахнул%damager.u% на %self.rname%...
mecho _Зашумел грозно вдруг поднявшийся ветер, и оказался пред Вами страж лесной.
mtransform 85208
wait 1s
г Предупреждали тебя, не бей зазря живность!
атак .%damager.name%
wait 1s
detach 85218 %self%
~
#85219
ЛоадМешкаСКотомНаРуме~
2 z 100
~
wload obj 85000
~
#85220
УмерлиМеряне~
0 f 100
~
if (( %random.1000% < 101 ) && ( %world.curobjs(85200)% < 4 ))
  mload obj 85200
end 
~
#85221
ОбнаружениеКвестераВПеруновойКрепи~
2 e 100
~
*триг относится к зоне 696
*сюда прицеплен в виду нехватки места для тригов в 696
if !%actor.quested(85021)%
  halt
end
*
if %actor.quested(85023)%
  halt
end
if %exist.mob(69603)%
  *атачим триг лоад идола у облакогонителя
  calcuid m2 69603 mob
  attach 85222 %m2% 
  exec 85222 %m2% 
  detach 85222 %m2% 
end  
detach 85221 %self%
~
#85222
ЛоадИдолаПеруна~
0 z 0
~
*триг относится к зоне 696
*сюда прицеплен в виду нехватки места в 696
mload obj 85135  
~
$~
