#60000
приветствие ворожеи~
0 r 100
~
wait 1
msend %actor% Ворожея сказала тебе:
msend %actor% - Да защитит тебя Белобог от всякого зла, %actor.name%!
msend %actor% - Вижу, что не просто так ты сюда зашел! Могу тебе помочь!
msend %actor% - Коли ты дашь мне немного кун для жертвы Яриле,
msend %actor% - я помогу тебе мигом добраться до нужного русского города.
~
#60001
дали деньги ворожее~
0 m 1
~
wait 1
if (%amount% < 200)
  say За такую лепту сам%actor.g% Ярило разгневай. А я не рискну!
  halt
end
emot пересчитал%self.g% монеты
eval target 0
switch %amount%
  * Владимир
  case 3800
    eval target 18244
  break
  * Переяславль
  case 4100
    eval target 25007
  break
  * Искоростень
  case 6400
    eval target 13506
  break
  default
    wait 1
    msend %actor% Ворожея сказала тебе:
    msend %actor% - Могу переместить тебя куда попросишь, не задаром, конечно.
    msend %actor% - Во Владимир - за 3800 кун.
    msend %actor% - В Переяславль - за 4100 кун.
    msend %actor% - Ну а в Искоростень - за 6400 кун.
    msend %actor% - И это еще дешево, за столько-то верст!
    wait 3
    msend %actor% - Жалко кун - подыщи мне для ворожбы вещичек.
    msend %actor% - Коли в Вышгород хочешь - принеси мне ветку и сочные ягоды.
    msend %actor% - Если добудешь ветку подлиннее и грибы, то смогу отправить тебя во Псков.
    msend %actor% - А захочешь во Владимир попасть - дай мне зубы и грибы.
    halt
  done
  кив
  emot сделал%self.g% несколько странных жестов
  mechoaround %actor% %actor.name% исчез%actor.q% в клубах дыма.
  msend %actor% У вас закружилась голова, и на миг вы потеряли сознание...
  msend %actor% 
  mteleport %actor% %target% horse
  mechoaround %actor% %actor.name% появил%actor.u% здесь в клубах дыма.
~
#60002
даем ингры ворожее~
0 j 100
~
wait 1
If (%object.vnum%==713)
  If (%object.val1%>5)
    Say Отличная ветка! Подойдет для сотворения заклятья!
    If  !(%self.haveobj(60067)%)
      Mload obj 60067
    Else 
      Say Не... ветка у меня уже есть. Вторая не нужна пока.
    End
  Else
    Say Не... ветка не достаточна длинная! Такая не подойдет.
  end 
End
If (%object.vnum%==705)
  If (%object.val1%>5)
    Say Замечательные сочные ягоды! В самый раз для заклинания!
    If  !(%self.haveobj(60061)%)
      Mload obj 60061
    Else 
      Say У меня уже есть ягоды! Еще одни пока не нужны!
    End
  Else
    Say Нет! Эти ягоды недостаточно сочные! Такие не подойдут.
  end 
End
If (%object.vnum%==700)
  If (%object.val1%>5)
    Say Уу-у! Замечательные грибы!
    If  !(%self.haveobj(60060)%)
      Mload obj 60060
    Else 
      Say У меня уже есть гриб! Мне второй не нужен!
    End
  Else
    Say Не-е-ет! Этот грибок маленький и червивый. Такой не годиться!
  end 
End    
If (%object.vnum%==751)
  If (%object.val1%>10)
    Say Отличные белые зубы!
    If  !(%self.haveobj(60068)%)
      Mload obj 60068
    Else 
      Say Зубы у меня уже есть! Вторая челюсть пока не нужна!
    End
  Else
    Say Не..этот гнилой зуб не годиться для заклинания! Ищи те, что получше!
  end 
End
Mpurge %object%
if (%self.haveobj(60067)% && %self.haveobj(60061)%)
  say Отлично! Сейчас я принесу жертву и сотворю заклятье!
  say С его помощью я перенесу Вас в Вышгород
  mecho Ворожея что-то пошептала над ингредиентами и поднесла их к идолу.
  брос все
  mat 63670 mecho Кто-то появился здесь в клубах сизого дыма.
  mteleport all 63670
end
if (%self.haveobj(60067)% && %self.haveobj(60060)%)
  say Отлично! Сейчас я принесу жертву и сотворю заклятье!
  say С его помощью я перенесу Вас во Псков
  mecho Ворожея что-то пошептала над ингредиентами и поднесла их к идолу.
  брос все
  mat 75105 mecho Кто-то появился здесь в клубах сизого дыма.
  mteleport all 75105
end   
if (%self.haveobj(60068)% && %self.haveobj(60060)%)
  say Отлично! Сейчас я принесу жертву и сотворю заклятье!
  say С его помощью я перенесу Вас во Владимир
  mecho Ворожея что-то пошептала над ингредиентами и поднесла их к идолу.
  брос все
  mat 18244 mecho Кто-то появился здесь в клубах сизого дыма.
  mteleport all 18244
end
~
#60003
Портал во Владимир~
2 z 100
~
wportal 18244 2
~
#60004
Портал в Переяславль~
2 z 100
~
wportal 25007 2
~
#60005
Портал в Искоростень (пока не подключен -- в Вышгород)~
2 z 100
~
wportal 13506 2
~
$~
