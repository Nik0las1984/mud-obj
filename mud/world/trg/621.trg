#62100
Старый ярл приветствует~
0 r 100
~
if (%quest621.name% == %nil%)
  wait 1s
  привет %actor.name%
  г Здравствуй, %actor.name%.
  г Приветствую тебя от имени всей нашей деревни!
  г Мы всегда рады гостям.
  грусть
  wait 1s
  г Хотя, теперь нам не до радости.
  г Беда у нас одна приключилась и не знаем, что и делать то.
elseif (%quest621.name%== %actor.name%)
  wait 1s
  г Ну как продвигаются поиски?
  вопрос %actor.name%
end

~
#62101
Старый ярл дает квест~
0 d 0
продолжай~
if (%quest621.name% == %nil%)
  wait 1s
  set quest621 %actor%
  global quest621
  г Ах.
  г Пропал у нас один мальчонка.
  г Ушел он на поле коров пасти и так и не вернулся.
  г Может с ним приключилось чего нехорошего?
  wait 1s
  г Молва идет, что возле пастбища есть пещера, в которой живет страшный зверь
  г И тех, кто осмеливается приблизиться к его пещере, он убивает.
  г Вот никто и не хочет туда идти, боятся.
  г Все, кто туда и хотел пойти, в последний момент струсили и отказались.
  wait 1s
  г Может, ты сходишь, проверишь, как он там?
  г И не бойся ты этих россказней, нет там никакого зверя!
  г Иди же, и да хранят тебя Боги.
elseif (%quest621.name% == %actor.name%)
  wait 1s
  г Чего ты все тут языком лопочешь?
  г Наш%actor.y% мальчугана нашего?
end

~
#62102
Старый ярл принимает квест~
0 j 100
~
if %object.vnum% == 62104 && %quest621.name% == %actor.name%
  wait 1
  mpurge %object%
  msend %actor.name% Вы рассказали о звере в пещере старому ярлу.
  mechoaround %actor.name% %actor.name% рассказал%actor.g% о звере в пещере старому ярлу.
  плак %actor.name%
  wait 2s
  г Как это грустно.
  г Никто, до самого последнего момента и не думал, что все так может обернуться.
  г Оказывается, все же это было правдой.
  г И в той самой пещере жил этот страшный зверь, марбендилл.
  взд
  wait 1s
  eval level 24-%actor.remort%/2
  г Ну что же. Ты оказал нам большую услугу, убив его, и теперь уже он не опасен.
  if %actor.class% == 4 && %actor.skill(скрытый удар)% == 0
    wait 1s
    г Я вижу, ты хороший наемник и думаю, ты достоин того, что бы я научил тебя кое чему.
    г В свое время, мой учитель научил меня древнему искусству.
    г Но, что бы я смог тебя ему научить, необходимо пройти испытание.
    г Если ты осилишь его, я возьмусь учить тебя.
    wait 1s
    г Приготовься, сейчас я телепортирую тебя в древний зал испытаний!
    mteleport %actor.name% 62164
    calcuid delete_trig 62109 mob
    detach 62100 %delete_trig.id%
    detach 62101 %delete_trig.id%
    detach 62102 %delete_trig.id%
  else
    wait 1s
    set Get_item %random.10%
    set vnum 62117
    eval vnum %vnum% + %Get_item%
    if (%vnum% > 62120)  || (%world.curobjs(%vnum%)% > 2)
      г Ты теперь всегда желанный гость для нас!
      г Мы всегда рады твоему визиту.
    else
      mload obj %vnum%
      г Вот твоя награда.
      дать все %actor.name%
    end
    calcuid delete_trig 62109 mob
    detach 62100 %delete_trig.id%
    detach 62101 %delete_trig.id%
    detach 62102 %delete_trig.id%
  end
end
~
#62103
Старый ярл учит умению~
0 r 100
~
if (%exist.mob(62100)% == %nil%)
  if (%exist.mob(62110)% == %nil%)
    if (%exist.mob(62111)% == %nil%)
      if (%exist.mob(62112)% == %nil%)
        if (%actor.id% == %quest621.id%)
          if (%actor.class% == 4)
            wait 1s
            г Ты удивил%actor.g% меня %actor.name%.
            г Я научу тебя умению Скрытого удара.
            г Против него не сможет устоять даже самый могущественный воин.
            wait 1s
            mskillturn %actor% скрытый.удар set
            msend %actor% Старый Ярл показал Вам как нужно скрывать свои намерения ударить.
            г А теперь прощай.
            calcuid delete_trig 62109 mob
            detach 62103 %delete_trig.id%
          end
        end
      end
    end
  end
end

~
#62104
Сник прошел~
0 r 100
~
* wait 10
if (%actor.affect(подкрадывается)% == 1)
  wait 10
  set skillquest1 1
  global skillquest1
  mecho Соломенное чучело вспыхнуло ярким пламенем и исчезло.
  mecho Это значит, Вы прошли это, наверно одно из самых сложных испытаний.
  wait 1
  mpurge %self%
end

~
#62105
Сник не прошел~
0 r 100
~
*wait 10
if (%actor.affect(подкрадывается)% == 0)
  wait 10
  г Я тебя вижу! Попробуй еще раз, претендент!
  msend %actor.iname% Неведомая сила подхватила Вас и куда-то унесла.
  mteleport %actor.iname% 62164
end

~
#62106
Удалось отравить~
0 k 100
~
if (%self.affect(яд)% == 1)
  wait 5
  г Да. Тебе это удалось! Ты отравил меня.
  set skillquest2 1
  global skillquest2
  wait 1s
  mecho Воздух содрогнулся от пронзительного звука.
  mecho Ваше сознание на время померкло.
  wait 1s
  mecho Когда же вы пришли в себя, Вы заметили исчезновение кожаного чучела.
  mecho Это может означать только одно - Вы прошли это испытание!
  wait 1
  mpurge %self%
end

~
#62107
Удалось заколоть (ОН)~
0 e 0
нанес удар своим оружием в спину манекена воина.~
wait 10
mecho Манекен взвыл ужасным, как будто человеческим голосом!
mecho Это поистине чудовищный звук, от которого Ваше сознание начало тускнеть.
mecho Когда же Вы пришли в сознание, Вы заметили, что манекена уже нет на месте!
mecho Это значит Вы прошли испытание.
wait 1
mpurge %self%

~
#62108
Удалось подсечь~
0 k 100
~
if (%self.position% == 6)
  wait 5
  mecho От куда-то донесся сильный треск, от которого Вы на мгновение потеряли сознание.
  wait 1s
  mecho Когда же Ваше сознание снова вернулось к Вам, чучела уже не было.
  mecho Теперь Вы прошли и это испытание.
  set skillquest3 1
  global skillquest3
  wait 1
  mpurge %self%
end
if (%actor.position% == 6)
  г Эх ты неумеха! Чего ты же сюда приш%actor.y%?!
end

~
#62109
Убит марбендилл~
0 f 100
~
msend %actor.name% Мощным ударом Вы выбили у марбендилла правый клык!
mechoaround %actor% Мощным ударом %actor.name% выбил%actor.g% у марбендилла правый клык!
mload obj 62104
if (( %random.1000% < 401 ) && ( %world.curobjs(62109)% < 6 ))
  mload obj 62109
end
if (( %random.1000% < 401 ) && ( %world.curobjs(62108)% < 11 ))
  mload obj 62108
end

~
#62110
Телепорт из зала испытаний~
2 e 100
~
wait 1
wecho Громкий звук рассек тишину.
wecho Все началось трястись и яркая вспышка возникшая в воздухе на мгновение ослепила Вас!
wportal 62116 2

~
#62111
Репоп~
2 e 0
~
set quest621 %nil%
global quest621
calcuid delete_trig 62109 mob
attach 62100%delete_trig.id%
attach 62101%delete_trig.id%
attach 62102%delete_trig.id%
attach 62103 %delete_trig.id%

~
#62112
Удалось заколоть (ОНА)~
0 e 0
нанесла удар своим оружием в спину манекена воина.~
wait 10
mecho Манекен взвыл ужасным, как будто человеческим голосом!
mecho Это поистине чудовищный звук, от которого Ваше сознание начало тускнеть.
mecho Когда же Вы пришли в сознание, Вы заметили, что манекена уже нет на месте!
wait 1
mpurge %self%

~
$
$
