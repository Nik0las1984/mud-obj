#28100
проверка на наличие хайда~
2 e 100
~
wait 1s
if %actor.affect(прячется)% == 1
  if %exist.mob(28100)%
    %force% %world.mob(28100)% кричать Т Р Е В О Г А !
    if %exist.mob(28100)%
      attach 28122 28199
      run 28122 28199
    end
  end
end
~
#28101
приветствие купца~
0 q 100
~
wait 1s
шля
г Я смотрю, Вы люди опытные и не местные...
г Может поможете мне с одним дельцем щекотливым?
вопр
~
#28102
Квест купца~
0 d 1
да согласен помогу конечно ~
wait 1s
if %actor.vnum% != -1
  halt
end
set questor2 %actor%
global questor2
г Отлично! Я знал что такой могучий воин как %actor.name% мне не откажет!
г Проблемка моя в сущности ерундовая. Брат мой старший, боярин, страшная сволочь.
г Законы не соблюдает, мзду взымает, купцов мирных грабит, баб насильничает...
эм испуганно огляделся и поднес указательный палец к губам
wait 1s
г Тссс...
wait 1s
г Это я Вам по секрету, смотрите, не рассказывайте никому!
миг %actor.name%
г Так вот, если бы у меня было колечко его, боярское, я бы смог у князя Тверского защиту вытребовать.
г Если принесешь мне колечко, я тебе подкину вещицу нужную.
wait 1s
г Да, забыл сказать. Колечко то боярин с собой носит всегда.
г Но так просто не отдаст, воин он сильный. Но Вы вроде тоже не промах!
г Ну все, удачи Вам!
if %exist.mob(28105)%
  attach 28106 %world.mob(28105)%
end
detach 28101 %self.id%
detach 28102 %self.id%
~
#28103
Приветствие Боярина~
0 q 35
~
wait 1s
if (%world.curmobs(28106)% > 0)
  г Ты кто такой?
  дум
  эм явно не узнает Вас
  wait 2s
  эм вопросительно взглянул на жену
  г Дорогая, ты его знаешь?
  wait 2s
  %force% %world.mob(28106)% эм пристально взглянула на Вас
  %force% %world.mob(28106)% г Как не знаю, это разве не любовник троюродного внука сестры твоей бабушки?
  эм задумался
  wait 2s
  г Нет, не он.
  wait 2s
  %force% %world.mob(28106)% г А может... а черт с ним. Зачем он тебе сдался?
  %force% %world.mob(28106)% эм насмешливо посмотрела на боярина
  wait 2s
  эм махнул рукой и занялся своими делами
  detach 28103 %self.id%
end
~
#28104
Дали кольцо~
0 j 100
~
wait 1
if %actor.id% != %questor2.id%
  г Ты кто таков, и откуда взялся? А ну иди отсюдова!
  плю
  брос все
  halt
end
if %object.vnum% != 28107
  г Что Вы мне всякую дрянь суете?
  морщ
  брос все
  halt
end
г О, это как раз то, что мне нужно!
г Спасибо Вам, добрая душа.
%purge% %object%
wait 2s
г А вот и награда.
%load% obj 28111
дат пучок %actor.name%
г Отнесите это травнику.
г Он давно искал именно эту траву, и взамен обязательно даст какую-нибудь полезность.
wait 1s
г А я поеду к князю Тверскому на поклон.
%echo% Купец попрощался с вами и вышел из избы.
%purge% %self%
~
#28105
Травник~
0 j 100
~
wait 1s
if %object.vnum% != 28111
  г Извиняйте, уважаемый, но мне этого добра ненадобно. Забирайте обратно.
  дат %object% %actor.name%
  halt
end
%purge% %object%
set questortravnik %actor%
global questortravnik
г Не может быть! Где ты нашел эту траву!
эм восторженно потирает руки
г Вот я вечерком покур...
эм закашлялся
wait 1s
г ..покумекаю, как бы ее лучше использовать...
г Держи, мил человек, награду свою!
if %world.curobjs(28106)% < 7 && %random.20% < 2
  mload obj 28106
  дат амул %actor.name%
elseif %world.curobjs(28112)% < 7 && %random.20% < 4
  mload obj 28112
  дат талисман %actor.name%
else
  %send% %actor% Травник дал вам 3000 кун.
  %actor.gold(+3000)%
  г Если еще что нужно будет - обращайся!
end
end
~
#28106
Вытребовать кольцо у боярина~
0 c 1
пугать испугать напугать пугнуть~
wait 1s
if %exist.mob(28101)% && %exist.mob(28102)% && %exist.mob(28103)% && %exist.mob(28104)%
  одет шапк
  г Да ты, наглец, пугать меня вздумал?
  кричать СТРАЖА, КО МНЕ!
  уб %actor.name%
  halt
end
одет шапк
г Ну все, хватит! Успокойся!
эм понурил взор
wait 2s
г Ну что тебе от меня надо, ирод ты чужеземный?
attach 28107 %self.id%
~
#28107
колечко мне~
0 d 1
кольцо колечко~
дум
wait 2s
г И зачем оно тебе?
дум
wait 2s
if (%world.curobjs(28107)% < 1)
  г Ну раз приспичило... на, получай!
  %load% obj 28107
  дат кольцо %actor.name%
  г А теперь иди отсюда!
  detach 28106 %self.id%
  detach 28107 %self.id%
end
~
#28108
приветствие охранника~
0 q 80
~
wait 1s
г И кто это к нам явился? 
см %actor.name%
г Мда... И что тебе, %actor.name% здесь понадобилось?
г Ладно, если не будешь буянить - проходи!
~
#28109
огенноокий волхв приветствие~
0 q 100
~
wait 1s
груст
wait 1s
см %actor.name%
г Приветствую путник, что ищешь ты в этом месте?
wait 1s
г Предупреждаю, ни денег, ни приключений ты не найдешь.
эм на секунду задумался
wait 2s
г А вот смерть найти можешь.
wait 3s
взд
г Если б смог кто объяснить, чего же мне в жизни не хватает...
г Кто-то мудрый, жизнь познавший...
wait 2s
г Да где же такого найдешь...
печал
г Может ты сможешь?
вопрос %actor.name%
~
#28110
проверка на мудрость~
0 d 1
смогу да попробую~
if (%actor.wis% + %actor.wisadd%) <= 20
  г Ты еще слишком молод и наивен, зачем ты обманываешь меня?
  г Я не буду наказывать тебя за ложь, только иди отсюда поскорей.
else
  осм %actor.name%
  wait 2s
  set questor1 %actor%
  global questor1
  г Да, ты можешь мне помочь...
  взд
  г Так слушай же!
  wait 1s
  г Всю жизнь я посвятил изучению ведовства.
  г Ради достижения истинного знания мне пришлось отказаться от многого. Практически от всего.
  г Теперь же я чувствую, что достиг невероятной мощи. Я властен над стихией огня, я сам вобрал силу пламени.
  г Но почему же человеческое сердце мое грустит? Чего оно просит? 
  г Возможно во мне слишком мало от человека осталось, чтобы понять это.
  г Возможно ты достаточно мудр, чтобы объяснить мне это? 
  г Скажи, что еще нужно для счастья, кроме власти и могущества?
  attach 28111 %self.id%
  detach 28109 %self.id%
end
~
#28111
что нужно для счастья~
0 d 1
любовь девушка женщина любви девушки~
if %actor.id% != %questor1.id%
  г Мне %questor1% и без тебя поможет. Иди отсюда лучше.
  halt
end
г Ты думаешь? А где ж её взять, где найти?
г Знаешь, а приведи ка мне девицу-красавицу. Вдруг и влюблюсь.
г Давай, иди, а я в долгу не останусь.
attach 28112 %self.id%
attach 28113 %self.id%
detach 28110 %self.id%
detach 28111 %self.id%
~
#28112
приводим Векшу~
0 q 100
~
wait 1s
eval mobs %self.npc%
foreach mob %mobs%
  if (%mob.vnum% == 28120)
    вопр
    г Хм, какая красивая девушка...
    г И как ее зовут?
  end
done
~
#28113
Векша пришла~
0 d 1
Векша~
if %actor.id% != %questor1.id%
  г Я не тебя спрашиваю. Куда лезешь-то? Иди отсюда.
  halt
end
эм мечтательно улыбнулся
г Векша...
г Ладно, ты выполнил мою просьбу. Я не знаю влюблюсь ли я в неё, но она мне подходит.
г Ты останешься со мной, Векша?
wait 2s
%force% %world.mob(28120)% г Да, волхв, останусь.
%force% %world.mob(28120)% сму
%force% %world.mob(28120)% след я
%force% %world.mob(28120)% след волхв
г Ну что же %actor.name%, что хочешь ты от меня, знания или вещь волшебную?
attach 28114 %self.id%
attach 28115 %self.id%
detach 28112 %self.id%
detach 28113 %self.id%
~
#28114
вещь~
0 d 1
вещь~
wait 1s
г Сейчас посмотрим, что у меня есть
if %world.curobjs(28114) > 7
  г Извини, вещь я тебе дать не в силах... Может что-то другое?
  halt
end
mload obj 28114
дат клинок .%actor.name%
г Ну, %actor.name%, спасибо тебе за все! А теперь ступай с миром.
detach 28115 %self.id%
detach 28114 %self.id%
~
#28115
знание~
0 d 1
знание~
if %actor.int% <= 25
  г Извини, ты не сможешь постигнуть знание, ума у тебя маловато.
  ухм
  г Может что-нибудь другое подойдет?
  halt
end
г Ну что же, ты достаточно умен. Можно попробовать.
wait 2s
эм задумчиво хмыкнул
эм раскрыл Вам парочку сокровенных тайн мироздания
%actor.exp(+300000)%
detach 28114 %self.id%
detach 28115 %self.id%
~
#28116
зелье чтобы Векша пошла~
0 d 1
помоги Векше отвар зелье Векша~
if %actor.id% != %questortravnik.id%
  г А ты кто такой? Нет, уважаемый, я тебя не знаю.
  г И помогать тебе не буду. Иди ка ты отсюда лучше...
  эм показал направление движения
  halt
end
г Ты просишь помочь этой девке? Неужто влюбился?
г Ладно, раз обещал - помогу.
г Держи вот.
mload obj 28115
дат зелье %actor.iname%
detach 28116 %self.id%
~
#28117
приветствие скучающих~
0 q 100
~
wait 1s
эм посмотрела вокруг
взд
wait 2s
г Скучно здесь, грустно.
г Мир бы повидать, людей посмотреть.
жда
detach 28117 %self.id%
~
#28118
мобы одевают шапки~
0 bhis 100
~
if %world.curobjs(28119)% > 0
  if %self.haveobj(28113)%
    %purge% %world.obj(28113)%
    %load% obj 28120
    од все
  end
end
~
#28119
векша сразу не пойдет~
0 d 1
пойдем следуй пошли~
вопрос %actor.iname%
г С тобой?
if %actor.cha% <= 15
  г Что-то в тебе не то. То ли нос большой, то ли глаза недобрые.
  г Не пойду с тобой.
  halt
end
г Но не могу я сразу пойти. Беда у меня.
рыд
г Дура, я! Такое сотворила...
рыд
wait 1s
см %actor.iname%
г А может ты мне поможешь?
г Принеси мне зелье от травника и я пойду с тобой хоть на край света.
г Только он так просто не поможет...
attach 28120 %self.id%
~
#28120
Векше дали зелье~
0 j 100
~
wait 1s
if %object.vnum% != 28115
  г Не, это мне не нужно.
  брос все
  морщ
  halt
end
%purge% %object%
г О, ты мой спаситель!
эм зажмурилась
%echo% Векша глубоко вздохнула и выпила все зелье.
wait 1s
%echo% Векша сильно побледнела, но это все, что вам удалось заметить.
wait 2s
г Я готова.
след .%actor.name%
detach 28119 %self.id%
detach 28120 %self.id%
~
#28121
лоад кольца с трупа боярина~
0 f 100
~
if (%world.curobjs(28107)% < 1)
  if %exist.mob(28108)%
    %load% obj 28107
  end
end
~
#28122
Лоадим триг-стафинку~
2 z 0
~
%load% obj 28119
~
#28123
меняем шапку~
1 gij 100
~
if (%actor.vnum% != -1)
  halt
end
otransform 28120
~
#28124
Умер огненный демон (лоад отвара)~
0 f 100
~
if %random.100% < 4 && %world.curobjs(1753)% < 1
  %load% obj 1753
end
~
$~
